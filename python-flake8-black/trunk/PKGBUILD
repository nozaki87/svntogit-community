# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_name=flake8-black
pkgname=python-flake8-black
pkgver=0.2.4
pkgrel=1
pkgdesc='Flake8 plugin to call black as a code style validator'
arch=('any')
url=https://github.com/peterjc/flake8-black
license=('MIT')
depends=('flake8' 'python-black' 'python-toml')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('a7871bfd1cbff431a1fc91ba60ae154510c80f575e6b9a2bbb13dfb4650afd22')
b2sums=('5dc3e00958d68015b3dffc649a905c78f1db72bbd58ec938f4dc2445619d6ac3b04447debfd03a5140352d55faa29aef3e55828f38bc0a9b3203af979cd90993')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  source test-env/bin/activate
  python setup.py install --optimize=1 --skip-build
  cd tests
  ./run_tests.sh
  deactivate
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.rst
}

# vim:set ts=2 sw=2 et:
