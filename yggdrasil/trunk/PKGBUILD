# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=yggdrasil
pkgver=0.4.0
pkgrel=1
pkgdesc="An experiment in scalable routing as an encrypted IPv6 overlay network"
arch=('x86_64')
url="https://github.com/yggdrasil-network/yggdrasil-go"
license=('LGPL3')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yggdrasil-network/yggdrasil-go/archive/v${pkgver}.tar.gz"
        'yggdrasil.sysusers')
sha512sums=('06e138d04b075e207d66eed7c13653b572edd807bf8b49f60624b6cf876653e976eef1f2c196a2960cc9a1276edce43d1f0a1010c33394fd5a03a756a4a30224'
            'b78d1f5efeeba184588ba7bdb2249d976aec160daa59742e032983da1aedad062d15c7c97cba3eba69412a0f7904ee123d98b58f859892d71188c25624295c32')
b2sums=('3ed0b50e20a3ad041d0d4db441665b30fd0aa53cf2dabb426de81d25fbf13d7f83be07c89d0efe171a556a89fcd731e16371030c906603b608801e9b1be217a2'
        '3cf5bfb3c09908f841b39f7206bb472e50e60f10d1b87ab6def97a7212e27627f1e795bbaffd964d2d27a80fbbd1a3be63d63c59793ec1967c02f48b8462c08b')

build() {
  cd "${pkgname}-go-${pkgver}"
  PKGNAME="${pkgname}" PKGVER="${pkgver}" \
    CGO_LDFLAGS="${LDFLAGS}" \
    GOFLAGS="-trimpath -buildmode=pie -mod=readonly" \
    ./build -l "-linkmode external -extldflags \"${LDFLAGS}\""
}

package() {
  cd "${pkgname}-go-${pkgver}"
  install -Dm755 "yggdrasil" "${pkgdir}/usr/bin/yggdrasil"
  install -Dm755 "yggdrasilctl" "${pkgdir}/usr/bin/yggdrasilctl"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 -t "${pkgdir}/usr/lib/systemd/system" \
    contrib/systemd/yggdrasil.service \
    contrib/systemd/yggdrasil-default-config.service
  install -Dm644 "${srcdir}/yggdrasil.sysusers" "${pkgdir}/usr/lib/sysusers.d/yggdrasil.conf"
}

# vim: ts=2 sw=2 et:
