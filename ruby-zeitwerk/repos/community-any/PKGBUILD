# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.5.1
pkgrel=1
pkgdesc='Efficient and thread-safe code loader for Ruby'
arch=('any')
url="https://github.com/fxn/zeitwerk"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=('ruby-minitest' 'ruby-minitest-focus' 'ruby-minitest-reporters')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('4558a63f33d26f2aa05e5f6e417230902f14d4ade5bfb50cbd94d283b33e94e8486158f5b4c677c4dcd7b8bee51af165693593d303d1f07420fb5d4c72f918ea')
b2sums=('d8049a1adfaa6b278b59298e46d4697b70121d0a599a286f91720b31a06cccd39424367fe9c1ffbb90b0862a9501115f808bcc4fd36f3a09081987ad706f89bd')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
