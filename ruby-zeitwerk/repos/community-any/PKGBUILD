# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.4.2
pkgrel=2
pkgdesc='Efficient and thread-safe code loader for Ruby'
arch=('any')
url="https://github.com/fxn/zeitwerk"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=('ruby-minitest' 'ruby-minitest-focus' 'ruby-minitest-reporters')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('774cc17a8732afea0a854ea22ec860fa5bca65e8743aa800017c06a251dfdb1d2c5af8e541cb56d22a4202a9faa42707ed2ad395d9472360cf64f46ce9b907f2')
b2sums=('5913561057a6d5cd9bfa4eb549770ca18de3aafa1549a40e5315cd1d71a2dec5f865e7bc79f6ab2a098e515a930c5f8c83b85c91d7dbe441fa8dfb08ffdbdb03')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
