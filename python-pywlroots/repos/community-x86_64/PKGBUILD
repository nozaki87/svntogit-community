# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.15.17
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(custom:UIUC)
depends=(glibc python-cffi python-pywayland python-xkbcommon wlroots
libwlroots.so)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('01f51f8680a088ebff865ef5cf415b3a699657ec31837fd3e7940278738579d2b4e7490c48a723e4aef53ee9e6ccdd9c7dfecec2c43734000b9675263226850f')
b2sums=('7f3c2bb4ecc90f0a4148b2c2649527f1f660ee03f538ae78158c19511b90c0255444bfc9f398a1d2ccee664e27639cebd04d751720bc00ed438e89260a9424b2')

build() {
  cd $_name-$pkgver
  python wlroots/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
