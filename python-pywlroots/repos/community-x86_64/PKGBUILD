# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.14.3
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=('x86_64')
url="https://github.com/flacjacket/pywlroots"
license=('custom:UIUC')
depends=('glibc' 'python-cffi' 'python-pywayland' 'python-xkbcommon' 'wlroots'
'libwlroots.so')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
provides=('pywlroots')
conflicts=('pywlroots')
replaces=('pywlroots')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('74bc22f65ff03163a5ad4bbb921abef871d93b77bc154352951ab6c507fcf2b6caad4b0471ea52dde8842a9a5077e42b64d9464ae6971a364272b92dfbe3b008')
b2sums=('bda6a4952f8c3a2ae14675d8dd43faaeab939a4d75c5be250e43528c041edf3494874faccfe9428c9212f97f75b570df206baa214893d35e0dfd3e9cc03b895f')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python wlroots/ffi_build.py
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
