# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-httpbin
pkgver=1.0.1
pkgrel=1
pkgdesc='A py.test fixture for httpbining code'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/pytest-httpbin'
depends=('python-pytest' 'python-six' 'httpbin')
checkdepends=('python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/pytest-httpbin/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('3a11073d85478e7b748cfae5a7f639b2cf6ebc396ebb9972f39ead3d869ab1abaf0afa90f113e2393652ed08621716e54c9c5dcb41c59ff67341f7b28445544d'
            '949cb93164ce92e0a5acc184cca2da5580e31def2d153771ebfa386030fa2da65bcd48670780096d2cefe3e631dc755fc3024ae417f2d1bb3333bd85a39baf22')

build() {
  cd pytest-httpbin-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it
  # https://github.com/kevin1024/pytest-httpbin/issues/64

  cd pytest-httpbin-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest --deselect tests/test_server.py::test_redirect_location_is_https_for_secure_server
}

package() {
  cd pytest-httpbin-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
