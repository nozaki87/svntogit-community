# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.7.0
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools' 'cmake')
checkdepends=('python-pytest-runner')
source=("https://github.com/heitzmann/gdstk/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('74fcecec8ba3a45e871d00cc4dd2cebdeb985f61fb2aa5e85cfe1f847585dee92b30ef0e001a1f07de495da15277ecd2923aeec24dedc8b8f4b36c34b4b8216b')

build() {
  cd gdstk-$pkgver
  python setup.py build
}

check() {
  cd gdstk-$pkgver
  python setup.py pytest
}

package() {
  cd gdstk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
