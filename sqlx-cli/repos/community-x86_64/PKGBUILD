# -*- mode: Shell-script; eval: (setq indent-tabs-mode 't); eval: (setq tab-width 4) -*-
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.5.5
pkgrel=2
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('1e480bd05bfe41cbf88995f122b150c98c86306b9359a9bb88ad2a8848b2b7d2d84f161525cb46a94f6ffc3a721042531f482430aae88e1b3ed1c8c698eb1613')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 "target/release/cargo-sqlx" "$pkgdir/usr/bin/cargo-sqlx"
    install -Dm755 "target/release/sqlx" "$pkgdir/usr/bin/sqlx"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
