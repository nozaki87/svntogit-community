# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.5.7
pkgrel=1
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('98f255a92a9031de2b41788d65e5673eedbc06e99b8f7afb4565e073c351c37c65ad0a15f4983210ada5637cd486b27d9c365545137d918bf8831673720ee93f')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 "target/release/cargo-sqlx" "$pkgdir/usr/bin/cargo-sqlx"
    install -Dm755 "target/release/sqlx" "$pkgdir/usr/bin/sqlx"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
