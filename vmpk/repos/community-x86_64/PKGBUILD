# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.5
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libxcb' 'qt5-base' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'drumstick' 'qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('62de7acc353b95511c1c1afd54d7c5ddb34de69fd2dff5dfccd6ed83b6e1ca2cfdcd36c1a4878118821dead8dd8b15c44a2c1138b677da3b218f8093e7a49abf')
b2sums=('d13965358d21a81053cc5c32e11d0b8a9f6fda733ef5e7295cee7b774d8ca584ee58c0f7cd5f10f0f3948528031e314d09736fe3f7c0dc94929bcadd3aa94e5d')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libdrumstick-rt.so' 'libdrumstick-widgets.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
