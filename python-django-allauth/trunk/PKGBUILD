# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.45.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=('any')
url="https://github.com/pennersr/django-allauth"
license=('MIT')
depends=('python-django' 'python-openid' 'python-requests'
'python-requests-oauthlib' 'python-pyjwt')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('15c1dc77db7ec0f3223f64301b57fb21485db7c8abedf2dd34f78049f17e7c0c9aa434a207cfb2bd938ef52121aeb59ca3e01a8e728a3a54152540c6838bc40c')
b2sums=('1dea24d4760e0dc8230fd926c8360b67c675b9577cc8089c4cade40825d4a201108dff93c076872f3958f6bc8c73968f34dbef1ee87a8355aa449aa7f8b5b12b')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python manage.py test allauth
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
