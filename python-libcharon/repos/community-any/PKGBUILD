# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-libcharon
pkgver=4.11.0
pkgrel=1
pkgdesc="Python File metadata and streaming library"
url="https://github.com/Ultimaker/libCharon"
arch=(any)
license=("AGPL")
depends=('python' 'python-dbus')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(libcharon-$pkgver.tar.gz::https://github.com/Ultimaker/libCharon/archive/${pkgver}.tar.gz)
sha512sums=('e0541ad0194959e62d766a79433d4cb331f9c3350931fc75168ab3a235b054e1d4087041abf9bd11506e870ee58ac1e8dc9aeaf43480f134313dda62821c8915')

build() {
  cd libCharon-$pkgver
  python setup.py build
}

check() {
  cd libCharon-$pkgver
  pytest tests
}

package() {
  cd libCharon-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
