# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-libcharon
pkgver=4.10.0
pkgrel=1
pkgdesc="Python File metadata and streaming library"
url="https://github.com/Ultimaker/libCharon"
arch=(any)
license=("AGPL")
depends=('python' 'python-dbus')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(libcharon-$pkgver.tar.gz::https://github.com/Ultimaker/libCharon/archive/${pkgver}.tar.gz)
sha512sums=('f53bf6cc4b224613c35b1fca9e348c3df106555bb115e7ea301782cccdf271b286536f56f8c645f010c14648a2ed282b206d4e2f0f3acba6b147c0fb5c2b95c7')

build() {
  cd libCharon-$pkgver
  python setup.py build
}

check() {
  cd libCharon-$pkgver
  pytest tests
}

package() {
  cd libCharon-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
