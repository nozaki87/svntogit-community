# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.5.1
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=(x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL2)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(alsa-lib cmake doxygen docbook-xsl fluidsynth graphviz
libpulse qt5-tools)
provides=(libdrumstick-alsa.so libdrumstick-file.so libdrumstick-rt.so
libdrumstick-widgets.so)
options=(debug)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('bfa0f823c45cef85152f92b21a4e4998fd8e674c742bc2423c7b58559f83c68d327e3bc520217b348c19310dbf3ebbe39c89b160be9d3e8e6a95a99ca9d7c307')
b2sums=('70cf08bc9d616f265ac42379edeccd17a31ede490ace066ec4ca1635d042fae9b6c6698fbfa1be4c73878a674b6cd81b5a58c094d021db4e1e29556bf235cf9c')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(libasound.so libfluidsynth.so libpulse.so libpulse-simple.so)

  make DESTDIR="${pkgdir}/" install -C build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
