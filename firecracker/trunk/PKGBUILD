# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.0.0
pkgrel=1
pkgdesc="Secure and fast microVMs for serverless computing"
arch=('x86_64')
url="https://firecracker-microvm.github.io"
license=('Apache')
makedepends=('git' 'rust' 'rust-musl')
options=('!lto')
_commit='d3e98b9a4ae024de63f072c57015d8b7b0c1b061'
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#commit=$_commit")
b2sums=('SKIP')

_cargo_target="$CARCH-unknown-linux-musl"

pkgver() {
  cd "$pkgbase"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$_cargo_target"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$_cargo_target"
}

check() {
  cd "$pkgbase"

  cargo test -p firecracker --frozen
}

package_firecracker() {
  find "$pkgbase/target/$_cargo_target/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm0755 -t "$pkgdir/usr/bin/" {} +
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" *.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"
}
