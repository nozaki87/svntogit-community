# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.15.0
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz")
sha512sums=('1439b0a1f212500a25f87da841ad0cb00a910bd36ed57b888e77c9689a3b1c0443527dcb2d34f88049ba79f53161290fb639d3cfebf1783940dae8e735ac93c3')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
