# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: shmilee

_pkgname=mistune
pkgname=python-mistune
pkgver=2.0.0rc1
pkgrel=1
pkgdesc="A fast yet powerful Python Markdown parser with renderers and plugins"
arch=('any')
url="https://github.com/lepture/mistune"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lepture/mistune/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('02d75f72f992cf73e97ab52a531cc68db289beede8631aa51e836ea16eced1853f6120b21b1f06aff69698ec7ed5ea74282de849687a75aeb851e990f67614a0')
b2sums=('73440038e056eb610cf26783d49572e3687f716307534ec3e74e20f370276e480c1d140d78ddbddbb441fbada9ec604bedb3c34dd5019ee82d20cb529726d030')

build() {
  cd "${_pkgname}-$pkgver"
  python setup.py build
}

check() {
  cd "${_pkgname}-$pkgver"
  pytest -vv
}

package() {
  cd "${_pkgname}-$pkgver"
  python setup.py install --optimize=1 --root="$pkgdir/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
