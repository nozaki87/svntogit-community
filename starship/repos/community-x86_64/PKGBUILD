# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kevin Song <kevin.s05@gmail.com>

pkgname=starship
pkgdesc='The cross-shell prompt for astronauts'
pkgver=0.57.0
pkgrel=1
arch=(x86_64)
url=https://starship.rs/
license=(ISC)
depends=(
  gcc-libs
  glibc
  libgit2.so
  openssl
)
optdepends=(
  'noto-fonts-emoji: emoji support for terminals'
  'powerline-fonts: powerline symbols for terminals'
)
makedepends=(
  git
  rust
)
checkdepends=(python)
_tag=f03c0825c0d9f99789128692324855a7d5baecca
source=(git+https://github.com/starship/starship.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cargo fetch \
    --locked \
    --target $CARCH-unknown-linux-gnu \
    --manifest-path starship/Cargo.toml
}

pkgver() {
  cd starship
  git describe --tags | sed 's/^v//'
}

build() {
  export CARGO_TARGET_DIR=target
  cargo build \
    --release \
    --frozen \
    --features notify-rust \
    --manifest-path starship/Cargo.toml
}

check() {
  cargo test \
    --frozen \
    --features notify-rust \
    --manifest-path starship/Cargo.toml
}

package() {
  install -Dm 755 target/release/starship -t "${pkgdir}"/usr/bin/
  install -Dm 644 starship/LICENSE -t "${pkgdir}"/usr/share/licenses/starship/
  install -dm 755 "${pkgdir}"/usr/share/{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}/
  ./target/release/starship completions bash > "${pkgdir}"/usr/share/bash-completion/completions/starship
  ./target/release/starship completions fish > "${pkgdir}"/usr/share/fish/vendor_completions.d/starship.fish
  ./target/release/starship completions zsh > "${pkgdir}"/usr/share/zsh/site-functions/_starship
}

# vim: ts=2 sw=2 et:
