# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: zimbatm <zimbatm@zimbatm.com>
# Contributor: ShadowKyogre <shadowkyogre.public+aur@gmail.com>
# Contributor: rmorgans <rick.morgans@gmail.com>

pkgname=direnv
pkgver=2.30.3
pkgrel=1
pkgdesc='A shell extension that manages your environment'
arch=('x86_64')
url="https://direnv.net"
license=('MIT')
depends=('glibc')
makedepends=('go')
checkdepends=('zsh' 'fish' 'bash' 'tcsh' 'elvish')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/direnv/direnv/archive/v$pkgver.tar.gz")
sha512sums=('e71810ff37a0164f3d822ebf4fe46c5edd3a99493311ee58fef4a1fc814d07c3f663c5de73c7ddd27a0e17fd4a752a239fd52677d25fdde434387ab30111dc8f')
b2sums=('93cfcc5993dc71c300fc27b424cd2b570a2486921a98e2a0286f4af10e1131ac3991fa8c1205f6c85aa05479babf1e0d46f1b78c3406084e023fb5f77147ae9c')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  go mod download
}
build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS}" \
    .
}

check() {
  cd "$pkgname-$pkgver"

  # run go tests
  # temporarily skip badly implemented test
  rm -f internal/cmd/cmd_version_test.go
  go test -v ./...

  # run shell-specific tests
  bash ./test/direnv-test.bash
  elvish ./test/direnv-test.elv
  fish ./test/direnv-test.fish
  tcsh -e ./test/direnv-test.tcsh
  zsh ./test/direnv-test.zsh

  # run stdlib tests
  ./test/stdlib.bash
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/*.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/docs" docs/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
