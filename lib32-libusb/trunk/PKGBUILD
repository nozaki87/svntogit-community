# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

_name=libusb
pkgname=lib32-libusb
pkgver=1.0.25
pkgrel=2
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=(x86_64)
url="http://libusb.info/"
license=(LGPL2.1)
depends=(lib32-glibc lib32-systemd libusb=$pkgver)
makedepends=(gcc-multilib)
provides=(libusb-1.0.so)
options=(debug)
source=("https://github.com/${_name}/${_name}/releases/download/v$pkgver/${_name}-$pkgver.tar.bz2")
sha512sums=('f1e6e5577d4bd1ff136927dc66c615014a06ac332ddd797b1d1ad5f7b68e2405e66068dcb210e2f0ae3e31681603ef72efbd88bf7fbe0eb41ce700fdc3f92f9d')
b2sums=('3aecfbf9fc068fdfb8e612918bb895988f8400dc2c3ffd96a81792ab844d632f0c098947200512ddc37cfa9003956f8454c1d0362973a5d646a225f81cc2923b')

prepare() {
  cd "${_name}-${pkgver}"
  autoreconf -fiv
}

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package () {
  cd "${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
