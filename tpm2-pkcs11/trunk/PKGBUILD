# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-pkcs11
pkgver=1.6.0
pkgrel=1
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('libyaml' 'openssl' 'python' 'python-cryptography' 'python-pyasn1-modules' 'python-yaml' 'sqlite'
         'tpm2-tools' 'tpm2-tss' 'libtss2-esys.so' 'libtss2-fapi.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
makedepends=('cmocka' 'python-setuptools')
checkdepends=('expect' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc'
              'python-python-pkcs11' 'swtpm' 'tpm2-abrmd' 'tpm2-tss-engine' 'wget' 'xxd')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        "tpm2-pkcs11-1.6.0_test-fix-build-for-gcc11.patch::$url/commit/83c484d0b40fac4a6320cf0a16456da1764849bf.patch"
        "tpm2-pkcs11-1.6.0_fix-stringop-overread-in-str_padded_copy.patch::$url/commit/9af101b54035f25c0b03685bc34f22a4c5e934b7.patch")
sha512sums=('db94b2194837564fa330ac7cc15a5185258d5d632f033a60fa82a2b3246e046dac29b6dfece19b4e075383e999ac157286c7f18f6c5ab6a98dd01c8e810065ea'
            'SKIP'
            '538894af5f6aa713a9b73db8afe3a0de023c4337dc96d299b9a8368e51f0eb55883850e717abd1027f35cdd67989bfde4e1f90b85d8cb7069494d40a27616468'
            '8983f1046e72f0ebeced4004d91e27d12cd635889c6514afc07bd75749597ac6d9828ec0c7f4f47b16893c622fd7e54f3d2c8d05e4c07965ba0529daa458ecef')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix compilation with GCC 11
	patch --forward --strip=1 --input="$srcdir/tpm2-pkcs11-1.6.0_test-fix-build-for-gcc11.patch"
	patch --forward --strip=1 --input="$srcdir/tpm2-pkcs11-1.6.0_fix-stringop-overread-in-str_padded_copy.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr --enable-unit $( ((CHECKFUNC)) && echo --enable-integration)
	make
	cd tools
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
