# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.5.3
pkgrel=1
pkgdesc="Audio session manager"
arch=(x86_64)
url="https://github.com/jackaudio/new-session-manager"
license=(GPL3)
depends=(gcc-libs glibc)
makedepends=(fltk jack liblo meson)
optdepends=('fltk: for nsm-legacy-gui')
provides=(non-session-manager)
conflicts=(non-session-manager)
replaces=(non-session-manager)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('98c3eff5f24f759f7de5253d8d8c6951f3b199319a1339679ddd0fb5047af7cdd942867722bcc8799d4634d907fbe4ac02a5252a5011e73ae682a0636eba058e')
b2sums=('1abbba9b2497f8a1906b918426f28e173e2880f3d7823f8216c602e80efea5e2ca737a6a939870ddfa082f716b369dfb539cdb9f033275f71762422099deba9f')

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

package() {
  depends+=(libjack.so liblo.so)

  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
