# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=laz-perf
pkgver=1.3.0
epoch=2
pkgrel=1
pkgdesc="Alternative LAZ implementation for C++ and JavaScript"
arch=(any)
url="https://github.com/hobu/laz-perf"
license=('LGPL')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hobu/laz-perf/archive/${pkgver}.tar.gz")
sha512sums=('4e6e5197a90bf98fe6784a56e940070ad640ee2791443b9232960fd0f7639cc8d48d5c13df0cfc228e6be5fccd600afb1c171cb5e127bd874688e0ab231c25a8')

build() {
  cd laz-perf-$pkgver

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd laz-perf-$pkgver

  ninja -C build test
}

package() {
  cd laz-perf-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
