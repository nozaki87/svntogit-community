# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=laz-perf
pkgver=2.1.0
epoch=2
pkgrel=1
pkgdesc="Alternative LAZ implementation for C++ and JavaScript"
arch=(any)
url="https://github.com/hobu/laz-perf"
license=('LGPL')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hobu/laz-perf/archive/${pkgver}.tar.gz")
sha512sums=('1544f1270104ba2bddc1cfcbbaf56c5bdb8778f4d5e8b03b62ac33cb3e726f118c2ec3ebb3ef778ad907e037c150cdaf939ca05fe1880ef85d021fe35a4ed213')

build() {
  cd laz-perf-$pkgver

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd laz-perf-$pkgver

  ninja -C build test
}

package() {
  cd laz-perf-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
