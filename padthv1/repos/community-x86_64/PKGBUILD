# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.24
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=(x86_64)
url="https://padthv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base)
makedepends=(alsa-lib cmake fftw jack liblo lv2 qt6-tools)
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application'
            'qt6-wayland: for native wayland support')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('694991b5ed5772ca48f54e9c99b3f9d64f7a51675011f83b54b6411e4669a0a6e61a7ad954bea8c358f36a55318dce305dc39b8c7960f519a692fe32864fc502')
b2sums=('40db669997b2ca2b8f272a6529c0abfb17a6660390c1a7d0e19bcc71e58558f61b0f1e788b7a762f1fcca9b173af584e18256348e9da4d32496a94122ae03b86')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfftw3.so)

  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
