# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-nbval
pkgver=0.9.6
pkgrel=4
pkgdesc="A py.test plugin to validate Jupyter notebooks"
url="https://github.com/computationalmodelling/nbval"
license=('BSD')
arch=('any')
# python-six can be removed from dependencies in the next version
# https://github.com/computationalmodelling/nbval/pull/162
depends=('python-pytest' 'python-six' 'python-jupyter_client' 'jupyter-nbformat' 'python-ipykernel'
         'python-coverage')
makedepends=('python-setuptools')
checkdepends=('python-doit' 'python-matplotlib' 'python-pytest-cov' 'python-sympy')
source=("https://github.com/computationalmodelling/nbval/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "https://github.com/computationalmodelling/nbval/commit/2f4df51d37ed51f6042f6eb4b75f91b5865df48a.patch")
sha512sums=('ce2122ab490b52537696b2a8e97fd3b604c547af93fccf7d83d82b9180d8bb50b57d002f4e16eb605acabab5b31e961fea97bf2d46bf45b9cd1f8b7fca028df2'
            'd315b38d0267a3488e53e46daeff04374fe3c5f9e5c8dc0c165cb8e7d2644fa8ecea2fc4642de0ce7ed0687bcec12317981bac1590cb62ffc06483928579f58e')

prepare() {
  cd nbval-$pkgver
  # Fix test with newer Python https://github.com/computationalmodelling/nbval/pull/178
  patch -Np1 -i ../2f4df51d37ed51f6042f6eb4b75f91b5865df48a.patch
}

build() {
  cd nbval-$pkgver
  python setup.py build
}

check() {
  cd nbval-$pkgver
  python setup.py egg_info

  PYTHONPATH="$PWD" doit test
}

package() {
  cd nbval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
