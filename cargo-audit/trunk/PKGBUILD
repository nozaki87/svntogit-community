# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.15.2
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ed330d33f86036acd27ab8f717903aa515c306d02217aa217c95e2a5fdab1f8e')
b2sums=('50d3a8c3982028ac17ce3c72a4a8038bbacd954a4958b7950f711f62499824e255f2991c448f3604557f1894d8b24daa6e41848f6952d951d48426a0a9e80bb5')

build() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo build --release --locked
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 ${pkgname}/LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
