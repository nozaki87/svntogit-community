# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.15.0
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('af5a7d7c681d1956433ec4bc239bfb2c24df90731ae514a7cd3ca909562f9855')
b2sums=('c31b76aaeb0f8260639c8ca287f491d093591e51b0edfc96ad46c820df37f7505119cc3d4b13ce4bed6f32d4241068f1c69085cdf53fcfd38e943d1ad537fd39')

build() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo build --release --locked
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 ${pkgname}/LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
