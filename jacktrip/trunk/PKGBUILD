# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.4.0
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=('x86_64')
url="https://github.com/jacktrip/jacktrip"
license=('LGPL3' 'GPL3' 'MIT')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('help2man' 'meson' 'jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6885b35eb3707779f494cf515ee2bac1ba4d6bbfa7358022d75f9d0147c9fda0d349691b839d9084a2a3106dceeeb492549ea92274a8c67b9f34524048215cc6')
b2sums=('13451d0624362c12fb9c72954546d8857bd4b9fafc6efb1ebf48e2688fd19ae8bd6062e32b7a062a2effd716133019aff0c9fb1ac063d67c886e6f00ce106b69')

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -fv build
}

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson -Drtaudio=disabled build
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  ninja -C build test
}
package() {
  depends+=('libjack.so')

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build

  # licenses
  install -vDm 644 LICENSE.md LICENSES/MIT.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
