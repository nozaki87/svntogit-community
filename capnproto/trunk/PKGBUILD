# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributors: Dave Reisner <dreisner@archlinux.org>
#               Matthias Blaicher <matthias@blaicher.com>
#               Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=0.9.0
pkgrel=1
pkgdesc="Cap'n Proto serialization/RPC system"
arch=('x86_64')
url='https://capnproto.org/'
license=('MIT')
depends=('gcc-libs' 'glibc' 'openssl' 'zlib')
makedepends=('cmake')
provides=('libcapnp.so' 'libcapnp-json.so' 'libcapnp-rpc.so' 'libcapnpc.so'
'libkj-async.so' 'libkj-gzip.so' 'libkj-http.so' 'libkj-test.so' 'libkj-tls.so'
'libkj.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('d50d8bb733e84d94a37065eeab1695e0053d0199aa529f6616df296d644ee6004cc0e3f10974ff95e95355fa4b2d5cee2108d1e503c0ffc4333e67d15fbdb1bc')
b2sums=('0fba946ee510322236b5f4e3b7c3dd23358a36f090cad527163c24e4d8575565b90bf457a112331afd84fb5a15684d1f31946432dc7ccbc35060bfcdd169e8e6')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CONTRIBUTORS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
