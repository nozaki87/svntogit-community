# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: ChaosKid42 <christoph.scholz@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

_name=rugged
# ideally this should match $pkgver, as package() might fail otherwise. thanks, github
_libgit2_pkgver=1.1.1
pkgname=ruby-rugged
pkgver=1.1.1
pkgrel=1
pkgdesc='A Ruby binding to the libgit2 linkable library'
arch=('x86_64')
url="https://github.com/libgit2/rugged"
license=('MIT')
depends=('glibc' 'ruby')
makedepends=('libgit2' 'ruby-minitest' 'ruby-rake' 'ruby-rake-compiler' 'ruby-rdoc')
checkdepends=('git')
options=(!emptydirs)
source=("${_name}-${pkgver}.tar.gz::https://github.com/libgit2/${_name}/archive/v${pkgver}.tar.gz"
        "libgit2-${_libgit2_pkgver}.tar.gz::https://github.com/libgit2/libgit2/archive/v${_libgit2_pkgver}.tar.gz")
sha512sums=('318de6d64ddcd714ab8ffbce53b6d38a8a8c2f44e5acefd13901a62b01a369536d1dd3c4f875c303e5e143f012b88fbc42dd6028cc2674a777a41fad0faeeddb'
            '287255c81e5baa04d8fbce95efca8174cb34da8025cab979445d41e8361955f1d8c582d7705666ae9fc03c5215fc4f4f57e51d73d14ca9b063ed94077f88e6bc')
b2sums=('8f3ff3b7b0c425ec5a366c5144cc31f6dbbdcc0e40bde3c7515af1ff0554aceb11cedc271e5104646bcc15b352ab76a044a8245abbaa5d276bd56468b16949e6'
        '46842ea270fc457d0960c1a825928eb2747b236e689ff785e9fc2bce919744d1da95a89e6fcf928bbe6a4981e2ff6a02989df92026ba181f2743e949982a18df')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec
  # remove broken and useless libgit2 "version check":
  # https://github.com/libgit2/rugged/issues/698
  sed '68,90d' -i ext/rugged/extconf.rb
  # source tarball doesn't include vendored libgit2 version, which is only
  # needed for test fixtures (resources): https://github.com/libgit2/rugged/issues/801
  mv -v ../libgit2-${_libgit2_pkgver}/* vendor/libgit2
}

build() {
  cd "${pkgname}-${pkgver}"
  export CI_BUILD=true
  export RUGGED_USE_SYSTEM_LIBRARIES=true
  export CMAKE_FLAGS=" --use-system-libraries"
  rake compile
  rake gem
}

check(){
  cd "${pkgname}-${pkgver}"
  # ConfigTest#test_read_global_config_file fails, if no gitconfig is set for
  # user...
  # https://github.com/libgit2/rugged/issues/432
  git config --global user.name "Foo Bar"
  rake test
}

package() {
  local _gemdir="$(gem env gemdir)"

  depends+=('libgit2.so')

  cd ${pkgname}-${pkgver}
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              pkg/${_name}-${pkgver}.gem
  install -vDm 644 "$pkgdir/$_gemdir/gems/$_name-$pkgver/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md CHANGELOG.md -t \
    "${pkgdir}/usr/share/doc/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache \
    "doc/${_name}-${pkgver}/ri/ext/" \
    "gems/${_name}-${pkgver}/"{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out} \
    gems/${_name}-${pkgver}/vendor
}
