# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.2.2
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python-zipp')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7af9aceece4dd25ad827310fde100a4b0b5e0aee03dfeba8789db060e4decafc543784046c2e9eee95d8cab61845c50e4bb0543bb20ae7e1e480aaa80c88fa27')
b2sums=('dde545a3f104c799432d7a2b6dbea6f071b82d0b6e3660384e2e0ab3c36eee8942b9f248a6139c708020c617bf0e0dce7fbbd5e06cb0bd192a0296fac6350b51')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
