# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Nikita Volodin <volodin.n at gmail dot com>

pkgname=keycloak
pkgver=15.0.2
_postgresql_jdbc=42.2.23
_java=11
pkgrel=1
pkgdesc="Open Source Identity and Access Management For Modern Applications and Services"
arch=('any')
url="https://www.keycloak.org/"
license=('Apache')
depends=("java-runtime=${_java}" 'grep' 'bash' 'coreutils' 'util-linux')
makedepends=('maven' 'git' "java-environment=${_java}")
backup=(
  opt/keycloak/standalone/configuration/application-roles.properties
  opt/keycloak/standalone/configuration/application-users.properties
  opt/keycloak/standalone/configuration/logging.properties
  opt/keycloak/standalone/configuration/mgmt-groups.properties
  opt/keycloak/standalone/configuration/mgmt-users.properties
  opt/keycloak/standalone/configuration/standalone-ha.xml
  opt/keycloak/standalone/configuration/standalone.xml
)
options=(emptydirs)
source=(https://github.com/keycloak/keycloak/archive/$pkgver/$pkgname-$pkgver.tar.gz
        keycloak.service
        keycloak.sysusers
        keycloak.tmpfiles
        layers.conf
        postgresql-module.xml
        https://jdbc.postgresql.org/download/postgresql-${_postgresql_jdbc}.jar
        pin-java-version.patch)
sha512sums=('76d4e83e32b81b0b55c67eab669df5944861331a370b07297b8271c1ff7122f6d21eac0e4b7a32a621d4a4f4d9d5a17d6300e957ce867f4219883f1185d61cee'
            'dd10a38e4c8867de6d1fcbb4939c6c5946684842462eba3d5355bf1de13fa059edfd5eba37adce587c824ddd618b6bb88d4bbe1ee568ca5abd0f72296da3ddfe'
            'c53b734598ccf675930754514bd2af628355025197e77541e5acb8718629a8ece763c814e8373278758a30475e6a716febeff10bdebf0847d914bc193e732c79'
            '9382b42f09040e34fe1a27d9ff4fb87cabd41525ca26f8ea9629df0169cf93d95cc4bb62bcc5c2ca30efdfd7eeeb13a6f090df9a05823baf701ba729a47f6fd1'
            'd54a04c120edb933c4f7168460bd813ff92a4e3771cedcabfeeb76c5d3e057b3cd0d0486dc11ddbd91e8d87ca90193e4c74cc3414e45106dd556d455ac7e60b2'
            '5eece4f3eb57372969f0e5319804198b546455828e938ff7f37bbcfb58be08efde43a5b0376c98f359c4bc5eb069e0cd9410daefe19da1c03e9e9c57d3c1b75b'
            'b68480b04dfd352e7b4655949a85eefea88bb6d1ecddfccb73fd5513136716f711c006ef2e104524d24bc608d83f63d54f474df65695c5c83a364a1d9b092aa8'
            '55a9f861cf5be2bdc64a4b831fbd4d832fae5b88c8c7d620b6ee0622602cce600124e571fff9847b5eb492bf9d522a9ead4179a60d0f90f4f1f70b1d530bde66')

prepare() {
  cd $pkgname-$pkgver
  # Pin the java version. Keycloak produces all those shell scripts deep down
  # the rabbit hole so applying them during packaging is the best we can get.
  # Create a local copy and dynamically adjust the patch.
  cp ../pin-java-version.patch .
  sed "s|java-11-openjdk|java-${_java}-openjdk|g" -i pin-java-version.patch
  sed "s|14.0.0|${pkgver}|g" -i pin-java-version.patch
}

build() {
  cd $pkgname-$pkgver

  export PATH="/usr/lib/jvm/java-${_java}-openjdk/bin:$PATH"
  mvn -Pdistribution \
    -pl distribution/server-dist \
    -am \
    -Dmaven.test.skip \
    clean install
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/opt"
  cp -r "distribution/server-dist/target/keycloak-$pkgver" "$pkgdir/opt/keycloak"
  install -Dm 644 "$srcdir"/layers.conf -t "$pkgdir/opt/keycloak/modules"
  install -d "$pkgdir"/opt/keycloak/standalone/{data,log}

  install -Dm644 "$srcdir"/postgresql-${_postgresql_jdbc}.jar "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/postgresql-${_postgresql_jdbc}.jar
  sed "s/@JDBC_POSTGRESQL_JAR@/postgresql-${_postgresql_jdbc}.jar/g" "$srcdir"/postgresql-module.xml > "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/module.xml

  install -d "$pkgdir/"{etc,var/log,var/lib/keycloak}
  ln -s /opt/keycloak/standalone/configuration "$pkgdir/etc/keycloak"
  ln -s /opt/keycloak/standalone/log "$pkgdir/var/log/keycloak"
  ln -s /opt/keycloak/standalone/deployments "$pkgdir/var/lib/keycloak/deployments"

  install -Dm644 "$srcdir"/keycloak.service "$pkgdir"/usr/lib/systemd/system/keycloak.service
  install -Dm644 "$srcdir"/keycloak.sysusers "$pkgdir"/usr/lib/sysusers.d/keycloak.conf
  install -Dm644 "$srcdir"/keycloak.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/keycloak.conf

  mkdir -p "$pkgdir"/usr/bin
  ln -s /opt/keycloak/bin/kcadm.sh "$pkgdir"/usr/bin/kcadm
  ln -s /opt/keycloak/bin/kcreg.sh "$pkgdir"/usr/bin/kcreg

  patch -Np1 -d "$pkgdir"/opt/keycloak/bin < pin-java-version.patch
}

# vim: ts=2 sw=2 et:
