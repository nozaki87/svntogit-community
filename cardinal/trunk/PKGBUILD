# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=cardinal
pkgname=(cardinal cardinal-data cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)
pkgver=22.02
pkgrel=4
pkgdesc="Virtual modular synthesizer"
arch=(x86_64)
url="https://github.com/DISTRHO/Cardinal"
license=(GPL3)
makedepends=(dbus file gcc-libs gendesk git glibc jansson libarchive libglvnd
liblo libsndfile libsamplerate libx11 libxext libxrandr mold python speexdsp)
# lv2lint needs the additional lv2-extensions from mod-sdk:
# https://github.com/moddevices/mod-sdk/blob/master/mod.lv2/mod.ttl#L174
# checkdepends=(kxstudio-lv2-extensions lv2 lv2lint xorg-server-xvfb)
options=(debug)
source=(
  https://github.com/DISTRHO/$pkgbase/releases/download/$pkgver/$pkgbase-$pkgver.tar.xz
  $pkgbase.svg::https://github.com/DISTRHO/Cardinal/blob/eb95b5990cf734c10f2caab1a246eac893f0266d/plugins/Cardinal/orig/distrho.svg
  $pkgbase-22.02-missing_symlinks.patch::https://github.com/DISTRHO/Cardinal/commit/13e9ef37c5dd35d77a54b1cb006767be7a72ac69.patch
  $pkgbase-22.02-remove_glibcxx_assertions.patch::https://github.com/DISTRHO/Cardinal/compare/d6823af74d3bd1940b1fc2f3c43f0d84f0656a79...79698d1443cfad232413eba61a1e7339d087b238.patch
)
sha512sums=('9ec540366f61437f5b7103a08fa7865bfa7e17e92757c3a742ba950eeb6483688be3335fe4ebf830c76a70c18789b698f9121762312aa7cfb3da629730268fe7'
            'e2b68f76aba6847e7fba7ae087f7019dfa129d685a4ffe8af41c69db295603dd03c8fe65deb024f7f3f956253d7dd6bd2c38fdd0a9c61f35c11df9df5e42659a'
            '8a84b3fd09d1e3ba401f2f2dd0dd9771520d3579074f06209a5c204feaf244d71d2042d450becfa1dc1918171a0158ba6593b22f66693c0499feb3fa54d28c75'
            '17be05ffb3b5807016c5330e45b1e49e20480bc98d6396e46bf9d355a45a7905114b951c30cdb413f6d3d15c0d6e16350dcf3e357c4c988c147f5272b91c8e98')
b2sums=('0b4c2d2ee7cb3b191df25b6f5643db1c923c0d74515f4c2c03665ce16b38c5c11968096b1e6d330bf64f99419039669ebba14ce0a29fafde32d9e07ec4b42c90'
        '4dc447e473f30714316a0c6d0e385b3c39145258a13cb7dae83b54a1c8118d3090972505b490bbe97c00171ed9954ab23f5bc18e6a6f4f28929d69dd89e042b4'
        '2d76b09441b0499e601d375f4a2a4bd454f2a3c2c365e5675e25b6be51cefbc1366fe95f02d299c04fd26c726eac63339786ca699301e73a6b7bef211a30f2b9'
        '53b6b8fb12ae32e1be4a169afcb5b2b8cec232a063e1445a1852523dbb9bf37b0f49a4a4e542927bd3f543653de80f201bacc796f8d46d545447b928aee57521')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  gendesk -n \
          --exec Cardinal \
          --name Cardinal \
          --pkgname studio.kx.distrho.Cardinal \
          --pkgdesc "$pkgdesc JACK standalone" \
          --icon $pkgbase \
          --genericname "Virtual modular synthesizer"

  # add missing symlinks for resources
  patch -d $pkgname-$pkgver -p1 -i ../$pkgbase-22.02-missing_symlinks.patch
  # undo the use of -Wp,-D_GLIBCXX_ASSERTIONS as it crashes hosts:
  # https://github.com/DISTRHO/Cardinal/issues/111
  patch -d $pkgname-$pkgver -p1 -i ../$pkgbase-22.02-remove_glibcxx_assertions.patch
}

build() {
  export CFLAGS+=" -B/usr/lib/mold"
  export CXXFLAGS+=" -B/usr/lib/mold"
  make PREFIX=/usr SYSDEPS=true WITH_LTO=true -C $pkgname-$pkgver
}

# check() {
#   declare -A _links=(
#     ["Cardinal"]="https://distrho.kx.studio/plugins/cardinal"
#     ["CardinalFX"]="https://distrho.kx.studio/plugins/cardinal#fx"
#     ["CardinalSynth"]="https://distrho.kx.studio/plugins/cardinal#synth"
#   )
# 
#   for _name in Cardinal{,FX,Synth}; do
#     xvfb-run lv2lint -s "lv2_generate_ttl" -Mpack -I $pkgname-$pkgver/bin/$_name.lv2 "${_links[$_name]}"
#   done
# }

package_cardinal() {
  depends=(cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)

  make PREFIX=/usr SYSDEPS=true DESTDIR="$pkgdir" install -C $pkgbase-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-data usr/share/$pkgbase
    _pick $pkgbase-data usr/share/doc
    _pick $pkgbase-jack usr/bin/
    _pick $pkgbase-lv2 usr/lib/lv2/
    _pick $pkgbase-vst usr/lib/vst/
    _pick $pkgbase-vst3 usr/lib/vst3/
  )
}

package_cardinal-data() {
  pkgdesc+=" - data"

  mv -v $pkgbase-data/* "$pkgdir"
}

package_cardinal-jack() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(cardinal-data gcc-libs glibc jack jansson libglvnd libx11 libxext
  libxrandr speexdsp libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-jack/* "$pkgdir"
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications"
  install -vDm 644 $pkgbase.svg -t "$pkgdir/usr/share/icons/hicolor/scalable/apps"
}

package_cardinal-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(lv2-plugins pro-audio)
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr lv2-host speexdsp libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-lv2/* "$pkgdir"
}

package_cardinal-vst() {
  pkgdesc+=" - VST2 plugin"
  groups=(pro-audio vst-plugins)
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr speexdsp vst-host libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-vst/* "$pkgdir"
}

package_cardinal-vst3() {
  pkgdesc+=" - VST3 plugin"
  groups=(pro-audio vst3-plugins)
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr speexdsp vst3-host libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-vst3/* "$pkgdir"
}
