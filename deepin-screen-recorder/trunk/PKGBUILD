# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.10.2
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-turbo' 'ffmpeg' 'libxtst')
makedepends=('deepin-dock')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('975e8f3e19854df5d78f3b3819931eef0dea891362e5af03661d28e72a5707869cbf6fe6f3eab03a0520595f35c325cd5150ccd28efe0eee7c64d96f724d0f4c')

prepare() {
  cd deepin-screen-recorder-$pkgver

  # X11 header's weirdness with GCC 10
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
  sed -i '/#include <iostream>/d;1i #include <iostream>' src/screen_shot_event.cpp
  sed -i '/include <X11.extensions.shape.h>/a #undef None' src/utils.cpp
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
