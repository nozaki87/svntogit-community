# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wails
pkgver=1.16.8
pkgrel=1
pkgdesc="Create desktop apps using Go and Web Technologies"
arch=('x86_64')
url="https://github.com/wailsapp/wails"
license=(MIT)
depends=(glibc)
makedepends=(go webkit2gtk gtk3)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wailsapp/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('56b648f5c92ffe3ebd0088cb40a65d00c03100097ad3bb53b7904477e53fe78ad6f5497194eefc06140a4a26bc6743dd1faa47dce157db13e2edfff38ce12e47')
b2sums=('8732265d57de92803725fd7bbdc89c4d4e014474a9670cab6319aee6ab4b80f80a9a5b6acdf3bf0af8df464c12487c98a0c54ba323eb1f108b9cd96b066bf79a')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/ ./cmd/wails
}

check() {
  cd "$pkgname-$pkgver"
  go test
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "build/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
