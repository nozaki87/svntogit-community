# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.18.17
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=('x86_64')
url="https://github.com/pymupdf/PyMuPDF"
license=('AGPL3')
depends=('glibc' 'gumbo-parser' 'jbig2dec' 'libmupdf' 'openjpeg2' 'python')
makedepends=('freetype2' 'libjpeg-turbo' 'python-distro' 'python-setuptools' 'swig' 'systemd')
checkdepends=('python-pillow' 'python-pytest')
# pypi sdist tarball does not have tests
# https://github.com/pymupdf/PyMuPDF/issues/1227
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('fc1f993611579b33d48be086e9966b6e45e4811973cad250905ca17e3b8a79a2fb885587997248fae0cf2de3bd527cdb44190402338de156e64bd9ea31737eb9')
b2sums=('1cee464f75ef6240e5353edea11ee67194061acb9a25ddaeedd87e071217f68bd349be5a01038b724d77b2e6f6376189a7132d61bfd4bc4e51f9552ca4f21fa6')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"
  cd "$pkgname-$pkgver"
  mkdir -vp "${_test_dir}"
  # install to test dir for testing
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${_test_dir}"

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  pytest -v -k "not test_font1 and not test_pageids and not test_textbox3"
}

package() {
  depends+=('libfreetype.so' 'libjpeg.so')
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  rm -rv "${pkgdir}/usr/README.md"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
