# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>

pkgname=istio
pkgver=1.11.4
pkgrel=1
pkgdesc='Istio configuration command line utility for service operators to debug and diagnose their Istio mesh.'
arch=('x86_64')
url='https://github.com/istio/istio'
license=('Apache')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/istio/istio/archive/$pkgver.tar.gz")
sha512sums=('64e6448b830fe6e199758f0ba156115018e8a8cbb8ca43f8ba0777e2ee70a2334b939fa98068d4e7439177868f8264ff91cf9d19cc47a346e716ca04585bf828')
b2sums=('02699f7bc878a3a88038e77e4fa65f16542ad175b12daa49f27d63a9a7a088fa96ef9c41ddd38eab2144f4150c07ddedef9d74ae29462756c2e5cc4e48f0f9dc')

build() {
  cd $pkgname-$pkgver
  export VERSION=$pkgver
  export TAG=$pkgver
  export BUILD_WITH_CONTAINER=0
  make build
}

package() {
  install -Dm 755 "${srcdir}/$pkgname-$pkgver/out/linux_amd64/istioctl" "${pkgdir}/usr/bin/istioctl"

  # Populate bash and zsh completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions"
  install -dm 755 "${pkgdir}/usr/share/zsh/site-functions"
  "${pkgdir}/usr/bin/istioctl" collateral --bash
  mv istioctl.bash "${pkgdir}/usr/share/bash-completion/completions/istioctl"
  "${pkgdir}/usr/bin/istioctl" collateral --zsh
  mv _istioctl  "${pkgdir}/usr/share/zsh/site-functions/_istioctl"
}
