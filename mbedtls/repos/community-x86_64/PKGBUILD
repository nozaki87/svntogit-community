# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.27.0
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc' 'sh')
checkdepends=('python')
makedepends=('cmake')
provides=('polarssl' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so')
replaces=('polarssl')
conflicts=('polarssl')
options=('staticlibs')
source=(https://github.com/ARMmbed/mbedtls/archive/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4f6a43f06ded62aa20ef582436a39b65902e1126cbbe2fb17f394e9e9a552767')
b2sums=('31840e5a518643495f319cac8e0c876bdca5dbdee6524d30cace3538d5fb5ba7847dd653f61f775f5c3c384ac0533afdbf6172b3e523db515eceecc29fadd2da')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h
}

build() {
  cd "$pkgname-$pkgname-$pkgver"
  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMBEDTLS_FATAL_WARNINGS=OFF \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DUSE_SHARED_MBEDTLS_LIBRARY=ON \
	-DCMAKE_INSTALL_RPATH=
  make -C build
}

check() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build test
  # make check?
}

package() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir" install

  # rename generic utils
  local _prog _baseprog
  for _prog in "$pkgdir"/usr/bin/*; do
	_baseprog=$(basename "$_prog")
    mv -v "$_prog" "${_prog//$_baseprog/mbedtls_$_baseprog}"
  done

  # fixup static lib permissions
  chmod 644 "$pkgdir"/usr/lib/*.a
}
