# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.2.0
pkgrel=2
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('8366569f27a62913dade9935e0688b3636ca11b8ee00a061c9fdcbb6b27f3e2f')
b2sums=('e1c3ffc6f88ef4a6c5b2f3e03838448540baeeacce3029a039b2fae1804c79193f9ee0bb64d9187f4b2f7f902f47211dc5c5fb185e4f2fcf94513bfe2f0913bf')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # thou shalt not -Werror
    sed -i 's/--warn-error//g' src/Makefile.am

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/muffin \
                --localstatedir=/var \
                --disable-gtk-doc \
                --disable-static \
                --disable-schemas-compile \
                --enable-compile-warnings=minimum

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool clutter/libtool cogl/libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Remove unused stuff
    make -C src DESTDIR="${pkgdir}" uninstall-binPROGRAMS uninstall-desktopfilesDATA
    make -C src/tools DESTDIR="${pkgdir}" uninstall
    make -C src/compositor/plugins DESTDIR="${pkgdir}" uninstall
    make -C doc/man DESTDIR="${pkgdir}" uninstall
}
