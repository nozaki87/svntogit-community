# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.33.1
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("ncurses" "musl" "kernel-headers-musl")
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B')
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"{,.sig}
        "config")
sha256sums=('12cec6bd2b16d8a9446dd16130f2b92982f1819f6e1c5f5887b6db03f5660d28'
            'SKIP'
            '21ce3115a6f3e8e4378bb0769efb8f079392a8639d8d6aae009266248e495e80')
b2sums=('2dce3427ab7703c56cbb3bdc0c93c1fe1c6dc24c5b467b6213b8f9cf55223ec63136b3837970e41293cc3d55d320599945d29a69a4ef8b4b3ab9fb5e2a527632'
        'SKIP'
        '9c14c0f830830f0f5d598198c665047e297c4d17962455b7fd253a51e340ea3757216b965cfbd3a6c03acf7b9429e5ab42f1e0bcfb13e9b5cfe89f49ab45368a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config .config
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make CC=musl-gcc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0755 busybox "$pkgdir"/usr/bin/busybox

  # docs
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
