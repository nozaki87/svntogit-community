# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sybil
pkgver=3.0.0
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/cjw296/sybil/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a23734a223a875ad38e86023e3d3f3ef7d6873ad5a9d2bd8d74eb249f848809af5806e80935d856c271a9d41d35a3956394ed1f7fc3f66ff89e4484966c91ead')

build() {
  cd sybil-$pkgver
  python setup.py build
}

check() {
  cd sybil-$pkgver
  python -m pytest
}

package() {
  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
