# Maintainer: David Runge <dvzrv@archlinux.org>

_name=doit
pkgname=python-doit
pkgver=0.34.2
pkgrel=1
pkgdesc="A task management and automation tool"
arch=(any)
url="https://pydoit.org/"
license=(MIT)
depends=(python-cloudpickle python-pyinotify)
makedepends=(python-setuptools)
checkdepends=(python-doit-py python-pyflakes python-pytest)
optdepends=(
  'python-toml: for TOML support'
  'python-setuptools: for plugin support'
  'bash-completion: autocomplete for bash'
  'zsh-completions: autocomplete for ZSH'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('24afc8078fef42a446733554c6ab63298ee5bb734381fc2bfc9d623dc98394916a1f89a18bb28e7ef862da8c0449b5cf16d605295e8948a0ea6968a231a1c8e2')
b2sums=('c7d81144da99147dbc6bf7d670b6f411879a8080e405866866bc97ae4701d9a47a3d4882db9672e2e36bc8af577ba9632da1fb4ed2c9f87717f719ac1cd53d34')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python setup.py install --optimize=1 --root="$pkgdir"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 bash_completion_$_name "$pkgdir/usr/share/bash-completion/completions/$_name"
  install -vDm 644 zsh_completion_$_name "$pkgdir/usr/share/zsh/site-functions/_$_name"
}
