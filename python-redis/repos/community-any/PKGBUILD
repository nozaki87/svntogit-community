# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgname=python-redis
pkgver=4.0.0
pkgrel=1
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="https://github.com/andymccurdy/redis-py"
license=('MIT')
depends=('python-deprecated')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('5975407b427d9edc87903aeb015e3f62a88bcc417be9fc91de56f852eed7d1bad9cd865ba40540a7c950b1a4e9bdfde20d6d7c4616ed859b6520adcca8085ea9')

build() {
  cd "$srcdir"/redis-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/redis-$pkgver
  pifpaf run redis py.test || echo "https://github.com/andymccurdy/redis-py/issues/1073"
}

package() {
  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
