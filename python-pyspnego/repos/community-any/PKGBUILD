# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyspnego
pkgname=python-${_pkg}
pkgver=0.1.6
pkgrel=1
pkgdesc="SPNEGO Authentication library"
arch=(any)
url="https://github.com/jborean93/pyspnego"
license=(MIT)
depends=(python-cryptography)
makedepends=(python-setuptools)
checkdepends=(cython python-pytest python-pytest-mock python-gssapi python-ruamel-yaml)
optdepends=('python-gssapi: Kerberos support'
            'python-ruamel-yaml: YAML configuration')
# No tests in pypi tarball
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2f0d35d85bf7178fbcf58cb955505c20dca31b047ed092e6f8d3265730eabdd8')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -v
}

package() {
  cd ${_pkg}-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
