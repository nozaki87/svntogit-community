# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.9.24
pkgrel=1
pkgdesc="Old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=(x86_64)
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base)
makedepends=(alsa-lib cmake jack liblo lv2 qt6-tools)
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application'
            'qt6-wayland: for native wayland support')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ef465c17d2a32b257525bfcb4d74bbf39582d48008626e059ce2ac6fc5abeeb04a5ef475137f60da21565578c7ff73e2135a48fd116bae73eedb65e2293093eb')
b2sums=('ddcc0ddce19196fe7b6fc39939cb8edf2d90e3701af4f20bc16fa6d6c1f1cb85c5c3d1783a2ea2ec6b9953ede04e02f627bdaedafc1bd3d0474d63a75bfe37e4')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
