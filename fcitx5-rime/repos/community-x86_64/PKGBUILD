# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-rime
pkgver=5.0.7
pkgrel=1
pkgdesc="RIME support for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-rime"
license=('GPL')
depends=('fcitx5' 'librime')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('9be0f4c4eedda0bd1b66cd100581be7d5c6f3a67469c4d5482e4d2be5c609f0767e5e5e50e370e99bb011c2dda88e455d724934a42efeacfcf0f4d4559439ba2'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
