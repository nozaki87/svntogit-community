# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-rime
pkgver=5.0.11
pkgrel=1
pkgdesc="RIME support for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-rime"
license=('GPL')
depends=('fcitx5' 'librime')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('1cc29171b671c76e77aa75f496a953419019075d6b46257272b4af75f7dc8cdc3b4fb7a1489600175f7bcfffd58362756d09e62f87ec44f5c70c3a476a9d0938'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
