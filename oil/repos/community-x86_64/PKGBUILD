# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.9.8
pkgrel=1
pkgdesc="A new Unix shell"
arch=('x86_64')
url="https://www.oilshell.org"
license=('Apache')
depends=('readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
options=('!lto' '!strip')
source=("$url/download/$pkgname-$pkgver.tar.xz")
sha512sums=('0031873ccbe569410976acf5a68f9c09ab9d1fd5b79b321aad9dee2d2a1ec5112de88f787052b999a0090ee94cd5d2fa7af0296307997e7b46f9806fe96cea8f')
b2sums=('05968245089e2e66e06612975106feea729b1c4bb16facba0fd6f519e748b0c90553bd83d839867653129f84aa1a4ba9be09f2d9a521a47426c9de722aa0e3ac')

build() {
	cd "$pkgname-$pkgver"

	./configure \
    --prefix=/usr \
    --with-readline

	make
}

package() {
	cd "$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
