# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh
pkgver=3.0.2
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot'
arch=('any')
url='https://github.com/acmesh-official/acme.sh'
license=('GPL3')
depends=('bash')
optdepends=('socat: for standalone mode')
source=("$pkgname-$pkgver.tar.gz::https://github.com/acmesh-official/acme.sh/archive/$pkgver.tar.gz")
sha512sums=('6bd40951df02e3eaa1ed0efd8767acb5245e9c26c666ec7087db2cd0a38b65ea3c79ade98be954b4fbde0e89b6a988ac1c85ce2a6bd77b33574fb3fc0266ea03')

package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share/acme.sh/deploy
  install -dm755 "$pkgdir"/usr/share/acme.sh/dnsapi
  install -dm755 "$pkgdir"/usr/share/acme.sh/notify
  install -dm755 "$pkgdir"/usr/bin

  install -m755 acme.sh "$pkgdir"/usr/share/acme.sh/
  install -m755 deploy/*.sh "$pkgdir"/usr/share/acme.sh/deploy/
  install -m755 dnsapi/*.sh "$pkgdir"/usr/share/acme.sh/dnsapi/
  install -m755 notify/*.sh "$pkgdir"/usr/share/acme.sh/notify/

  ln -s /usr/share/acme.sh/acme.sh "$pkgdir"/usr/bin/acme.sh
}
