# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>
# Contributor: Daniel Maslowski <info@orangecms.org>

pkgname=fisher
pkgver=4.3.1
pkgrel=1
pkgdesc="A package manager for the fish shell"
arch=('any')
url="https://github.com/jorgebucaran/fisher"
license=('MIT')
depends=('fish' 'curl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('157d0e80c784d3b93aef03a01abd4398dfbdc6b404da091a81e0842f6c876c1c5b05513b1b142eed086433dcf44963d4fae1161df9ff544859adf472834cd163')
b2sums=('c44a73f6499a3250bc3aa8627311ede985725389a257794955d06cdccc273755ffc827f045765e0c01028be6d3a3239dfd4f6a1433e80ac2d64491ca327dc570')

package() {
  cd "$pkgname-$pkgver"

  # install to global fish directory
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_functions.d" functions/fisher.fish
  install -vDm644 -t "$pkgdir/usr/share/fish/vendor_completions.d" completions/fisher.fish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
