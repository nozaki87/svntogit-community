# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20211004
pkgrel=4
_pjprojectver=2.11
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(glibc opendht asio gnutls libgit2 libsecp256k1 ffmpeg speexdsp fmt
         yaml-cpp jsoncpp zlib dbus-c++ alsa-lib libpulse jack
         libupnp libnatpmp openssl webrtc-audio-processing systemd-libs
         libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl msgpack-c msgpack-cxx restinio systemd autoconf-archive) # meson
checkdepends=(cppunit)
_commit=042ad25ca2174df18ca18bcaf39286259fe275be
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/pjsip/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz)
#        jami-daemon-meson-651-part1.patch::https://git.jami.net/savoirfairelinux/jami-daemon/-/commit/17f31b59416e2b04b7694aa0fee594ae8189be5f.patch
#        jami-daemon-meson-651-part2.patch::https://git.jami.net/savoirfairelinux/jami-daemon/-/commit/95617cc2eca13030c6bfec2f67addb4e1d544bea.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz)
sha512sums=(SKIP 021fe3f904b15e149cf647fe2ebdf530b5a8f11ba226396b6df1c8b85c406a532e6d9c310617434b520de787ec0cc4b07d9d54507d51b80627a5c23e884516c2)
#            33a5df1fcd8b2924ff1cd86b63366ad3b4b337ab863cb935a912a440a0cd7a4fa1e9dcb96452c99635321e0dc76a2bcb663ef57d6220efd9c5d355c4317b9ed9
#            6098d09cb1866ee6ccc9de11a636e19091ae76ef3ea6a9ea478ce09548729f7c4b77c4a0627c3c78305d0d3e30b7937bc593f952c8026e3206626a30ed491cc6)

prepare() {
  cd ${pkgname}
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  mkdir contrib/native
  #patch -p1 < ../jami-daemon-meson-651-part1.patch
  #patch -p1 < ../jami-daemon-meson-651-part2.patch
  autoreconf -fvi
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/651
  #cd "${srcdir}"
  #export PATH="$PATH:${PWD}/${pgname}/contrib/$(cc -dumpmachine)/bin"
  #arch-meson ${pkgname} build -D interfaces=library,dbus -D opensl=disabled -D portaudio=disabled -D tests=false \
  #                            -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  #meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/651
#  cd ${pkgname}
#  make -k check
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  #meson install -C build --destdir "${pkgdir}"
}
