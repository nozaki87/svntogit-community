# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=batctl
pkgver=2021.2
_advver=$pkgver
pkgrel=1
pkgdesc='B.A.T.M.A.N. advanced control and management tool'
arch=('x86_64')
url='http://www.open-mesh.net/'
license=('GPL')
depends=('libnl')
source=("http://downloads.open-mesh.net/batman/releases/batman-adv-$_advver/batctl-$pkgver.tar.gz"{,.asc})
sha512sums=('f125c1e9aabac557f6808477cbe316c40b527a04469f31d5b56cdd4957313d37506eda5b4179f0190a32a30f67cfbc8683b5461feffe2cda4083633129ac3f73'
            'SKIP')
validpgpkeys=('2DE9541A85CC87D5D9836D5E0C8A47A2ABD72DF9')  # Simon Wunderlich

build(){
  cd batctl-$pkgver
  make
}

package() {
  cd batctl-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr SBINDIR=/usr/bin install
}
