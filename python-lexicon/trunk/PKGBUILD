# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lexicon
pkgver=1.0.0
pkgrel=1
pkgdesc="Powerful dict subclass(es) with aliasing & attribute access"
url="https://github.com/bitprophet/lexicon"
license=('BSD')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-spec')
source=("https://github.com/bitprophet/lexicon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0e1c8534a368eabde31c8f78043b966fa881586f00d2480a6cd3407c8135d08381b6904530cea9839ea57b6a45b9604610fde773ed885e966284f8fdb3e4ccff')

build() {
  cd lexicon-$pkgver
  python setup.py build
}

check() {
  cd lexicon-$pkgver
  PYTHONPATH="$PWD" spec
}

package() {
  cd lexicon-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
