# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-scikit-learn
pkgver=1.0.2
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=('x86_64')
url='https://scikit-learn.org'
license=('BSD')
depends=('python-scipy' 'python-joblib' 'python-threadpoolctl')
optdepends=('python-matplotlib: plotting capabilities')
makedepends=('python-setuptools' 'cython' 'openmp')
options=(!emptydirs)
_archive="${pkgname#python-}-$pkgver"
source=("https://github.com/scikit-learn/scikit-learn/archive/${pkgver}/$_archive.tar.gz")
sha256sums=('34471662f0e5ba8d2c799391338c6f976680cc66b715e00db0c7589f4f371bc4')

build() {
  cd "$_archive"
  python setup.py build
}

package() {
  cd "$_archive"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm066 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
