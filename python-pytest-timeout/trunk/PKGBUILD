# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-timeout
pkgver=2.0.1
pkgrel=1
pkgdesc='py.test plugin to abort hanging tests'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-timeout'
depends=('python-pytest')
makedepends=('python-setuptools' 'python-pytest')
checkdepends=('python-pexpect' 'python-pytest-cov')
source=("https://github.com/pytest-dev/pytest-timeout/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('18c123611c9203620ee09332d9e8062bf8de8b0e2698459674a8a101713b1672487b538c7bfd111c82d8fb3aae2ed9f2f3ad1b754302145e8f003732ed391351')

build() {
  cd pytest-timeout-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-timeout-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-timeout-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
