# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=12.1.1
pkgrel=1
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
groups=('nextcloud-apps')
makedepends=('nextcloud')
checkdepends=('ripgrep' 'yq')
source=("https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('4fc511e90bd738ed9f8a95c8c39a27b79a4a7715e5223c0f931651d6e98651a86f748fd6c54ebedb6b06c3135ee0d8686be50ab9b4488401db7b1025c417d3a4')
b2sums=('3f6c2d955cb73e03dd539a712ac50efc42dd3f731e2b34d351ddc4771453e8251406b4c48abd3f4cf2b8494ba1a2186fd5579c8c0804643cfb7e36687d9bcdb9')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")"<0)) || (("$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions
  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
