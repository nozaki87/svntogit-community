# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=13.0.3
pkgrel=1
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=(any)
url="https://apps.nextcloud.com/apps/spreed"
license=(AGPL3)
groups=(nextcloud-apps)
makedepends=(nextcloud)
checkdepends=(ripgrep yq)
source=("https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}-v${pkgver}.tar.gz")
sha512sums=('4cc580ed2968f38632cbbc230218261a65225b6a6c04dda1d436665cacbfe2b9fa653f254e258c163966705e37e52b9a8457cc487ce10624f1c3e89eb5ad3246')
b2sums=('8e79da9567b82ef202240ee6d50bec202ae0729a7cc01164485925b68cf3a71ea4541c238f77da4f299c54618c683a4d425a1769e0db84c75b66d59d97f2354f')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")"<0)) || (("$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions
  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
