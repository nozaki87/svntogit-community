# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.3
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d9c9dfc3eeb409faf5a7f8b27d87fd95ea2f6921740ee1b6fab71bace6c808688a5162f99b6339c1b767a1bea274f10db9658a00901b90c1d08d711e49e32e23')
b2sums=('46e1f5168da525763ebc94803a15631b70a36f2fbc1d0edd8e58881d1c80d22d34239cc024c3594e6f4d7f6a79e5e38be30d71d45e3abbd4d1de243e7fe93db2')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
