# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.5
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('37e75cfaa404397678bc86c95a5e9205603b6ed677ff4f97c3508db604466040c275b4127bdaf09306f212096dc1434f50a4cb51bfa01874a7fd9cb2102566e4')
b2sums=('ad879a50ab7bb31279cb241b6d3f71d5f94113e2a85de77a42450a29b04b2d7683e9ed2cd56b3ab147febc62109e11e948964f5f3fc302342dcbe8c55e6d317c')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
