# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.5
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(cmake qt6-tools)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f0f3d23e6a154413852b267d5591b7900ae91f2cc689c8278807de8b7b365fdc747a236b9d9e925f4266205d2439978362765e0f59065300e7c35b7428582a2f')
b2sums=('1805521c5ed93fa30b6543e83a32690b2e47424b65c54f7b67874b9cfd00ba790b8347296e55399e4678e227151e84909652a7cc7ba4837d8888314735cfad62')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "$pkgname-$pkgver/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
