# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.8.0
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=(x86_64)
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=(hdf5 curl)
makedepends=(cmake)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=(!makeflags)
license=(custom)
source=(https://github.com/Unidata/netcdf-c/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        netcdf-fix-tst_h_atts3-hdf5-1.12.patch::https://patch-diff.githubusercontent.com/raw/Unidata/netcdf-c/pull/1980.patch
        netcdf-fix-H5Epush_ret-hdf5-1.12.1.patch::https://patch-diff.githubusercontent.com/raw/Unidata/netcdf-c/pull/2034.patch)
sha256sums=('aff58f02b1c3e91dc68f989746f652fe51ff39e6270764e484920cb8db5ad092'
            'db10788679c0240a2b3b422b24198ca5199973098c238a3e42b1989d5cf22dee'
            'dd7c77c364bc513092f91fbb3286778bfd1aff6c2bfb7bb7fc19077db204c2b6')

prepare() {
  cd ${pkgname}-c-${pkgver}
  patch -p1 -i ../netcdf-fix-tst_h_atts3-hdf5-1.12.patch
  patch -p1 -i ../netcdf-fix-H5Epush_ret-hdf5-1.12.1.patch
}

build() {
  # https://github.com/Unidata/netcdf-c/issues/1965
  export CPPFLAGS+=" -DH5_USE_110_API"
  # https://github.com/Unidata/netcdf-c/issues/1983
  export CFLAGS+=" -fno-strict-aliasing"
  cmake -B build -S ${pkgname}-c-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_BYTERANGE=ON \
    -DENABLE_CDF5=ON \
    -DENABLE_DAP_LONG_TESTS=ON \
    -DENABLE_EXAMPLE_TESTS=ON \
    -DENABLE_EXTRA_TESTS=ON \
    -DENABLE_FILTER_TESTING=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DENABLE_UNIT_TESTS=ON
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-c-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
