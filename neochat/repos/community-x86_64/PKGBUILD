# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: derfenix <derfenix@gmail.com>

pkgname=neochat
pkgver=22.02
pkgrel=1
pkgdesc='A client for matrix, the decentralized communication protocol'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/neochat'
depends=(qqc2-desktop-style libquotient kquickimageeditor kitemmodels kio syntax-highlighting qtkeychain-qt5 cmark hicolor-icon-theme)
makedepends=(extra-cmake-modules qcoro-qt5)
source=(https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('be54a898ca1ac7be404be3d666859291669293ee1843917bf8cdcb385787609a'
            'SKIP')
validpgpkeys=(B3ED0BAE371BEDA00717921D18F4795C1CE7042B  # Carl Schwan <carl@carlschwan.eu>
              0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D) # Bhushan Shah (mykolab address) <bshah@mykolab.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
