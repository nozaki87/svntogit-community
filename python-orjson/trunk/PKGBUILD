# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.6.3
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=('x86_64')
url="https://github.com/ijl/orjson"
license=('Apache' 'MIT')
depends=('python')
makedepends=('maturin' 'python-pip' 'rust')
checkdepends=('python-arrow' 'python-pendulum' 'python-psutil' 'python-pytest'
'python-pytz' 'python-xxhash')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('d915574e0643d2a7a678242dafc1588e01ad2698d982a7af632c67f764e19e5c2e19fd91e3d57691f0bcc0fffde25a90348f653ab17c6bd01d6d78e33adacf9c')
b2sums=('415920864abb9c895c3f158afe46b502c0dc40a617e79c08d19fa84eb3cb4a0c9a768fdc0a0d1a23b16234ebc3ceb1cc7e93f3b8eb108f96c2c6f7a05d53b1f3')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  maturin build --no-sdist \
                --release \
                --strip
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgname-$pkgver"
  mkdir -vp testdir
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --root="testdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PIP_CONFIG_FILE='/dev/null'
  pip install --isolated \
              --root="$pkgdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
