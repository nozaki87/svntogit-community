# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.6.7
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-pip rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('5fd7d354f19a01a72218bca4bb7d767ebb73cc79398c5e32c482fed65a93d9f98d58835ebaa0c5e85a975e5eb29d2522a1051bad8f03de8bc227dc2838b8a72b')
b2sums=('2d666885420745d1e783b1d2795bd9814530447818e14f7ea4203a225849869a30962da9a377a86af1f012e3119b9377a723ebf08cf61127b5e50bd3125bdc4a')

build() {
  cd "${_name}-${pkgver}"
  maturin build --no-sdist --release --strip
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${_name}-${pkgver}"
  mkdir -vp testdir

  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="testdir" --ignore-installed --no-deps target/wheels/*.whl
  PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}" pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
