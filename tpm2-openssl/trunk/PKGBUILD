# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-openssl
pkgver=1.1.0
pkgrel=1
pkgdesc='OpenSSL Provider for Trusted Platform Module 2.0 integration'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-openssl'
license=('BSD')
depends=('openssl>=3.0' 'tpm2-tss' 'libtss2-esys.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
checkdepends=('curl' 'dbus' 'swtpm' 'tpm2-abrmd' 'tpm2-tools')
optdepends=('tpm2-abrmd: user space resource manager')
options=('debug')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        'tpm2-openssl_check.sh')
sha512sums=('a57e25d8efcbaa3a898a2803d816169e4227466f9391e5dc7c80a44686865cae0c24ee1973c6376d86d5be8081850f7b35cd0d4cb0b3a8cf654d400d78ae383b'
            'SKIP'
            '08818eb11a2e4f53a6af3c7e2069b04ff5a4d135414a0f7e0dd316abffd9870432fe7f9cd02a2de2f8c99d89862933c8f4af5c76c68834849defc802511290be')
validpgpkeys=('BB0C68611BD255F10FD1EE94B7900BECDB740A3B') # Petr Gotthard <petr.gotthard@centrum.cz>

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	dbus-run-session "$srcdir/tpm2-openssl_check.sh"
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
