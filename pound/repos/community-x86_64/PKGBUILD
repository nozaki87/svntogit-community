# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

_name=Pound
pkgname=pound
pkgver=3.0.1
pkgrel=1
pkgdesc="A reverse proxy, load balancer, and SSL wrapper"
arch=('x86_64')
url="https://www.apsis.ch/pound.html"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'libyaml' 'nanomsg' 'pcre')
makedepends=('cmake' 'gperftools' 'mbedtls' 'uthash')
backup=("etc/${pkgname}/${pkgname}.yaml")
source=("http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.tgz"
        "${_name}-${pkgver}.tgz.asc::http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.asc"
        "${pkgname}-3.0-runtime_dir.patch"
        "${pkgname}.service"
        "${pkgname}.yaml")
sha512sums=('85a3a3ec41ec26574b18cd7e304c82fe5bd3cc2699f3ffbbdf9bc2378cd4820d3c9a2fac702ee1de03209f0c050fa4e9baaae5612bf1fd87a6bc31316681bf9c'
            'SKIP'
            '25db5c13750c2770ae5a91d850f2f00e888474a05b2464c6f03ebe4dc628c86edf8df640daa96e9dcb7240de43537a731671376f20e0dfb599ef5fc2eaf6c053'
            '887ebace94fb6974b34e096c3f9d85e7e45186f816f8a0b4218ffecf2bf041702bc5d40547ca5883691e1d5f959e28b8ce3aac0f12c877760c0e80d8319f57e9'
            '329fb0064f9720cc41332894d3e96e098ffd789d13bcb3b35d67dd9f7b4c0667d1b4cf1d93df9427b4e867d5f0b5857be412ae8d34abed5aa3c2774a499b3292')
b2sums=('6e6cfa8832cbbf462a8df69fc76e8e5c15685d9640c51e3c85b1d9db8e3c6182093f601fd167738f067669126c4ea5c9dc5d3eb8acc64e39b9e1a9f3561a6c4f'
        'SKIP'
        'eba66845b02eff8cea9353f1617f9fcbce040609ea22a1061d98a69c109fed1aaaf6fb338cde1c969153df383def7ae5e3231f281140d32702acfb8c628fa0e4'
        '886e7218d0098f86edfde32b0d8ccdf47f7a8d6a3417144bbd9279cac14d51065fe72b312844ff6a24be6d16183a3b4a44b0d012c1d08808d07ea96d660ff69f'
        'a7ebb7714abf8bc7c2c0f627f6fdcc226a293277a98455bd52340536754ef3a5aef75340b8535402c78a9a754f78d4793c1d7b0c15b0b8d393fd3760a6398858')
validpgpkeys=('8BB562A0F1DB703FB7EB1E95AB72C62A8691DD02') # Robert Segall <roseg@apsis.ch>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # set runtime dir to /run/pound
  patch -Np1 -i "../${pkgname}-3.0-runtime_dir.patch"
  # remove vendored uthash so we build with the packaged version
  rm -v include/ut{hash,array}.h
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libmbedtls.so' 'libmbedcrypto.so' 'libmbedx509.so'
  'libtcmalloc.so')

  cd "${pkgname}-${pkgver}"
  # cmake setup has no install target :(
  install -vDm 755 build/"${pkgname}" -t "${pkgdir}/usr/bin/"
  # configuration
  install -vDm 644 "../${pkgname}.yaml" -t "${pkgdir}/etc/${pkgname}/"
  # systemd service
  install -vDm 644 "../${pkgname}.service" \
    -t "${pkgdir}/usr/lib/systemd/system/"
  # man page
  install -vDm 644 man/${pkgname}.8 -t "${pkgdir}/usr/share/man/man8/"
  # docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
