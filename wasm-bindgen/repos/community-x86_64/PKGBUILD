# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Contributor: Grey Christoforo <first name at last name dot net>

pkgname=wasm-bindgen
pkgver=0.2.81
pkgrel=1
pkgdesc="Interoperating JS and Rust code"
arch=('x86_64')
url="https://github.com/rustwasm/wasm-bindgen"
license=('Apache' 'MIT')
replaces=('wasm-bindgen-cli')
depends=('rust-wasm' 'nodejs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "Cargo.lock")
sha256sums=('301652b8854fe63cf3af1c570b0111fe6788d0d158b5725549df2bb592749f2c'
            '864c8053da46756e9b9268136bb6a898455c7d21c5220095be9170fa36cf2de3')

prepare() {
  # https://github.com/rustwasm/wasm-bindgen/issues/1819
  cp Cargo.lock "$pkgname-$pkgver"
  cd "$pkgname-$pkgver/crates/cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver/crates/cli"
  cargo build --frozen --release --all-features
}

# https://github.com/rustwasm/wasm-bindgen/issues/2946
#check() {
#  cd "$pkgname-$pkgver/crates/cli"
#  cargo test --frozen --all-features
#}

package() {
  cd "$pkgname-$pkgver"
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} \;
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
