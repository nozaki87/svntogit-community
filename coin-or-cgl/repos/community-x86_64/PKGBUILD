# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cgl
pkgver=0.60.5
pkgrel=1
pkgdesc='COIN-OR Cut Generation Library'
arch=(x86_64)
url='https://github.com/coin-or/Cgl'
license=(EPL)
groups=(coin-or)
depends=(coin-or-clp)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Cgl/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('5a2e7ca380425b3d7279d0759c625a367d06ec8293698b59f82fae38ae5df64e')

build() {
  cd Cgl-releases-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd Cgl-releases-$pkgver
  make test
}

package() {
  cd Cgl-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
