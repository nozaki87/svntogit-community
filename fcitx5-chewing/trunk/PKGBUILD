# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chewing
pkgver=5.0.9
pkgrel=1
pkgdesc="Chewing Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chewing"
license=('GPL')
depends=('fcitx5' 'libchewing')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-chewing/fcitx5-chewing-$pkgver.tar.xz"{,.sig})
sha512sums=('1857a6de9705bfefc3d8358bfb91ab37789dbe05f524de602318acbb01162ac181f72107b7f7fff83ce98d296bed004939e525771f264e50e0d07388c34bb6fa'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
