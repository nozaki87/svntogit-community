# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=datree
pkgver=0.14.33
pkgrel=1
pkgdesc='CLI tool to ensure K8s manifests and Helm charts follow best practices as well as your organizationâ€™s policies'
arch=('x86_64')
url='https://github.com/datreeio/datree'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pgname}-${pkgver}.tar.gz::https://github.com/datreeio/datree/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('390c78ae7c35f938f0849dee0389501c8e7c32510c45e99107f3a92c2d367594cbc2c28fa7ce40cc7138aff3335f32233b748120849a540936761ae5e55cf937')
b2sums=('2d70488cde8a372703aaaaa0b02a9d158e39c78ee979b55a1b4d5d45ff049d2427f649fe04a2275d30fc3159f2faaab2bbd0627ad251d50303559b9d7e198503')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o datree .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 datree "${pkgdir}/usr/bin/datree"
}
