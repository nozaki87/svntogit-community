# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=datree
pkgver=0.15.0
pkgrel=1
pkgdesc='CLI tool to ensure K8s manifests and Helm charts follow best practices as well as your organizationâ€™s policies'
arch=('x86_64')
url='https://github.com/datreeio/datree'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pgname}-${pkgver}.tar.gz::https://github.com/datreeio/datree/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('1b796df7b0b3c1e17147ee4e61ca4e8feb75021b8a0fd13188c18bac33d913a9c2096ffef4a7deca81b1b5dd8c6c1a86aae069e01c71c5ad26c05747a3acfdf5')
b2sums=('65898b4a63f928c530d487017ceada22bdb4b99218dce7b3021bbae799c6fba6fac61568ba182ab88b805c9bcb8fb5e1a1ececaa82f62dde8ed87e1c7db94198')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o datree .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 datree "${pkgdir}/usr/bin/datree"
}
