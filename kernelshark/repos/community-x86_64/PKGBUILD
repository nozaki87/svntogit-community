# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=2.1.0
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=(x86_64)
url="https://kernelshark.org/"
license=(GPL2)
depends=(freeglut gcc-libs glibc glu libglvnd qt5-base)
makedepends=(boost cmake json-c libtracefs libtraceevent trace-cmd ttf-fira-sans)
provides=(libkshark.so libkshark-plot.so libkshark-gui.so)
source=("${pkgname}-${pkgver}.tar.gz::https://git.kernel.org/pub/scm/utils/trace-cmd/kernel-shark.git/snapshot/kernel-shark-${pkgname}-v${pkgver}.tar.gz")
sha512sums=('4b463cf06a919d67fa38ada8221e4702e2fac725d917e7abd12e1d69880ba964881ac549caf90bd7cb22edea4f28d7937494a05f75218f16fe2edcdff7161c3e')
b2sums=('2798626c598ec2464f018b5808a6da460a552920c373dbf1eaba5d243e2f150da2a9a25668f8cb27e2ddc7dc286d4d5215d942893456de9339a63bde0e4d7381')

prepare() {
  mv -v "kernel-shark-${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"

  # replace FreeSans with ttf-fira-sans
  sed -e 's/FreeSans/FiraSans-Regular/g' \
      -i CMakeLists.txt
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -D_INSTALL_PREFIX=/usr \
        -D_LIBDIR=/usr/lib \
        -Wno-dev \
        -B build \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libjson-c.so libtracefs.so libtracecmd.so libtraceevent.so)

  make DESTDIR="${pkgdir}" prefix=/usr VERBOSE=1 install -C build
}
