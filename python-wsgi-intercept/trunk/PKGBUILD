# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi_intercept
pkgname=python-wsgi-intercept
pkgver=1.9.3
pkgrel=1
pkgdesc="Intercept socket connection to wsgi applications for testing"
arch=(any)
url="https://github.com/cdent/wsgi-intercept"
license=(MIT)
depends=(python-six)
makedepends=(python-setuptools python-sphinx)
checkdepends=(python-httplib2 python-pytest python-requests
python-urllib3)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b23cbbe20299dad413db0130bf9b055b58ad0a8349d18d9a3eb22cec4db6c9c570785c2ba29245ac52fa1c340a80cb7eb18c04b7bd75dd57961ae8c5142eabdc')
b2sums=('65b7b39543dc486d8ea394b093a3892bdbd7b8e8314a3b839af13ca7abc79f0397092b0d908a873821b1d42d713f5fdd64d2cd815498c7e4f77300725b8ab30c')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
  cd docs
  make man
}

check() {
  cd "${_name}-${pkgver}"
  pytest -v
}

package_python-wsgi-intercept() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 644 docs/_build/man/${_name}.1 -t "${pkgdir}/usr/share/man/man1/"
}
