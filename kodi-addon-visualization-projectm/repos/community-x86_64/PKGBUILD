# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-projectm
epoch=1
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="ProjectM visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.projectm'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'projectm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.projectm/archive/$pkgver-$_codename.tar.gz"
)
sha512sums=('d219f16784f4077c8823f680c0c6f6fe4109eaf3f6105fd0e6ca55d9a6f12942c03bce0f244edcf4778d3359850fd1f9939ebf76d9819791a45f8afd56d31ef7')

build() {
    cd "visualization.projectm-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'projectm')
    cd "visualization.projectm-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

