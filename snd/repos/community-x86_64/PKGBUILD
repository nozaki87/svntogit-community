# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=22.1
pkgrel=1
pkgdesc="An advanced sound editor"
arch=(x86_64)
url="https://ccrma.stanford.edu/software/snd/"
license=(custom:free)
groups=(pro-audio)
depends=(glibc glu gmp gsl libglvnd libmpc libx11 libxext
libxpm libxt mpfr openmotif)
# TODO: maybe replace mpg123 with mpg321
makedepends=(alsa-lib fftw flac gendesk jack ladspa libpulse
libsamplerate mpg123 ruby speex timidity++ vorbis-tools wavpack)
optdepends=(
  'flac: For flac support'
  'mpg123: For MPEG support'
  'pulseaudio: For pulseaudio support'
  'ruby: for snd-ruby'
  'speex: For speex support'
  'timidity++: For MIDI to WAVE support'
  'vorbis-tools: For OGG support'
  'wavpack: For wav support'
)
options=(debug)
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1dc36e041c6970965832d10a8cfd3ebc2fd68bc3b06ea9a76150fe3283645d60c63188badead50b3fbfa6f9de91d52c4238fbc6b33bacaade4a1fc55b37101d1')
b2sums=('a322d1896f7899931b6a0619f3e6e7084b7ac25b8cf503c81ea98fc18f6af8a17e21e0e651bc57493fa37ae75813a369c6f11625384f7dce1875cd2e51928178')

prepare() {
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.${pkgname}-s7" \
          --name "${pkgname} (s7)" \
          --exec "${pkgname}-s7" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.${pkgname}-ruby" \
          --name "${pkgname} (ruby)" \
          --exec "${pkgname}-ruby" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  gendesk -f -n \
          --pkgname "edu.stanford.ccrma.${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
  rm -v "${pkgname}-${pkgver}/"*.desktop
  cp -av "${pkgname}-${pkgver}" "${pkgname}-ruby-${pkgver}"
}

build() {
  (
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-s7
  make
  )
  (
  cd "${pkgname}-ruby-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-ruby
  make
  )
}

package() {
  depends+=(libasound.so libfftw3.so libjack.so libsamplerate.so)

  (
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  mv -v "${pkgdir}/usr/bin/snd"{,-s7}
  )

  cd "${pkgname}-ruby-${pkgver}"
  make prefix="${pkgdir}/usr" install
  mv -v "${pkgdir}/usr/bin/snd"{,-ruby}

  # set s7 version as default
  ln -sv snd-s7 "${pkgdir}/usr/bin/snd"

  # XDG desktop integration
  install -vDm 644 ../*.desktop -t "${pkgdir}/usr/share/applications/"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"

  # remove examples of unsupported extension language forth (not in the repos)
  rm -v "${pkgdir}/usr/share/${pkgname}/"*.fs
}
