# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ratpoints
pkgver=2.2.1
pkgrel=1
pkgdesc='A program that uses an optimized quadratic sieve algorithm in order to find rational points on hyperelliptic curves'
arch=(x86_64)
url='http://www.mathe2.uni-bayreuth.de/stoll/programs/index.html'
license=(GPL2)
depends=(gmp)
source=(http://www.mathe2.uni-bayreuth.de/stoll/programs/$pkgname-$pkgver.tar.gz)
sha256sums=('e31b8ef12c1dc347176241f93901c0aabb39236c0c0a281b33d413416cced370')

prepare() {
# Honor system build flags
  sed -e "s|CCFLAGS =|CCFLAGS = -fPIC $CXXFLAGS $LDFLAGS|" -i $pkgname-$pkgver/Makefile
}

build() {
  cd $pkgname-$pkgver

  make ratpoints libratpoints.a
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,include,lib}
  make install INSTALL_DIR="$pkgdir"/usr
}
