# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=ct
pkgver=0.9.1
pkgrel=1
pkgdesc='config transpiler for Flatcar Container Linux'
arch=('x86_64' 'aarch64')
url='https://github.com/flatcar-linux/container-linux-config-transpiler'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flatcar-linux/container-linux-config-transpiler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('5bbe79d4a17f610289f323732467b1f4afb2bca3b5939de2a4fc848db7873e08aaa4ca5484bade6cf01dcbcaaa64e886dcfbdd8c8fdd4f1c97e50958fc48e254')
b2sums=('4caa22bbfb894ee203d882c6c99a073e0e3d98ae018293e577a6d1835586eceecb3880756aad3452f569e80027d37d965fe3f05eb956b90259e4ca808e76c9e5')

build() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o ct
}

check() {
  cd "container-linux-config-transpiler-${pkgver}"
  go test ./...
}

package() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  install -Dm755 ct "${pkgdir}/usr/bin/ct"
}
