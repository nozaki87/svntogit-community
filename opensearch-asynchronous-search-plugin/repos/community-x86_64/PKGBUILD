# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-asynchronous-search-plugin
pkgver=1.2.4.0
_opensearchver=1.2.4
pkgrel=1
pkgdesc="OpenSearch Asynchronous Search Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/search-plugins/async"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/asynchronous-search/archive/${pkgver}.tar.gz"
)
sha256sums=('3f99ea081ba32474c7a3adf9ec2e67020a5f0e4f2cc0db90ebae43707ebd389d')

build() {
  cd "asynchronous-search-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-asynchronous-search"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-asynchronous-search"
  unzip "${srcdir}/asynchronous-search-${pkgver}/build/distributions/opensearch-asynchronous-search-${pkgver}.zip"

  install -Dm644 "${srcdir}/asynchronous-search-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
