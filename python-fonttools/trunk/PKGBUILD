# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Firmicus <firmicus āt gmx dōt net>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=python-fonttools
pkgver=4.26.2
pkgrel=3
pkgdesc='A library to manipulate font files from Python'
url='https://github.com/fonttools/fonttools'
license=('MIT')
arch=('x86_64')
depends=('python')
makedepends=('cython'
             'python-setuptools')
optdepends=('python-brotli: to compress/decompress WOFF 2.0 web fonts'
            'python-fs: to read/write UFO source files'
            'python-lxml: faster backend for XML files reading/writing'
            'python-lz4: for graphite type tables in ttLib/tables'
            'python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel'
            'python-pyqt5: for drawing glyphs with Qt’s QPainterPath'
            'python-reportlab: to drawing glyphs as PNG images'
            'python-scipy: for finding wrong contour/component order between different masters'
            'python-sympy: for symbolic font statistics analysis'
            'python-unicodedata2: for displaying the Unicode character names when dumping the cmap table with ttx'
            'python-zopfli: faster backend fom WOFF 1.0 web fonts compression')
source=("https://github.com/fonttools/fonttools/archive/$pkgver/fonttools-$pkgver.zip")
sha256sums=('343de533d4780d34ac8d7138dc933550064c9d54a4afded01180e57000381040')

build() {
	cd "fonttools-$pkgver"
	python setup.py build_ext --inplace
	python setup.py build
}

package() {
	cd "fonttools-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm0755 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
