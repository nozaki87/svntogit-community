# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Max Roder <maxroder@web.de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=libhx
pkgver=3.26
pkgrel=1
pkgdesc='A library providing queue, tree, I/O and utility functions'
arch=(x86_64)
url='http://libhx.sourceforge.net/'
license=(GPL)
depends=(glibc)
source=(https://inai.de/files/libhx/libHX-$pkgver.tar.{xz,asc})
sha256sums=('67e2dd0b5b888ab8c02362d043ac2d4dd26eb5bb976064603b57b3e3a7adb209'
            'SKIP')
validpgpkeys=(23686C10A45691BE7A425109D6388181F35A0938) # Jan Engelhardt <jengelh@inai.de>

build() {
  cd libHX-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd libHX-$pkgver
  make DESTDIR="$pkgdir" install
}
