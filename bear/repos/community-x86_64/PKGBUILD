# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yiyao Yu <yuydevel at protonmail dot com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
pkgver=3.0.18
pkgrel=2
pkgdesc="A tool to generate compilation database for clang tooling"
arch=('x86_64')
url="https://github.com/rizsotto/Bear"
license=('GPL3')
depends=('grpc' 'fmt' 'spdlog' 'nlohmann-json')
makedepends=('git' 'cmake')
checkdepends=('gtest' 'python' 'llvm')
_commit='275ac3ea4d3907eb194995d5e16f6f9b204c4600'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --tags
}

prepare() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR="lib/$pkgname"
}

build() {
  cmake --build build
}

check() {
  cd "$pkgname"
  # Bear runs tests alongside build. As to why this function even exists,
  # turn your attention to `man PKGBUILD` and look up checkdepends.
  # > These dependencies are only considered when the check() function is
  # > present and is to be run by makepkg.
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
