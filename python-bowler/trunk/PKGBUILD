# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=bowler
pkgname=python-bowler
pkgver=0.9.0
pkgrel=1
pkgdesc="Safe code refactoring for modern Python."
arch=('any')
url="https://github.com/facebookincubator/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-click' 'python-fissix' 'python-setuptools' 'python-sh')
checkdepends=('python-volatile' 'python-moreorless' 'python-coverage')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('cdb85ce2e7bd545802a15d755d1daf2b6a125429355c50d2019a9f35d63e45db')
b2sums=('4e4a1b39e23cb76ad74edcd371b3bb1e4b52aae2342d026bdf07cb3f1598b62092e946ac5f8ddec7a882ff918ea44be12dc06646a775a557df260622910ca593')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m coverage run -m bowler.tests
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
