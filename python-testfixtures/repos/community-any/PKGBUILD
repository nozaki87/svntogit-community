# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=6.18.4
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("https://github.com/Simplistix/testfixtures/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('79d7fc9f66c6dc1663cf996dbc20c10b4ecf9f5bbe9dea19b80ba89e0fc632c8dc42a7d05c59774e44b8d717bcc5ea6cf35648dd5c869f0a1d858900957654c0')

build() {
  cd  testfixtures-$pkgver
  python setup.py build
}

check() {
  cd  testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
