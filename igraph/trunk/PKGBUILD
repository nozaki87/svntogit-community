# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.9.6
pkgrel=1
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(x86_64)
url='https://igraph.org/c/'
license=(GPL2)
depends=(glpk arpack libxml2 suitesparse plfit)
makedepends=(cmake)
source=(https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
        igraph-pflit.patch)
sha256sums=('7c299ec54eecfe413758c332a42c4cb71d02d2951b2ac232584d317c5792f387'
            '21687f028108cd9ce02a614dd399563b3b8cb1dcb059ed6bd3d05aa5f94b8f79')

prepare() {
  patch -d $pkgname-$pkgver -p1 < igraph-pflit.patch # Make cmake find pflit
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
