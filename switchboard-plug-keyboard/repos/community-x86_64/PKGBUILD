# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-keyboard
pkgver=2.5.0
pkgrel=1
pkgdesc='Switchboard Keyboard Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-keyboard
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  ibus
  libgee
  libgnomekbd
  libgranite.so
  libhandy-1.so
  libxml2
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=f924468b716ea6e0d7452222c6e34fcf9b5077c0
source=(git+https://github.com/elementary/switchboard-plug-keyboard.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-keyboard

  git describe --tags
}

build() {
  arch-meson switchboard-plug-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
