# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=2.3.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client} 'rtmpdump')
checkdepends=("python-"{freezegun,mock,pytest,requests-mock})
makedepends=("python-"{recommonmark,setuptools,sphinx})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        0001-fix_test_cookie_clear_unbound.patch)
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('1497e6dc5d2fb8c5b17688f20ca2e8989a279de804a4de37d2107af33b9faa04'
            'SKIP'
            '193ce085e1655551865ea0d1055de0814bbdf6bcd4df510656538fb483482901')
sha512sums=('928cddecb4b91bf29a5c2cb5c5de037863323434a4e20b72261740b6a8703508fbd3b14c6ac99592548205ebf7282d35890f02a493372b74908f4d974c468084'
            'SKIP'
            '215af7f630661a95d6f30ef2024253fdf0fb25e3abc4a611e56d808cde485b9c88e0dd3bf569adc0ba2391355c5170218c2b05357e31e633ba3fbdaa480b830d')

prepare() {
  cd "$pkgname-$pkgver"

  # fix the test_cookie_clear_unbound issue
  patch -Np1 < "$srcdir/0001-fix_test_cookie_clear_unbound.patch"
}

build() {
  cd "$pkgname-$pkgver"
  export STREAMLINK_USE_PYCOUNTRY="true"
  python setup.py build
  python setup.py build_sphinx -b man
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 build/sphinx/man/streamlink.1 \
    "$pkgdir/usr/share/man/man1/streamlink.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
