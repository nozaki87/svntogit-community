# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-responsemock
pkgname=python-pytest-responsemock
pkgver=1.1.0
pkgrel=1
pkgdesc="Simplified requests calls mocking for pytest"
arch=(any)
url="https://github.com/idlesign/pytest-responsemock"
license=(BSD)
depends=(python-pytest python-responses)
makedepends=(python-setuptools)
checkdepends=(python-requests)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fd2eb1802a400d23ad9eead0a5706f8e2f6de969d994e22f7d7d75704f4b8392348f1cfd3270c1d93ad3bda886761086975e9a63486476ec73ffd6e08e4a593f')
b2sums=('a503359e8dc7f35d213a6b87397255e091eff9f4178ee9cbb2fad92f6a0a20139f970eb0fe94daf717817e2d294b8649cb1ae1b5cedc418210a8385c30890ad2')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable flaky tests
  pytest -v -k "not test_bypass"
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
