# Maintainer: David Runge <dvzrv@archlinux.org>

_name=natsort
pkgname=python-natsort
pkgver=8.0.0
pkgrel=1
pkgdesc="Simple yet flexible natural sorting in Python"
arch=('any')
url="https://github.com/SethMMorton/natsort"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-mock' 'python-pytest-mock')
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('59263d0bda41094c5ab7564f81d53e12bc9695a56a5c63bb2e907b52030a18f68a51e2298c74cc0e87629fb14a9f6d32955caa86a539bc0c2008fbe03aae30d8')
b2sums=('9649d77e314e515865a4ba905c8db9810031ab1db378e99b935c7b4ca92f59a273930d92535652cb322771ca58ce858c7108db7ec58e512e82390919289f4ba3')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
