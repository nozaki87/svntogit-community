# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=16.3.0
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("https://github.com/jaraco/path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('10065efeb36a05b6885eff3e91fc63b80e0418567d5be0a442c1c46283faaafe8032a7eeb2fe547416bab4208c53db3fa0048a939a6294f04393a155983ec1dd')

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
