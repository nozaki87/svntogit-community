# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx_basic_ng
pkgname=python-sphinx-basic-ng
pkgver=0.0.1a11
pkgrel=1
pkgdesc='Modernised skeleton for Sphinx themes'
arch=('any')
url=https://github.com/pradyunsg/sphinx-basic-ng
license=('MIT')
depends=('python-sphinx')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('bf9a8fda0379c7d2ab51c9543f2b18e014b77fb295b49d64f3c1a910c863b34f')
b2sums=('16b30164c84335a9a1ac0b1c2b64994fd3b4d7984660c11c73538f11a22c608867be7948d910a5f1b9f12b48ea712c3ad25163ffbae7b9a1d5386abd03c8259b')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
