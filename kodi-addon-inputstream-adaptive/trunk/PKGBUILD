# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Cedric Girard <cgirard.archlinux@valinor.fr>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-adaptive
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="InputStream client for adaptive streams for Kodi 18+"
arch=('x86_64')
url="https://github.com/peak3d/inputstream.adaptive"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'kodi-dev' 'gtest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/peak3d/inputstream.adaptive/archive/$pkgver-$_codename.tar.gz")
sha512sums=('dca318f434e87242857af672db4b6edc27e37bc202cce0ea3ff549af0894d27ab73adc084703d04dc2d8dde6219d00ae83209fd61f62916deaf65486feb8e74f')

build() {
    cd "inputstream.adaptive-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'kodi-platform')
    cd "inputstream.adaptive-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
