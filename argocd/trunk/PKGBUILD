# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd
pkgver=2.1.6
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes"
arch=('x86_64')
url='https://argo-cd.readthedocs.io/en/stable/'
license=('Apache')
makedepends=('go' 'yarn')
depends=('glibc')
provides=('argocd-cli')
replaces=('argocd-cli')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz"
        "argo-ui-a7be8b3208549c7cf5a0b9531ca41af5614f0eda.tar.gz::https://github.com/argoproj/argo-ui/archive/a7be8b3208549c7cf5a0b9531ca41af5614f0eda.tar.gz"
        "argo-ui.patch")
sha512sums=('1d47dd170a4670215e7a2358661b5159fee294b99b8c9d56c353df0c15806b86f8a796a378a9607ca6862d33e134d68e990da8dd51482607f551ed8a4ebb15b1'
            'a8f8e51875acfa26ec59aace8268538b031ae9d3dde83869e1b51d906f6be6726d5659bd2eb3587e8849d57053ef5103cfba6025743d28be1ae2771386b7d1ab'
            '083f144dac3bc81cd803985f7259c2fa2fbc1be70e4da2a5b290eab964acd7e049f090ff736f38e7d96b607e250d9ba7a67fe7d36bb6324c3d4c448a2bc4cd8a')

prepare() {
  cd "argo-cd-$pkgver"

  patch -p0 -i ../argo-ui.patch
}

build() {
  cd "argo-cd-$pkgver"

  (
    cd ui
    yarn install --cache-folder "${srcdir}/yarn-cache"
    ARGO_VERSION="$pkgver" NODE_ENV='production' NODE_ONLINE_ENV='online' yarn build
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  go build -o argocd \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/v2/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/v2/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/v2/common.gitTag=${pkgver} \
              -linkmode=external \
              -extldflags \"${LDFLAGS}\"" \
    ./cmd

  ./argocd completion bash > argocd.bash
  ./argocd completion zsh > argocd.zsh
}

package() {
  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
  ln -s argocd "$pkgdir/usr/bin/argocd-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-repo-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-application-controller"
  ln -s argocd "$pkgdir/usr/bin/argocd-dex"

  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"
}
