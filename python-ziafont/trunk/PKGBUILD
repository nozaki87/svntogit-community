# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziafont
pkgver=0.3.1
pkgrel=1
pkgdesc="Convert TTF/OTF font glyphs to SVG paths"
url="https://ziafont.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nbval' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/ziafont/get/$pkgver.tar.bz2")
sha512sums=('db7c7288e11e0f3bedf1a07c375fce56e54c09e98a11900cec9f5143523ef4a0401c0f0df9ba24db54583d5ffd96354f57cf92a5d1e233c77c3df22b31ea434c')

prepare() {
  mv cdelker-ziafont-* ziafont-$pkgver
}

build() {
  cd ziafont-$pkgver
  python setup.py build
}

check() {
  cd ziafont-$pkgver
  python setup.py pytest --addopts --nbval || echo "Tests failed"
}

package() {
  cd ziafont-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
