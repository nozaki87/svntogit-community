# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=6.4.2
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-chardet' 'python-jinja' 'python-jinja_pluralize'
'python-pluggy' 'python-pygments')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-tomli')
optdepends=('python-tomli: for TOML support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('15202ccb66365b0578bbdf3183448998c22e7dcc56e800e94eebe77cfb2985db23b1214a2113dd7631fae7cd8f7284da2297b8ef546d94177f5f06dd9d205c6d'
            'SKIP')
b2sums=('f9fb4a00caf8c92599c7aeadfd9ac3a608552d13396c36d68766c3419b603aa9ad72c6085e12348ea594fde33aef144402d0346c9568e53c18ea0bc4abb043ba'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir='test_dir'

  cd "$pkgname-$pkgver"
  # install to temporary location, as importlib is used
  python setup.py install --optimize=1 --root="${_test_dir}"
  PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}" \
  # ignore integration and code style checks
  pytest -v --ignore 'tests/test_integration.py' --ignore 'tests/test_violations_reporter.py' --ignore 'tests/test_clover_violations_reporter.py'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
