# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor:  kpcyrd <git@rxv.cc>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: flaccid aka Chris Fordham <chris@fordham.id.au>
# Contributor: Sparadox <etienne.lafarge at gmail.com>

pkgname=cloud-init
pkgver=21.3
pkgrel=3
pkgdesc="The standard for customising cloud instances"
arch=('any')
url="https://cloud-init.io"
license=('GPL3')
depends=('dhclient' 'netplan' 'python-configobj' 'python-jinja'
'python-jsonpatch' 'python-jsonschema' 'python-netifaces' 'python-oauthlib'
'python-requests' 'python-yaml' 'sudo' 'systemd')
makedepends=('python-setuptools')
checkdepends=('python-httpretty' 'python-prettytable' 'python-pytest')
optdepends=('cloud-guest-utils: for growpart')
backup=('etc/cloud/cloud.cfg' 'etc/cloud/cloud.cfg.d/05_logging.cfg')
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-21.3-lib_dir.patch"
        "${pkgname}-21.3-python-configobj.patch"
)
sha512sums=('d248add04b3bf3807ca397fb075a20f55cff9a98116e349c5bf59b44e2d2e76559f92ac37d619551e2c871cdfb35c27a58f4759078d0355d6868e85d13c527bd'
            '3d23d060779e2bb82a7044ee6e00169baa7ea4cda756ea32c80329b73fd69521f7dd76a4079cb1c7a941859389a8d67dec7bfb5c2c9eb236a3950eca92f446ca'
            'e4404702b51b2f199026bf8e8a75fe18cf216ce91da47f5faa644b6ed6251f49c49a85772e48f2bbfcf283292d67234b05d588ad69e5804bb4fb6b7a63f2dad5')
b2sums=('9d500055902c75d1765d09970382b398fe2d44831def7979dcd674c5934b2c10fbe50b3160fb5d9f8b4526e088bef7d304b3b6821776b1765612acac27111697'
        'a7845162108cdc90f576d2408c6244fbddac13431395740e02c8915b5a6a4b70f3dd5d44de27e13fb6d7a24d2e916c474bdce842b6992410385a786914e4da0f'
        '7e370e2ba8c159195586ae839a450368eeb6f558366714e873743d871bda9613dbdb2391db365f66f613fbfa60cb36a13cff990a5b92c17b31f711a098e5747f')

prepare() {
  cd "${pkgname}-${pkgver}"
  # we use /usr/lib
  patch -Np1 -i ../"${pkgname}-21.3-lib_dir.patch"
  # we have python-configobj > 5.0.6, which does not provide ConfigObj._a_to_u() anymore
  patch -Np1 -i ../"${pkgname}-21.3-python-configobj.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 \
    --init-system=systemd \
    --root="${pkgdir}"
}
