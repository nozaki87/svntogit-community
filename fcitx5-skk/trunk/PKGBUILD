# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-skk
pkgver=5.0.10
pkgrel=1
pkgdesc="libskk wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-skk"
license=('GPL')
depends=('fcitx5-qt' 'libskk' 'skk-jisyo')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('9a5b71712601bd5d67612a0dbd4a8966c02915560565d1fccfecf8c5b79297231e743491063743448e2acd7b493fb3a3bfe57553e48983b35be9dbf483033a6c'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
