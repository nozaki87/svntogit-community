# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.37
pkgrel=4
pkgdesc='Plugins for Geany'
arch=(x86_64)
url='https://plugins.geany.org/'
license=(GPL)
depends=(ctpl "geany>=$pkgver" gpgme gtkspell3 libgit2 lua51 python vte3 webkit2gtk)
makedepends=(cppcheck intltool)
source=("$url$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig})
b2sums=('4e6043b38d2756646275bdb8f5432c8285d9db09143b3a86edd3cac3ef5c0a1910731582c1fe4381f914480c82bd2c2e69b1184b220182cf35a828281596cce0'
        'SKIP')
validpgpkeys=('6BEC027CD44552A5B566F28DB7A4039D0630EA07') # Frank Lanitz <frank@geany.org>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi

  # don't redefine bool
  sed -i 's/typedef unsigned int bool/#include <stdbool.h>/g' \
    pretty-printer/src/PrettyPrinter.h
}

build() {
  cd $pkgname-$pkgver
  export CFLAGS+=' -w'
  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
