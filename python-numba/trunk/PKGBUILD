# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Michael Schubert <mschu.dev at gmail> github.com/mschubert/PKGBUILDs
_pkgname=numba
pkgname="python-${_pkgname}"
pkgver=0.55.1
pkgrel=1
pkgdesc="A Just-In-Time Compiler for Numerical Functions in Python."
url="https://numba.pydata.org/"
arch=('x86_64')
license=('BSD')
depends=('python-llvmlite' 'python-numpy' 'tbb')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm' 'cuda' 'cython' 'git' 'openmp')
checkdepends=('python-tests')
optdepends=('python-scipy: for numpy.linalg support'
            'python-cuda: for NVIDIA CUDA support'
            'openmp: for OpenMP support')
source=("${_pkgname}-${pkgver}::git+https://github.com/${_pkgname}/${_pkgname}#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export CUDA_REL=11
  export NUMBA_CUDA_USE_NVIDIA_BINDING=1
  export CUDA_HOME=/opt/cuda
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-${python_version}" python -m test -v
  # cannot run CUDA tests in chroot
  # PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-${python_version}" python -m numba.runtests numba.cuda.tests -v -m
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
