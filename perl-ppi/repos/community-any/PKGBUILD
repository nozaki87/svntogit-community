# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppi
pkgver=1.272
pkgrel=1
pkgdesc="Parse, Analyze and Manipulate Perl (without perl)"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPI"
depends=('perl-clone' 'perl-io-string' 'perl-list-moreutils' 'perl-params-util' 'perl-task-weaken')
makedepends=('perl-class-inspector' 'perl-file-remove' 'perl-test-deep' 'perl-test-object'
             'perl-test-subcalls')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/PPI-$pkgver.tar.gz")
sha512sums=('f934623994a2e845767239b4101048085b33dbdfd5424829fd4c51dbc9f9395cd9ad1502955a2d9146c1e982b71c591c4dfb2824c4d59f760022f2c63dbac846')

build() {
  cd PPI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPI-$pkgver
  make test
}

package() {
  cd PPI-$pkgver
  make DESTDIR="$pkgdir" install
}
