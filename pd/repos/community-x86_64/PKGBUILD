# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

_resolutions=( 16 32 48 64 96 128 256 512 1024 )
_name=pure-data
pkgname=pd
pkgver=0.51.4
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=2
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('BSD')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'tk')
makedepends=('alsa-lib' 'fftw' 'gendesk' 'inkscape' 'jack' 'portaudio'
'portmidi')
provides=('puredata')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pure-data/pure-data/archive/${_ver}.tar.gz"
        "${pkgname}-0.51.4-system_portmidi.patch"
        "${pkgname}-${pkgver}.svg::https://raw.githubusercontent.com/pure-data/pd-icon/9e4f117ade0bc1da951981128998ad82fff83b0c/masters/icon.svg"
        "${pkgname}-${pkgver}-icon-LICENSE.txt::https://raw.githubusercontent.com/pure-data/pd-icon/9e4f117ade0bc1da951981128998ad82fff83b0c/LICENSE.txt")
sha512sums=('87c5b81730b13d4770b5a158362c7d7143e5218a23be689a78532adaf472554c242dcc1ab989e04924c3e4617a363435a616b5e8d8a29045196ac6fe527722a8'
            '67f14a39ae9a7da3776d3d92b8bf2b695b6a4e465e76bb821b0d97440dabb550dfdc9e5e1426cb3a7eeb898b57a278ec59d4988b6560e305cc6acce98ac6eed9'
            '8d8b274e159bd4cc6a83d5d9d5019b106a74fa61943b36e2a56336b9c3c2c1b1092ce7f79f5264eb69ca43a9360404b800df338b2ad738ca3c237a8ae3c9c41d'
            'dfc919b56ffc8020ffa4e9b16677a6a710f78a73b22d5bb418414f3196a5cbbe5d6cc9cfbf1389811129f219cb21c0eafdfa6dd90e172d73db2452e5c3fd4f5c')
b2sums=('22aabe30f9001b92e3dec83689f42678cb15653a6200380df1adc448d60b1deb2293b0de8ec78b5c87dc5f32b485a890562703fd4723e1094d45e137e10c9fd5'
        'af41732beacc45be8ff561a9cb47f1dd369e913f434cf98f714241e986b69f19f784d3397730c5506738fb11fc985498013f843656b72835ca5973f65f8ae2a8'
        '04f131540fd7ae9b4547a081df61a5a98265c6c9b283587aeda976834d9bbb4486cd30470d5bfb37b6f760d13705074c71cfeb1a9843613e93f3d1e0ca198af9'
        'c3fabeeb8e5c45716fbf5d3e6338b41d5adc0fbefb385e3b42d94c732522e99a4045c88e8b8e2e16a72fc5795dbc0933f07078d0eba80b76fb274450c7518dc7')

prepare() {
  mv -v "${_name}-${_ver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"

  # ensure the system portmidi libraries are properly linked against
  patch -Np1 -i ../"${pkgname}-0.51.4-system_portmidi.patch"

  autoreconf -vfi
  # XDG desktop files
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --exec ${pkgname} \
          --name "Pure Data" \
          --categories "AudioVideo;Audio"
  for _res in ${_resolutions[@]}; do
    inkscape -o "pd-${_res}.png" \
             -w "${_res}" \
             -h "${_res}" \
             "../${pkgname}-${pkgver}.svg"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  export EXTERNAL_LDFLAGS="$LDFLAGS"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-fftw \
              --enable-jack \
              --enable-portaudio \
              --enable-portmidi \
              --disable-oss \
              --without-local-portaudio \
              --without-local-portmidi
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libjack.so' 'libportaudio.so'
  'libportmidi.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # licenses
  install -vDm 644 LICENSE.txt \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 ../${pkgname}-${pkgver}-icon-LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/icon-LICENSE.txt"

  # desktop file
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  # icons
  install -vDm 644 "${pkgname}-32.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 "${srcdir}/${pkgname}-${pkgver}.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  for _res in ${_resolutions[@]}; do
    install -vDm 644 "${pkgname}-${_res}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/apps/${pkgname}.png"
  done
  # readme
  install -vDm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # fix broken symlink
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  rm -v "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ln -sv "/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
}
