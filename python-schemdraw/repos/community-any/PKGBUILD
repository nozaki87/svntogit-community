# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sequencer <liujiuyang1994@gmail.com>

pkgname=python-schemdraw
pkgver=0.11
pkgrel=1
pkgdesc="Electrical circuit schematic drawing."
url="https://bitbucket.org/cdelker/schemdraw"
license=('BSD')
arch=('any')
depends=('python')
optdepends=('python-matplotlib: for matplotlib backend'
            'python-ziafont: for svg backend'
            'python-ziamath: for svg backend'
            'python-latex2mathml: for svg backend')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-nbval' 'python-matplotlib' 'python-ziafont'
              'python-ziamath' 'python-latex2mathml')
source=("$pkgname-$pkgver.tar.bz2::https://bitbucket.org/cdelker/schemdraw/get/$pkgver.tar.bz2")
sha512sums=('328e0551178faf03fcaff29919a98bbd8845b81b16ae4372af549886310a0adcb8d045b248ebfcb8aaddd5080c9c265a8fb843a13b6730196a18e71a56718cec')

prepare() {
  mv cdelker-schemdraw-* schemdraw-$pkgver
}

build() {
  cd schemdraw-$pkgver
  python setup.py build
}

check() {
  cd schemdraw-$pkgver
  python setup.py pytest --addopts --nbval-lax
}

package() {
  cd schemdraw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
