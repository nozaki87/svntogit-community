# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.8.10
pkgrel=1
pkgdesc="New wave internationalisation support for Ruby"
arch=('any')
url="https://github.com/ruby-i18n/i18n"
license=('MIT')
depends=('ruby' 'ruby-activesupport' 'ruby-json' 'ruby-mocha')
makedepends=('ruby-bundler' 'ruby-rake' 'ruby-rdoc')
# # TODO: package ruby-pry
# checkdepends=('ruby-minitest' 'ruby-test_declarative')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ruby-i18n/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('e8b10bf380241d1e6b19250cd63e727366215b4ad7e63479a02cb81a5b46a48e06db827d9df9969c2052895646ee30c237fd17c3352a2bbfed97615bb9b6cfca')
b2sums=('58e8041f8cf652627a305e3d987ff2450913d3dc919ea7aae641cb484524d17312ebd3e0ee68cde1c01091e42713ea229ccb41ed3998c6bbf772d32f855ca107')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i "${_name}.gemspec" Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

# # TODO: package ruby-pry
# check(){
#   cd "${pkgname}-${pkgver}"
#   rake test
# }

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
