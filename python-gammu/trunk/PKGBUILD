# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2.3
pkgrel=1
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL')
depends=('python' 'gammu')
makedepends=('python-setuptools')
checkdepends=('libdbi-drivers')
source=($pkgname-$pkgver.tar.gz::https://github.com/gammu/$pkgname/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('a452c05e53b12150e095ca5477a78f15523179c926061fa792caf43a829f77de0e1efe74c5061ef43da6d84857ca9c351cbd0902be4ac0b81e69478c089dfb6e')

build() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py build
}

check() {
  # tests can be flaky on slower hardware due to timing
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py test 
}

package() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
