# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.9.6
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
arch=(x86_64)
url="https://qjackctl.sourceforge.io"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(alsa-lib cmake jack2 jack2-dbus qt6-tools portaudio)
optdepends=(
  'jack2-dbus: for JACK dbus support'
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0af54ffc9a4afa3672e5996273f2ba20c7b0aec2d64831944a3aed16b7d33a48eca7d71dd799f197e450c44010065ea9e8ca9d5cc307af926697a2f95a4fa023')
b2sums=('8d4d6a5ddb712c7ea8c7494a0392250e8128b4284addce797031294c19059e05f7db43ca2b421d7ebaf0abe8db95e2cf098dec3fb2b520a3ff282383fe169455')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libjack.so libportaudio.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README,TRANSLATORS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
