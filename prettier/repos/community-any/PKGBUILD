# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=prettier
pkgver=2.4.1
pkgrel=1
pkgdesc="An opinionated code formatter for JS, JSON, CSS, YAML and much more"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("yarn")
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('56d16af6b95bed8fc984d511e08f1c7dccc6676296b78f7b123719f21f5dcfbf')

prepare() {
    cd "$pkgname-$pkgver"
    yarn --frozen-lockfile
}

build() {
    cd "$pkgname-$pkgver"
    yarn build
}

package() {
    cd "$pkgname-$pkgver"
    install -d "$pkgdir/usr/"{bin,"lib/$pkgname","share/licenses/$pkgname"}
    cp -a dist/* "$pkgdir/usr/lib/$pkgname"
    ln -s "/usr/lib/$pkgname/bin-$pkgname.js" "$pkgdir/usr/bin/$pkgname"
    ln -s "/usr/lib/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
