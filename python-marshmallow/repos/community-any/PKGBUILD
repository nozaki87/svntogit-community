# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-marshmallow
pkgver=3.15.0
pkgrel=1
pkgdesc="A lightweight library for converting complex datatypes to and from native Python datatypes."
url="https://github.com/marshmallow-code/marshmallow"
license=('MIT')
arch=('any')
depends=('python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-simplejson' 'python-pytz' 'python-dateutil')
source=("https://pypi.io/packages/source/m/marshmallow/marshmallow-$pkgver.tar.gz")
sha512sums=('9d688466b9dfce7cd3307538aadb68c8297d3ee026a7a61953d361a9e52acafd00da81f48ab26771dba74bf8024117d53292763ab98cb78e2bbd7b5604bee4f1')

build() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py pytest
}

package() {
  cd marshmallow-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
