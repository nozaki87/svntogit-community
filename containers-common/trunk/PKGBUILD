# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.46.0
pkgrel=4
_image_pkgver=5.16.1
_podman_pkgver=3.4.1
_skopeo_pkgver=1.5.0
_storage_pkgver=1.37.0
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('e4406b8996e0ae374c463433ce00a392c1ca8259b512a324845e0332bf925284f71d92071eb1d511c00070b3ab41318fbe08bf47d98a6404e7c6d4231143be48'
            '2f943c888da97cbcb99b97fc28deb66f8d5ac4f1e00f5ec679977a3db23d14e6d610fac78a9820554ac6ed8e5c9bc2cc943ea46fe26a5e9db2c9842933a2ad8f'
            'b8602464358ab73429a9003262c92c38c9a06e1133c776fd0c4ce65dae26639db66f8f5ad510528f3e6e7bc2fe692746fcf1d90d4e95d701d102006d9a83b27e'
            '908882424bf103ef1c278d8b3e6117360e6c6d69ce92e70c7bc936984a0dbac73ef11b4c423032aaec2f935e2c2be55e2d9d5b44d63310a1486d36b02895e442'
            '3a65a50946e1eed1c79ea77b65feb1c6c8cf58a0b7b81932acb8891b6daa25e007582eb7f70ef7e7f2a6d8262be5db7757ead2fe079e47c99114b47917d81084'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('9a6c5ad52a91d3186aceba301f36dbc9e98088a71a0c2babe119837b25381461bb92b52f16f1b4c6696e9449c502ef87e5026cac6d295d8398a8a68eda3a3ec0'
        '64f9cb4bfd9ff59bc0ce42900fe5076237a2f1ac29047e3ca525352e0e86eae892d19fd3ddd4668911cf8d74e8bc89c83eab2c095a86a09936f1558915eef9df'
        '423aaecabce5242ae87887e36e9caaf499b3d5c18978e47b738ef3b6a0a094c64f18f5fef614193157c3de0a6c0d88a232efcdcb60de4ae8ab14abbe266ad512'
        'db34d76dbd48c246d428c58dcdd7ffad70bf7d9fa2b4a1462a960f9ba9d0f11263e30ef096ebbc748ca0deb202379693724da023848ca8d32509b188bb08b7be'
        'ebe5c8e562149a9d007ac87d7fc615d29ad7decbfd6b1b8d919b14684a6341575d8f65080a808ba52674664d187a18bb47e88f10e1a02a2975898862873dadf9'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
