# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.43.2
_image_pkgver=5.15.2
_podman_pkgver=3.3.0
_skopeo_pkgver=1.4.0
_storage_pkgver=1.34.1
pkgrel=2
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('25658720a9b032b2588a96e489c1d1dcd985036716f2410fa8f071754d9780a95f244784dc2caf13c4f85d420d2a3068d839b6197c3fd24c073b57f912e4d5ab'
            '239bf7d58f5126fcd1e721ecc5e14ab28284535348fc39fd9232a5cd2725a397f5e51287e22ac5e1e4f7fcf1b0159eecd0660da9a50d52d0cdbf9503bf39fb47'
            '0e77edfdbafc4ccb7b0b29d5e41054d52b09258fd25424be3339c256489a5d215109eda903eb620f2ed566c042a6d6b8a5d8adc26b08bba4fa66263a1ee3104d'
            '2a21ed33ef39c700fc8247876d33ee7592129d8454eac09ee0a896d7c7c59ee02dd714c38bf4cab03c51b5060ea638ee0da6a2e6ed37f1846de296ce4c843076'
            '4c32aa6a6f8ee50eec4b94f693eb8299c9d02f0bedf97c174ea6e23bf614b9eb582797bcec75a51b141aba6e742ffc240ef7d492f53e8e621c4c3df75df1a841'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('8f35d9ce203fc388e1cc9160e5f3481c24a05977272841bb04af2e75cbad67a0f47aeff856a4ee2cfc070e432d35bc74afcb07e582bd3bb470917e4dfecbf5da'
        'f5725486b2a643c94d23455577ab9235e765d92b5b67950674eba962a36952a0856530bbdeb0cdbb54baae3eeb990148f1de8726f2c78937f3c3ba533f0f3978'
        '84643c8e71158dcaf8a3805e3170f10a366002e5e361e90609417b2521bd24e2694e63f268bb9da9e01d72861dda17b0e28fe79bf86be838c547778477fac53a'
        '2150742ebf146d42b841e8e3f1b28e0c6875b10214fd323ce6915f6b776dca85e8cbf1fcd8477dd600cf4104fba2b2aca8358bbb091e3c9ff3a3e89ddbdc6cb7'
        '431b1bb2e0cae2cae69d6475271f97666bee5c15a2f2cd6ba52f30a95d86551933ceec2df863150daa526f49fa0b50e2e5ed61bd6f29f1e5a63df26254ce93c8'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
