# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=4.6.0
pkgrel=1
pkgdesc="Oslo Logging Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("https://github.com/openstack/oslo.log/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('80e9a4f9537b88bcb89dfdd418815ca816bd1e5be4fc3165afb2eab48c54ebf43273fe5f5eacb5eb6fca5716ad7a48c27ffea45e8ef9aa0512e198762b0ac2f1')

export PBR_VERSION=$pkgver

build() {
  cd oslo.log-$pkgver
  python setup.py build
}

check() {
  cd oslo.log-$pkgver
  stestr run
}

package() {
  cd oslo.log-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
