# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-gnome
pkgver=20220207
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (GNOME client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-libclient jami-daemon
         gtk3 dconf clutter clutter-gtk webkit2gtk qrencode qt6-5compat
         libnotify libcanberra libnm)
makedepends=(git cmake)
_commit=ec8eef7dd3ac175ca923ee42f2b3cc911dd78919
source=(git+https://git.jami.net/savoirfairelinux/jami-client-gnome.git#commit=${_commit})
md5sums=(SKIP)

pkgver() {
  cd jami-client-gnome
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd jami-client-gnome
  # Fix build system looking for lrc sources
  rm -r web
  ln -s /usr/include/libringclient/web-chatview/ web
}

build() {
  cmake -B build -S jami-client-gnome \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
