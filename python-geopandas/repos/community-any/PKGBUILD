# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=geopandas
pkgname=python-${_pkg}
pkgver=0.9.0
pkgrel=2
pkgdesc="Python tools for geographic data"
arch=(any)
url="https://geopandas.org"
license=(BSD)
depends=(
    python-fiona
    python-numpy
    python-pandas
    python-pyproj
    python-shapely
)
optdepends=(
    'python-rtree: spatial access methods'
    'python-psycopg2: PostGIS access'
    'python-sqlalchemy: other geodatabase access'
    'python-geopy: geocoding'
    'python-matplotlib: plotting'
#    'python-mapclassify: plotting'
)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-psycopg2 python-sqlalchemy python-geopy python-matplotlib python-scipy) #python-mapclassify
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz
        geopandas-fix-missing-fillna.patch::https://patch-diff.githubusercontent.com/raw/geopandas/geopandas/pull/1878.patch
        geopandas-fix-proj8.patch::https://patch-diff.githubusercontent.com/raw/geopandas/geopandas/pull/1958.patch
        geopandas-fix-geopy2.2.patch::https://github.com/geopandas/geopandas/commit/b03259deabaea290168f33dc3462ca348d3199b5.patch)
sha256sums=('63972ab4dc44c4029f340600dcb83264eb8132dd22b104da0b654bef7f42630a'
            'edfded8808eed42fef5289f5171d3ba0ccd33a410b3d66b8422765afcd5b5e6d'
            '14ce4e7b1bb90aae9c48adcbb84490dd1ed61f5c095740aaaaa9b4eede09c622'
            '56194e7ec2beacdb839fb4e20ba1cb5b52724b64ce2010e1ff39b0c5421284a5')

prepare() {
  cd ${_pkg}-${pkgver}
  patch -p1 < ../geopandas-fix-missing-fillna.patch
  patch -p1 < ../geopandas-fix-proj8.patch
  # https://github.com/geopandas/geopandas/pull/2007
  patch -p1 < ../geopandas-fix-geopy2.2.patch
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib/ pytest
}

package() {
  cd ${_pkg}-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
