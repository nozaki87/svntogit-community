# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pyenv
pkgver=2.2.4
# botched 2.2.4 release: https://github.com/pyenv/pyenv/issues/2231
_ver=2.2.4-1
pkgrel=1
pkgdesc="Easily switch between multiple versions of Python"
arch=(any)
url="https://github.com/pyenv/pyenv"
license=(MIT)
depends=(bash)
optdepends=('git: installing development versions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${_ver}.tar.gz")
sha512sums=('09f3539f10549a3d940d6d88756a550c8bf2ab50ad0e139f0394f91fe6161233cb9ce1461f7384a46850384f0ac96ad4d4555dfd43534ed517d722669b4cacc6')
b2sums=('0837f91e63d1c1cd107ece02f7beb25f88131165d00e1795d0552813477fd580160a6afae367790a4c18e11084bb99724e903c34093e02f45effaa11e4dfd54c')

package() {
  cd "$pkgname-$_ver"
  # application
  install -vDm 755 libexec/* -t "${pkgdir}/usr/share/${pkgname}/libexec"
  install -vdm 755 "${pkgdir}/usr/bin"
  ln -sv "/usr/share/${pkgname}/libexec/${pkgname}" \
    "${pkgdir}/usr/bin/${pkgname}"
  # application hooks
  install -vDm 755 pyenv.d/exec/pip-rehash/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/exec/pip-rehash"
  install -vDm 644 pyenv.d/exec/*.bash \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/exec/"
  install -vDm 644 pyenv.d/rehash/*.bash \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash"
  install -vDm 644 pyenv.d/rehash/conda.d/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash/conda.d"
  install -vDm 644 pyenv.d/rehash/source.d/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash/source.d"
  # plugin
  install -vDm 755 plugins/python-build/bin/* \
    -t "${pkgdir}/usr/share/${pkgname}/plugins/python-build/bin"
  for bin in {${pkgname}-{install,uninstall},python-build}; do
    ln -sv "/usr/share/${pkgname}/plugins/python-build/bin/${bin}" \
      "${pkgdir}/usr/bin/${bin}"
  done
  cp -av plugins/python-build/share \
    "${pkgdir}/usr/share/${pkgname}/plugins/python-build"
  # licenses
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 plugins/python-build/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.python-build"
  # shell completion
  install -vDm 644 completions/*.fish \
    -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
  install -vDm 644 completions/${pkgname}.bash \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -vDm 644 completions/${pkgname}.zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # man page
  install -vDm 644 man/man1/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,COMMANDS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 plugins/python-build/README.md \
    "${pkgdir}/usr/share/doc/${pkgname}/README-python-build.md"
}
