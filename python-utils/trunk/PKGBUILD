# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.1.0
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest-runner)
source=("https://pypi.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('e22ea0dc7aa0e6c5b45c55843011971c5075639424d38a712703b0d5ef8af32e3661d39af63b6015c4edfe11fca7a256f4f02c3705e1724fe8b9015931d85cbf'
            'SKIP')
b2sums=('c00f4c45480f0948cddfa65d4f1ce17ac6831e29b24913d225544619ebedaca67c0f143b55698f8e2edd658e5f90ac39e005a5c29e09d75b5643f79992ba878a'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v -c /dev/null
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
