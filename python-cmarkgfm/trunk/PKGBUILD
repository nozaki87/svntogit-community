# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-cmarkgfm
pkgver=0.7.0
pkgrel=1
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
depends=('cmark-gfm' 'python-cffi')
makedepends=('cmark-gfm' 'python-setuptools' 'python-cffi')
checkdepends=('python-pytest-runner')
source=("https://github.com/theacodes/cmarkgfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e663e52d5dcb0799d8e15c7a1c06916a5b603ab32e95a12a03897001b6581c6e45aff1a985dc16a65ad05307e9f6c01f941e9210bea3d617439154cfbb1f1bce')

prepare() {
  cd cmarkgfm-$pkgver
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  # test failure in new cmark-gfm
  python setup.py pytest --addopts "--deselect tests/test_cmark.py::test_github_flavored_markdown_to_html_tasklist"
}

package() {
  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm0644 -t "$pkgdir"/usr/share/licenses/$pkgname/ LICENSE.txt
}
