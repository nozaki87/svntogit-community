# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krita-plugin-gmic
pkgver=3.1.2.3
pkgrel=1
pkgdesc='GMic plugin for Krita'
arch=(x86_64)
url='https://github.com/amyspark/gmic'
license=(custom:CeCILL)
depends=(krita gmic)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://github.com/amyspark/gmic/releases/download/v$pkgver/gmic-$pkgver-patched.tar.xz{,.asc}
        https://github.com/amyspark/gmic/commit/acc77d4d.patch)
sha256sums=('2c961eb5a77692536e917cd684681813a3bfee333bbade5bd03e0496882e82d2'
            'SKIP'
            '0af6d68b7d883fac20d08d300fe1f17d2c74a31d1a431abb1eccef84c261b3f5')
validpgpkeys=(4894424D2412FEE5176732A3FC00108CFD9DBF1E) # https://github.com/amyspark.gpg
options=(debug)

prepare() {
  patch -d gmic-$pkgver-patched -p1 < acc77d4d.patch # Fix build
}

build() {
  cmake -B build -S gmic-$pkgver-patched/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMIC_QT_HOST=krita-plugin
  VERBOSE=1 cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm "$pkgdir"/usr/share/gmic/gmic_cluts.gmz # Provided by gmic
  install -Dm644 gmic-$pkgver-patched/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
