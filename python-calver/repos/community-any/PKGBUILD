# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-calver
pkgver=2021.7.30
pkgrel=1
pkgdesc="Setuptools extension for CalVer package versions"
url="https://github.com/di/calver"
license=('Apache')
arch=('any')
depends=('python')
makedepends=('python-calver' 'python-setuptools')
checkdepends=('python-pytest' 'python-pretend')
source=("https://github.com/di/calver/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('17c10b0f60ec4c8c582de660bd715d054c3478d8fdec88a6830396bad1d6147ce375a34c5ff57d244c8c2e337a6b382b6ee10673182a40bc14ad16a97405f181')

prepare() {
  cd calver-$pkgver
  echo "Version: $pkgver" > PKG-INFO
}

build() {
  cd calver-$pkgver
  python setup.py build
}

check() {
  cd calver-$pkgver
  mv PKG-INFO PKG-INFO.bak
  PYTHONPATH="$PWD"/build/lib pytest
  mv PKG-INFO.bak PKG-INFO
}

package() {
  cd calver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
