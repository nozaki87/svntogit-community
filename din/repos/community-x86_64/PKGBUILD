# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=53.1
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=(x86_64)
url="https://dinisnoise.org/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libglvnd sdl tcl)
makedepends=(boost glu rtaudio rtmidi)
options=(debug)
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-52-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('42058b00402e726869af8a5cb8eb9b34e7b1709185c972d3f3b633a980ad7de57a143ca0ce854ce681ec67b81cd6dd2b2b06023a15d3f1b1bd54c4ac643b6096'
            '56aaa4f2e39297d3b64d254ddbf53ae7c7d0ea7df462a7f9ab3071ce3a21c997874b8a39065422b012a10e8ecd90b5373c887e924a8843134a6b095f588ffd26')
b2sums=('882d4ae6cbe24c3d7fc326df3f3630d7ea7cd65b5d53c4751844dc3edcf9d533cd7cfe58fed4c46766fab6b4983e75f75145206b5362bceb27382d02705b9a36'
        'f38ddc9c92a025fe1e361e8c1e48fd2fced13e19894c117f5e79f00eb5444fd4861562895dbef9884bd9996e2248923b511e3879655bb6cb87a1242b7fbb6042')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-52-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -fiv
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=(librtaudio.so librtmidi.so)

  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
