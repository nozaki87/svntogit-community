# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=Kvantum
pkgname=kvantum-qt5
pkgver=0.20.2
pkgrel=2
pkgdesc="SVG-based theme engine for Qt5 (including config tool and extra themes)"
arch=(x86_64)
url="https://github.com/tsujan/Kvantum"
license=(GPL)
depends=(qt5-base qt5-svg qt5-x11extras libx11 libxext hicolor-icon-theme kwindowsystem)
makedepends=(cmake qt5-tools)
source=(${url}/releases/download/V${pkgver}/${_pkgname}-${pkgver}.tar.xz{,.asc})
sha256sums=('195fcf504a0119b9ddb147551e4f6f301dc3ec04c3e41700ccd71f22d262e2ef' 'SKIP')
validpgpkeys=(19DFDF3A579BD509DBB572D8BE793007AD22DF7E) # Pedram Pourang <tsujan2000@gmail.com>

build() {
  cmake -B build -S ${_pkgname}-${pkgver}/${_pkgname} -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
