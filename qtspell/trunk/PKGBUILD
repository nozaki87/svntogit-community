# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=qtspell
pkgver=0.9.0
pkgrel=1
pkgdesc='Spell checking for Qt text widgets'
arch=('x86_64')
url='https://github.com/manisandro/qtspell'
license=('GPL3')
depends=('enchant' 'iso-codes' 'qt5-base')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://github.com/manisandro/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('49a5ab995c3150572c3c48989cd3e506b0c2d64b09d80fb5b30daa7325445635')

prepare() {
  # Fix location of translations
  sed -i 's|share/${QT_VER}/translations)|share/qt/translations)|' $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake -S $pkgname-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
