# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-settings-daemon
pkgver=1.0.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/settings-daemon.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  geoclue
  glib2
  libgranite.so
)
makedepends=(
  git
  meson
  vala
)
_tag=37d090797975e1c5d1827ce220cd6737c035ce42
source=(pantheon-settings-daemon::git+https://github.com/elementary/settings-daemon.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-settings-daemon
  git describe --tags
}

build() {
  arch-meson pantheon-settings-daemon build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
