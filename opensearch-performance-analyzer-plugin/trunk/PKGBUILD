# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-performance-analyzer-plugin
pkgver=1.2.4.0
_opensearchver=1.2.4
pkgrel=1
pkgdesc="OpenSearch Performance Analyzer Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/monitoring-plugins/pa"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/performance-analyzer/archive/${pkgver}.tar.gz"
)
sha256sums=('c35a2d7b8c355dac213a640d43a562c4e2ec65a6e427509bbc54b197224e913a')

build() {
  cd "performance-analyzer-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-performance-analyzer"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-performance-analyzer"
  unzip "${srcdir}/performance-analyzer-${pkgver}/build/distributions/opensearch-performance-analyzer-${pkgver}.zip"

  install -Dm644 "${srcdir}/performance-analyzer-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
