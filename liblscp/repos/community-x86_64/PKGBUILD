# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.9.5
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(x86_64)
url="https://www.linuxsampler.org/"
license=(LGPL2.1)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7ec8eb07d997ee860e2f288036154dc0c5516ceca9d0c7dd8bb75cbfd28e8920b369487bf1fad31bb2e83f4b7a173238ed252e19fe4de5e08250175c66dd09b9')
b2sums=('62e2db115f0911281ebc1652efb1a13dc166086d82bf8f4c6202f7a7666532f05a74090970ccc7aed3ee625cc84750fed0c3a724fcb9b1a640fadb46ba8def3c')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

