# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaeger-client
pkgver=4.7.0
pkgrel=1
pkgdesc="Jaeger Python OpenTracing Tracer implementation"
url="https://github.com/jaegertracing/jaeger-client-python"
license=('Apache')
arch=('any')
depends=('python-opentracing' 'python-threadloop' 'python-thrift' 'python-tornado')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-prometheus_client' 'python-pycurl' 'python-pytest-benchmark' 'python-pytest-localserver')
source=("https://github.com/jaegertracing/jaeger-client-python/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f86f41e402a42f1a40941f192bdea9eb1b0065c67fe0dc5891fce0d1ed1d86c93a243df85dbe0eccb468b54229dd9d546866e8b373a8ab87f8c93a372bc8ae18')

build() {
  cd jaeger-client-python-$pkgver
  python setup.py build
}

check() {
  cd jaeger-client-python-$pkgver
  # Tracer test dependencies don't support new Tornado
  pytest --deselect tests/test_prometheus.py --deselect tests/test_tracer.py --deselect tests/test_tracer_benchmark.py
}

package() {
  cd jaeger-client-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
