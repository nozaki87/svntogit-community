# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Stéphane Graber <stgraber AT ubuntu DOT com>

pkgname=distrobuilder
pkgver=1.3
pkgrel=2
pkgdesc="System container image builder for LXC and LXD"
arch=('x86_64')
url="https://github.com/lxc/distrobuilder"
license=('Apache')
depends=(
  'rsync'
  'squashfs-tools'
  'gnupg'
  'debootstrap'
  'dosfstools'
  'gptfdisk'
  'qemu'
  'e2fsprogs'
  'btrfs-progs'
)
makedepends=('go' 'git')
conflicts=('distrobuilder-git')
source=("$url/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
b2sums=('fa3b9ee784fb84f349e0646389ee4cb11811cde15da11ce7cdc2f983f39e2ced51612d734ea2093865dc4f141951287be772e414907fe09d4b364f7a70acd7f0'
        'SKIP')
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67') # Stéphane Graber <stgraber@stgraber.org>

prepare() {
  cd "$pkgname-$pkgver"

  # create folder for build output
  mkdir build

  # download dependencies
  go mod vendor
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  # build binary
  go build -v -o build ./...
}

check() {
  cd "$pkgname-$pkgver"
  go test -v ./...
  go vet -v ./...
}

package() {
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"
}
