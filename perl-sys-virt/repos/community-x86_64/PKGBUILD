# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-sys-virt
pkgver=7.7.0
pkgrel=1
pkgdesc="Represent and manage a libvirt hypervisor connection"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/Sys-Virt"
depends=('libvirt' 'perl')
makedepends=('perl-cpan-changes' 'perl-module-build' 'perl-xml-xpath' 'perl-test-pod'
             'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DA/DANBERR/Sys-Virt-v$pkgver.tar.gz")
sha512sums=('09a62cdbae3f9a6f32c0b52d474ae9f6189bf3ccc1108508595bb91411b82e73983512dc86bb5f289a0bd2296a1a5ece4ab478c583f8a58b16b750cd83d061d7')

build() {
  cd Sys-Virt-v$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Sys-Virt-v$pkgver
  make test
}

package() {
  cd Sys-Virt-v$pkgver
  make DESTDIR="$pkgdir" install
}
