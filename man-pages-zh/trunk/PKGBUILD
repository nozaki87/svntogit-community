# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: dickeny@gmail.com
# Contributor: cuihao <cuihao dot leo at gmail dot com>

pkgbase=man-pages-zh
pkgname=(man-pages-zh_cn man-pages-zh_tw)
pkgver=1.6.3.5
pkgrel=1
arch=('any')
url='https://github.com/man-pages-zh/manpages-zh'
license=('custom:GFDL1.2')
depends=('man-db')
makedepends=('opencc' 'python' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/man-pages-zh/manpages-zh/archive/v$pkgver.tar.gz")
sha512sums=('87f0483153a1bb26d68647933c2bb2a9ed53df21fb01d73cfe1db5b5ed41b4e2684a395d15520b3029a2266222e77a5b3113271a6979c9652f4ec5ad0028bc4f')

build() {
  cd manpages-zh-$pkgver
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package_man-pages-zh_cn() {
  pkgdesc='Simplified Chinese Linux man pages'

  cd manpages-zh-$pkgver
  make install DESTDIR="$pkgdir"

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/share/doc/manpages-zh/COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/
  rmdir "$pkgdir"/usr/share/doc/manpages-zh "$pkgdir"/usr/share/doc

  rm -r "$pkgdir"/usr/share/man/zh_TW

  # remove conflicted files with shadow
  cd "$pkgdir"/usr/share/man/zh_CN
  rm ./man1/groups*
}

package_man-pages-zh_tw() {
  pkgdesc='Traditional Chinese Linux man pages'

  cd manpages-zh-$pkgver
  make install DESTDIR="$pkgdir"

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/share/doc/manpages-zh/COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/
  rmdir "$pkgdir"/usr/share/doc/manpages-zh "$pkgdir"/usr/share/doc

  rm -r "$pkgdir"/usr/share/man/zh_CN

  # remove conflicted files with shadow
  cd "$pkgdir"/usr/share/man/zh_TW
  rm ./man1/groups*
}
