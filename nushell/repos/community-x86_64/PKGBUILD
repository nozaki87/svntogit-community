# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.39.0
pkgrel=1
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
sha512sums=('c3735a22f7b41b78847e5361c300252f4de77a2c783ea219a8f7f56b3211845e19807f216986361b75d6dac0b67340371e1475b3e6515a8c2dac4fc52ac9488a')
b2sums=('d3192b0b88bf0232a46b6c3818fcbb9322f461c37fac5e8b0e3aa28d9a737a5bb650c3ae9ab3cefe46975484ed5247d7fa75632a0b9aa5b2a054313a6431f643')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --workspace --features=extra
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --workspace --features=extra
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # remove binaries not present in upstream releases
  rm -f "$pkgdir/usr/bin/table"
  rm -f "$pkgdir/usr/bin/"nu_plugin_{core,extra}_*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
