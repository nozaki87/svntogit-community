# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.36.0
pkgrel=1
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
b2sums=('4e9fb8962c24fe03020f7dd8b168a057334ee96e25809f0678f214fc40f2c5a973d7d91723fe51e61e999b2b32b4a0670237b7f5944ef3778f8bf2f69a247d7d')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --all-features --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
