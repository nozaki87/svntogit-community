# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=operator-sdk
pkgver=1.14.0
pkgrel=1
pkgdesc="SDK for building Kubernetes applications"
arch=('x86_64')
url="https://github.com/operator-framework/operator-sdk"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/operator-framework/operator-sdk/archive/v${pkgver}.tar.gz")
sha512sums=('8e890571550691ebada5be11a0f28e740d9591ee03fdc4a11246eefe341fa60ccd5adb181e246aef9cee7abb1d5012cc5f11a6f8e3887b4bfadd2d037e24198a')
b2sums=('50c83207e071779f9116fa56ec71bc999a92557d2084402e8722c2e714a70b1172174b338db5ebfea716c93e80f52255404f02d892b836335e43e421cddb5ffd')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/operator-framework "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/operator-framework/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/operator-framework/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-linkmode=external" -v -o . ./cmd/{operator-sdk,ansible-operator,helm-operator}
}

#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./operator-sdk "${pkgdir}/usr/bin/operator-sdk"
  install -Dsm755 ./ansible-operator "${pkgdir}/usr/bin/ansible-operator"
  install -Dsm755 ./helm-operator "${pkgdir}/usr/bin/helm-operator"
}
