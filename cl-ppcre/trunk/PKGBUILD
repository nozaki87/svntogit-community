# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-ppcre
pkgver=2.1.1.r3.gb4056c5
pkgrel=1
pkgdesc='Perl-compatible portable regexp library for Common Lisp'
arch=('any')
url='https://edicl.github.io/cl-ppcre/'
license=('BSD')
depends=('common-lisp' 'cl-asdf' 'cl-unicode')
makedepends=('git')
checkdepends=('sbcl' 'cl-flexi-streams' 'perl')
_commit='b4056c5aecd9304e80abced0ef9c89cd66ecfb5e'
source=(
  "$pkgname::git+https://github.com/edicl/cl-ppcre#commit=$_commit"
  'run-tests.lisp'
)
b2sums=('SKIP'
        'cd4fc113c2f1b1e3180010a79621d84b44d79dd47f10aaef9615b56b41f94625584a034059164546456b472adcac19ca0c2db98c889c159ac3c323fc5163d469')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed -e 's/^v//' -e 's/-/.r/' -e 's/-/./g'
}

check() {
  cd "$pkgname"

  sbcl --script ../run-tests.lisp
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$pkgname"
  cp -vr test cl-ppcre-unicode ./*.{lisp,asd} "$pkgdir/usr/share/common-lisp/source/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md docs/index.html

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
