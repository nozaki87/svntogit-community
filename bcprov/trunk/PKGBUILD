# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=bcprov
pkgver=1.71
pkgrel=1
pkgdesc='Bouncy Castle Crypto APIs for Java'
arch=('any')
url='https://www.bouncycastle.org/java.html'
license=('MIT')
depends=('java-runtime-headless')
makedepends=('ant' 'strip-nondeterminism')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bcgit/bc-java/archive/refs/tags/r${pkgver/./rv}.tar.gz")
sha512sums=('c9a095a034863a46027e28754ae96d9750162120bc1af124fa700ad05d23716a606633b674e2e8490bda658d30d30e93f42ba00dcc1ac13596a24f8aa43a666d')

build() {
	cd "bc-java-r${pkgver/./rv}"
	ant -f ant/jdk18+.xml clean build-provider build

	# Timestamps in JAR files generated by Ant do not honour SOURCE_DATE_EPOCH
	# (https://bz.apache.org/bugzilla/show_bug.cgi?id=61269)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "build/artifacts/jdk1.8/jars/bcprov-jdk18on-${pkgver/./}.jar"
}

check() {
	cd "bc-java-r${pkgver/./rv}"
	ant -f ant/jdk18+.xml test
}

package() {
	cd "bc-java-r${pkgver/./rv}"
	install -Dm644 "build/artifacts/jdk1.8/jars/bcprov-jdk18on-${pkgver/./}.jar" -t "$pkgdir/usr/share/java/$pkgname"
	ln -s "bcprov-jdk18on-${pkgver/./}.jar" "$pkgdir/usr/share/java/$pkgname/bcprov.jar"
	install -Dm644 LICENSE.html -t "$pkgdir/usr/share/licenses/$pkgname"
}
