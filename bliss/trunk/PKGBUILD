# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=bliss
pkgver=0.77
pkgrel=1
pkgdesc='A library for computing automorphism groups and canonical forms of graphs'
arch=(x86_64)
url='https://users.aalto.fi/~tjunttil/bliss/'
license=(GPL3)
depends=(gmp)
makedepends=(cmake)
source=(https://users.aalto.fi/~tjunttil/bliss/downloads/$pkgname-$pkgver.zip
        digraph_heuristic.patch)
sha256sums=('acc8b98034f30fad24c897f365abd866c13d9f1bb207e398d0caf136875972a4'
            '584dbb948c5ad39c32373a3736d457ef5ecf0c23b552d9e925fa0ace61652461')

prepare() {
  patch -d $pkgname-$pkgver -p1 < digraph_heuristic.patch # Fix splitting heuristics for digraphs
}

build() {
  cmake -B build -S bliss-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_GMP=ON
  cmake --build build
}

package() {
   install -Dm644 $pkgname-$pkgver/src/*.hh -t "$pkgdir"/usr/include/bliss/
   install -Dm755 build/libbliss.so -t "$pkgdir"/usr/lib/
   install -Dm755 build/bliss -t "$pkgdir"/usr/bin/

#  DESTDIR="$pkgdir" cmake --install build
}
