# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libomemo
pkgver=0.8.0
pkgrel=1
pkgdesc='Implementation of OMEMO (XEP-0384) in C'
arch=('x86_64')
url='https://github.com/gkdr/libomemo'
license=('MIT')
depends=('sqlite' 'libgcrypt' 'mxml' 'glib2')
provides=('libomemo.so')
source=("https://github.com/gkdr/libomemo/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('9b13aaaa21f16425fcecf963e187c5b686986e6bb01255c78042ea64e48d2c75a06729d1ad6e6b26d62d191e7d99305b7a8f1e07078221c6d14a12f587bf0743')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX="/usr"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  chmod a+x "${pkgdir}"/usr/lib/libomemo.so.${pkgver}
  ln -sf libomemo.so.${pkgver} "${pkgdir}"/usr/lib/libomemo.so
  ln -sf libomemo.so.${pkgver} "${pkgdir}"/usr/lib/libomemo.so.${pkgver%%.*}
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname} LICENSE
}
