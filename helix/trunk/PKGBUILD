# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=0.4.1
_gitcommit=1caedc18ca47d07b30a5997c092f2683cce3042e
pkgrel=1
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL2')
depends=('bash')
makedepends=('rust' 'git')
install="$pkgname.install"
source=("${pkgname}::git+https://github.com/helix-editor/helix.git#commit=${_gitcommit}"
  "${pkgname}-tree-sitter-julia::git+https://github.com/tree-sitter/tree-sitter-julia"
  "${pkgname}-tree-sitter-cpp::git+https://github.com/tree-sitter/tree-sitter-cpp"
  "${pkgname}-tree-sitter-python::git+https://github.com/tree-sitter/tree-sitter-python"
  "${pkgname}-tree-sitter-javascript::git+https://github.com/tree-sitter/tree-sitter-javascript"
  "${pkgname}-tree-sitter-html::git+https://github.com/tree-sitter/tree-sitter-html"
  "${pkgname}-tree-sitter-php::git+https://github.com/tree-sitter/tree-sitter-php"
  "${pkgname}-tree-sitter-bash::git+https://github.com/tree-sitter/tree-sitter-bash"
  "${pkgname}-tree-sitter-scala::git+https://github.com/tree-sitter/tree-sitter-scala"
  "${pkgname}-tree-sitter-typescript::git+https://github.com/tree-sitter/tree-sitter-typescript"
  "${pkgname}-tree-sitter-rust::git+https://github.com/tree-sitter/tree-sitter-rust"
  "${pkgname}-tree-sitter-json::git+https://github.com/tree-sitter/tree-sitter-json"
  "${pkgname}-tree-sitter-elixir::git+https://github.com/IceDragon200/tree-sitter-elixir"
  "${pkgname}-tree-sitter-nix::git+https://github.com/cstrahan/tree-sitter-nix"
  "${pkgname}-tree-sitter-c-sharp::git+https://github.com/tree-sitter/tree-sitter-c-sharp"
  "${pkgname}-tree-sitter-ruby::git+https://github.com/tree-sitter/tree-sitter-ruby"
  "${pkgname}-tree-sitter-java::git+https://github.com/tree-sitter/tree-sitter-java"
  "${pkgname}-tree-sitter-agda::git+https://github.com/tree-sitter/tree-sitter-agda"
  "${pkgname}-tree-sitter-css::git+https://github.com/tree-sitter/tree-sitter-css"
  "${pkgname}-tree-sitter-go::git+https://github.com/tree-sitter/tree-sitter-go"
  "${pkgname}-tree-sitter-haskell::git+https://github.com/tree-sitter/tree-sitter-haskell"
  "${pkgname}-tree-sitter-latex::git+https://github.com/latex-lsp/tree-sitter-latex"
  "${pkgname}-tree-sitter-toml::git+https://github.com/ikatyang/tree-sitter-toml"
  "${pkgname}-tree-sitter-swift::git+https://github.com/tree-sitter/tree-sitter-swift"
  "${pkgname}-tree-sitter-c::git+https://github.com/tree-sitter/tree-sitter-c"
  "$pkgname.sh"
  "$pkgname-full-relro.patch"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'f782c93bc0f2c7b27ec62fb2ba36e11b3354c3953405f8d2017b039c9eac500a'
            '34014564164fec6a8ffaa1c9ff2436eb74662224ea4920ca165e9fe500b51554')

prepare() {
  cd "$pkgname"
  patch -p0 -i "../$pkgname-full-relro.patch"

  git submodule init
  git config submodule."helix-syntax/languages/tree-sitter-cpp".url "${srcdir}/${pkgname}"-tree-sitter-cpp
  git config submodule."helix-syntax/languages/tree-sitter-javascript".url "${srcdir}/${pkgname}"-tree-sitter-javascript
  git config submodule."helix-syntax/languages/tree-sitter-julia".url "${srcdir}/${pkgname}"-tree-sitter-julia
  git config submodule."helix-syntax/languages/tree-sitter-python".url "${srcdir}/${pkgname}"-tree-sitter-python
  git config submodule."helix-syntax/languages/tree-sitter-typescript".url "${srcdir}/${pkgname}"-tree-sitter-typescript
  git config submodule."helix-syntax/languages/tree-sitter-agda".url "${srcdir}/${pkgname}"-tree-sitter-agda
  git config submodule."helix-syntax/languages/tree-sitter-go".url "${srcdir}/${pkgname}"-tree-sitter-go
  git config submodule."helix-syntax/languages/tree-sitter-ruby".url "${srcdir}/${pkgname}"-tree-sitter-ruby
  git config submodule."helix-syntax/languages/tree-sitter-java".url "${srcdir}/${pkgname}"-tree-sitter-java
  git config submodule."helix-syntax/languages/tree-sitter-php".url "${srcdir}/${pkgname}"-tree-sitter-php
  git config submodule."helix-syntax/languages/tree-sitter-html".url "${srcdir}/${pkgname}"-tree-sitter-html
  git config submodule."helix-syntax/languages/tree-sitter-scala".url "${srcdir}/${pkgname}"-tree-sitter-scala
  git config submodule."helix-syntax/languages/tree-sitter-bash".url "${srcdir}/${pkgname}"-tree-sitter-bash
  git config submodule."helix-syntax/languages/tree-sitter-rust".url "${srcdir}/${pkgname}"-tree-sitter-rust
  git config submodule."helix-syntax/languages/tree-sitter-json".url "${srcdir}/${pkgname}"-tree-sitter-json
  git config submodule."helix-syntax/languages/tree-sitter-css".url "${srcdir}/${pkgname}"-tree-sitter-css
  git config submodule."helix-syntax/languages/tree-sitter-c-sharp".url "${srcdir}/${pkgname}"-tree-sitter-c-sharp
  git config submodule."helix-syntax/languages/tree-sitter-c".url "${srcdir}/${pkgname}"-tree-sitter-c
  git config submodule."helix-syntax/languages/tree-sitter-haskell".url "${srcdir}/${pkgname}"-tree-sitter-haskell
  git config submodule."helix-syntax/languages/tree-sitter-swift".url "${srcdir}/${pkgname}"-tree-sitter-swift
  git config submodule."helix-syntax/languages/tree-sitter-toml".url "${srcdir}/${pkgname}"-tree-sitter-toml
  git config submodule."helix-syntax/languages/tree-sitter-elixir".url "${srcdir}/${pkgname}"-tree-sitter-elixir
  git config submodule."helix-syntax/languages/tree-sitter-nix".url "${srcdir}/${pkgname}"-tree-sitter-nix
  git config submodule."helix-syntax/languages/tree-sitter-latex".url "${srcdir}/${pkgname}"-tree-sitter-latex

  git submodule update --init --recursive
  cargo fetch --locked
}

build() {
  cd "$pkgname"
  cargo build --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  install -Dm 755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  mkdir -p "$pkgdir/usr/lib/helix/"
  cp -r "runtime" "$pkgdir/usr/lib/helix/"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
