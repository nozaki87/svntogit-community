# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-jsondiff
_pkgname=jsondiff
pkgver=1.3.1
pkgrel=1
pkgdesc='Diff JSON and JSON-like structures in Python'
arch=(any)
url='https://github.com/xlwings/jsondiff'
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-nose python-nose-random)
source=("https://github.com/xlwings/jsondiff/archive/$pkgver/jsondiff-$pkgver.tar.gz")
sha256sums=('4c27f0618fedfa9bbae8880cee1120be4ea6e64a30165d64b0a4cd85ed4f93bb')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  nosetests -v tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  # FS#60911, https://github.com/xlwings/jsondiff/issues/24
  rm "$pkgdir"/usr/bin/jsondiff
}
