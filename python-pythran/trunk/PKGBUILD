# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ashwin Vishnu <ashwinvis+arch at pr0t0nm4il dot com>
# Contributor: xantares

pkgname=python-pythran
pkgver=0.10.0
pkgrel=2
pkgdesc='Ahead of Time compiler for numeric kernels'
arch=(any)
url='https://pythran.readthedocs.io/'
license=(BSD)
depends=(python-networkx python-ply python-numpy python-beniget python-six xsimd boost)
makedepends=(python-setuptools python-pytest-runner git)
source=(https://files.pythonhosted.org/packages/source/p/pythran/pythran-$pkgver.tar.gz
        pythran-xsimd-8.patch::https://github.com/serge-sans-paille/pythran/commit/418bd3e8.patch)
sha256sums=('9dac8e1d50f33d4676003e350b1f0c878ce113e6f907920e92dc103352cac5bf'
            'c059f2c42c8cca8bedf45dde1fe59587f3001bf14d47518157657463ec878ea4')

prepare() {
  cd pythran-$pkgver
  git apply < ../pythran-xsimd-8.patch # Port to xsimd 8
}

build() {
  cd pythran-$pkgver
  python setup.py build
}

package() {
  cd pythran-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -r "$pkgdir"/usr/lib/python*/site-packages/pythran/{boost,xsimd} # Remove bundled boost and xsimd
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
