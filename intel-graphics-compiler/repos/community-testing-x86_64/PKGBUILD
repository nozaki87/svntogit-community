# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.8173
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang lld llvm python)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        llvm12-porting.patch
        igc-revert-stack-analysis.patch::https://github.com/intel/intel-graphics-compiler/commit/c50d77f3c8a92fae6f43b47580e7d485f9ea5327.patch
        igc-missing-limits-headers.patch::https://github.com/intel/intel-graphics-compiler/commit/8e1a461d3e6b85a6cf018caf6abf4a3ba9a1758d.patch
        git+https://github.com/intel/vc-intrinsics.git#commit=d9ffe1f9cbe45b296f098669173bcaeff12bfe99)
sha256sums=('20794f1451b971c82ed824dd65723a6c562158e8061644e596b53417e9daa045'
            '1f8b1aa9bce25de79cdbbe2589cb5d7a6e05b1b506b01e7af144b21b155754ea'
            '65b17d5bdc77e68147eacc7671a6e3580968508527bdb8fd032bae830d8daf17'
            'd943a3d56ae62bde12caf133ab171777b4a73ee50a8d07449831ffc57e73e514'
            'SKIP')

prepare() {
    cd ${pkgname}-igc-${pkgver}
    patch -p1 < ../llvm12-porting.patch
    patch -p1 -R < ../igc-revert-stack-analysis.patch
    patch -p1 < ../igc-missing-limits-headers.patch
}

build() {
    CXXFLAGS+=' -Wno-error=deprecated-declarations'
    cmake -B build -S ${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_OPTION__LLVM_PREFERRED_VERSION='12.0.1' \
        -DIGC_OPTION__LLVM_MODE=Prebuilds \
        -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
        -DINSTALL_SPIRVDLL=0 \
        -DINSTALL_GENX_IR=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
    mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
