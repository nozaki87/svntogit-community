# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.8744
pkgrel=3
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang lld llvm python libunwind)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-fix-209.patch::https://github.com/intel/intel-graphics-compiler/commit/c74d33971d65d6bf410e77fca0c27aeedce7b81e.patch
        ${pkgname}-fix-lld-prebuilt.patch::https://github.com/intel/intel-graphics-compiler/pull/210.patch
        git+https://github.com/intel/vc-intrinsics.git#commit=e5ad7e02aa4aa21a3cd7b3e5d1f3ec9b95f58872)
sha256sums=('1e8ef2dcebb670e7d66de053c2b0c92b8cf8733dae04503bf26dfd7cc798b5ef'
            '5c4a77e85a85b4b23189a94466eedf5e8103f9583792720b61df1b7044b38796'
            'b2da1a00ac59ce7e98fd6cfb554f8974b1500bc1b9973387181414ffa4cffea5'
            'SKIP')

prepare() {
  cd ${pkgname}-igc-${pkgver}
  # https://github.com/intel/intel-graphics-compiler/issues/209
  patch -p1 < ../${pkgname}-fix-209.patch
  patch -p1 < ../${pkgname}-fix-lld-prebuilt.patch
}

build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='13.0.0' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_KHRONOS_SPIRV_TRANSLATOR_IN_VC=ON \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
