# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cogapp
pkgver=3.1.0
pkgrel=1
pkgdesc="A code generator for executing Python snippets in source files"
url="https://nedbatchelder.com/code/cog"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/c/cogapp/cogapp-$pkgver.tar.gz")
sha512sums=('f60657facd210a3a0e13a85107e63bf217d267c69406eef429450cd16d0b136dc97fdb6fda199922f515a62be4d54d9b01210a5cf83ec330581dba66085e9a80')

build() {
  cd cogapp-$pkgver
  python setup.py build
}

check() {
  cd cogapp-$pkgver
  python setup.py pytest
}

package() {
  cd cogapp-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
