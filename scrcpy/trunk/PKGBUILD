# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=1.22
pkgrel=2
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('2cc9867ab854e81e502b1127e275f5bea7f061f19b08c94bf53c6ef6563c537e56f8531444e1ba7eaebd4f768946d1f19dab7f9cc7bce82eda0edf5fea64e79f'
        '691217a8e776eca14f4d40da3de50fa98687664747de8ef246d629a5a9f9394296b82760696e1b31a44287e3678cbd3e6be18c89e8347d6a5ce7c6bf7a1a3790')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server="../$pkgname-server-v$pkgver"
  ninja -v -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}
