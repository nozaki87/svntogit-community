# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Morgenstern <charles [at] charlesbwise [dot] com>

pkgname=cherrytree
pkgver=0.99.39
pkgrel=2
pkgdesc="Hierarchical note-taking application"
arch=('x86_64')
url="https://www.giuspen.com/cherrytree/"
license=('GPL3')
depends=('fmt' 'gspell' 'gtksourceviewmm' 'libxml++2.6' 'uchardet')
makedepends=('cmake' 'python' 'spdlog')
checkdepends=('xorg-server-xvfb')
source=("https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz"{,.asc})
sha512sums=('33f79f328dccc5d3a9da00b7ac50d91cad245f8045e85df6b81ca84a2eb39ef0c882bc5cf08f7ae6d01289ac97ec884cf92a1e1216bc8ff592fb5d8b7e8e1635'
            'SKIP')
validpgpkeys=('C7BF38CE0BD442C2369AA984049128A20CE0648D') # Giuseppe Penone <giuspen [at] gmail [dot] com>

build() {
  cmake \
  -B "${pkgname}_${pkgver}/build" \
  -S "${pkgname}_${pkgver}" \
  -DINSTALL_GTEST:BOOL='OFF' \
  -DAUTO_RUN_TESTING:BOOL='OFF' \
  -Wno-dev
  make -C "${pkgname}_${pkgver}/build"
}

check() {
  cd "${pkgname}_${pkgver}"
  xvfb-run ./"${_pkgname}/build/run_tests_with_x_1"
  xvfb-run ./"${_pkgname}/build/run_tests_with_x_2"
}

package() {
  make -C "${pkgname}_${pkgver}/build" DESTDIR="${pkgdir}" install
}
