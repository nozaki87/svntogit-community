# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.8.0
pkgrel=1
pkgdesc="Access the Mediathek of many German TV stations"
arch=('any')
url="https://github.com/mediathekview/mediathekview"
license=('GPL3')
depends=('bash' 'hicolor-icon-theme' 'java-runtime')
makedepends=('maven')
conflicts=('mediathek')
provides=('mediathek')
replaces=('mediathek')
optdepends=('libnotify: to use desktop notifications'
            'mplayer: for recording streams'
            'vlc: for stream playback')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
        "de.${pkgname}.${_name}.desktop"
        "${pkgname}.sh")
sha512sums=('67624a5d7303a51ef4d57d0ef6cfbd2bd59ee9e7f0df5aed53a270a7b4ea3270f0eedb41a363cb19062b1b312b4ef144ffc5034720bb58fb3f83cc88af4a3c04'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '3b4b96bd7f8dd26b9fbc71b109c57257eb57969bfc67dabb87b4d541da37b196f54534eb38266c0b74407c5493aa35db3dd1374b84ab562eb02a02d1660e2ec8')
b2sums=('c3776a200ccd12253869e3b7fccb51f06a2d2564e0868391f1e761df754b0ec2ad669c8c201b849835a63df79f2944e33d4be0ffca1dee29e882e7b19d17a468'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        'db19620fdde16e95df7b866745d95484571db421c791d2f8e28e5b96c59347dc13cc24fab69f07a6d32d5567bdefd8dda7a7057018be7a98a8186d3b156aa298')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./mvnw clean install -Plinux,install4j-linux
}

package() {
  local _size

  cd "${pkgname}-${pkgver}"
  # jar
  install -vDm 644 "target/${_name}.jar" \
    -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
