# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=copr-cli
pkgver=1.95
pkgrel=1
pkgdesc="CLI tool to run copr"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-copr' 'python-humanize' 'python-simplejson' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-responses')
source=("https://pagure.io/copr/copr/archive/copr-cli-$pkgver-1/copr-copr-cli-$pkgver-1.tar.gz")
sha512sums=('b747a06975d2846df5e5cd1a66c58c55caac2bb75118eea80294104e005b358c25f3a814790aed064bb625feb65e8934f88a62a1fc99bc1ce2a5124f0deec015')

build() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py build
}

check() {
  cd copr-copr-cli-$pkgver-1/cli
  python -m pytest
}

package() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py install --root="$pkgdir" --optimize=1
}
