# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=3.2.2
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(poppler-glib webkit2gtk xapps)
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'mathjax2: support for math in epub files'
            'texlive-bin: support for dvi files')
makedepends=(meson samurai intltool itstool gobject-introspection djvulibre
             libgxps libspectre texlive-bin)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('fb2ba147ffbe64b47ff8967cfc49930f68867e6a6ab5d8e496208fc0e03cf481')
b2sums=('ae69e3b3dc21b0d2639ed712b5d420679d6afff1ce97321a477c2381c9e9493c6d0e10f5e3182fb6d796d681fbb3fc785f3224bc01a6b5087f789b67c59e97c9')

build() {
  mkdir -p build
  cd build
  arch-meson --libexecdir=lib/${pkgname} \
    -Dmathjax-directory=/usr/share/mathjax2 \
    -Dcomics=true \
    -Ddjvu=true \
    -Ddvi=true \
    -Dt1lib=true \
    -Dpixbuf=true \
    -Dhelp_files=true \
    -Dintrospection=true \
    ../${pkgname}-${pkgver}
  samu
}

package(){
  cd build
  DESTDIR="${pkgdir}" samu install
}
