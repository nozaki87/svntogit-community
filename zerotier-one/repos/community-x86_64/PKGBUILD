# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.6.6
pkgrel=1
pkgdesc="Creates virtual Ethernet networks of almost unlimited size."
arch=('x86_64')
url="https://www.zerotier.com/"
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz")
sha512sums=('bc3716803d60e58806762b21c851402ae40fddbd94d53a6b54d2e398173e6c790d45c551baf6e07f6be5e19a301a40eaadab32529259999ddea78d234dc804ac')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
