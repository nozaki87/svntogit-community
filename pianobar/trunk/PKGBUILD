# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2020.11.28
pkgrel=2
pkgdesc="Console-based frontend for Pandora"
arch=('x86_64')
url="https://6xq.net/pianobar/"
license=('MIT')
depends=('libao' 'ffmpeg' 'curl' 'json-c')
source=(https://6xq.net/pianobar/$pkgname-$pkgver.tar.bz2{,.asc}
        https://github.com/PromyLOPh/pianobar/commit/b14c9d6b.patch
        ffmpeg5.patch)
validpgpkeys=('017D74E27F5856963801781DF663943E08D8092A') # Lars-Dominik Braun <lars@6xq.net>
sha256sums=('653bfb96b548259e3ac360752f66fdb77e8e220312e52a43c652f7eb96e7d4fe'
            'SKIP'
            '01ac6fa2fa7ee7aad022bbac4ad3aaf47b44ed29c3ba88536b1b24ccc126b1ab'
            '5e4aa07d03f273a815515f99b441cacc4e0a4b69ad5728a424b1911cb23fde76')

prepare() {
  # Fix build with FFmpeg 5
  patch -d $pkgname-$pkgver -p1 < b14c9d6b.patch
  patch -d $pkgname-$pkgver -p1 < ffmpeg5.patch
}

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm644 $pkgname-"${pkgver}"/COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 $pkgname-"${pkgver}"/contrib/headless_pianobar "${pkgdir}"/usr/bin/pianoctl
}
