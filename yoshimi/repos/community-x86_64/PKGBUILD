# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.1.1.1
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'zlib')
makedepends=('alsa-lib' 'cmake' 'fftw' 'fltk' 'jack' 'lv2' 'mxml' 'ncurses'
'readline')
checkdepends=('kxstudio-lv2-extensions' 'lv2lint')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'readline: for standalone application'
            'ncurses: for standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('4feaf7b779d450e4d8f9612a141fdcd2858117c738c56bb43fa12a19f9806d6c990e560dfc62755c7d39e23608f516fb017b0c4063440996c5c17286a58bf946')
b2sums=('eec292d489c01054ff3e7b4402a359d5abc29157048b475df8482630dccaae9fdff6cc697d95614778e34d363215633b0dc8beaeb1c832c02f98f0f21384b42e')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfltk.so' 'libfltk_images.so'
  'libjack.so' 'libmxml.so' 'libncursesw.so' 'libreadline.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
