# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio
pkgver=2.1.0
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz)
sha512sums=('a0f4fb62c768b392ca1129e2046df96d670d785fc057d4103768440b7adb65d8faf5b7d39729060791794e83e6cd4e67821aed9b30f3f04b410c60809cd72267')

build() {
  cd OpenColorIO-$pkgver

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja -C build
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
