# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=6.2.0
pkgrel=1
pkgdesc='Switchboard Online Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL2)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk3
  json-glib
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libgee
  libgranite.so
  libhandy-1.so
  libswitchboard-2.0.so
  rest
  webkit2gtk
)
makedepends=(
  git
  meson
  vala
)
_tag=da8b244b641b086ef1d72e753c3ab5675e333048
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-online-accounts
  git describe --tags
}

build() {
  arch-meson switchboard-plug-online-accounts build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
