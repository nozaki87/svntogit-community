# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=vmaf
pkgver=2.3.0
pkgrel=1
pkgdesc='Perceptual video quality assessment algorithm based on multi-method fusion'
arch=('x86_64')
url='https://github.com/Netflix/vmaf/'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson' 'nasm' 'vim' 'doxygen')
options=('!lto')
source=("https://github.com/Netflix/vmaf/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d8dcc83f8e9686e6855da4c33d8c373f1735d87294edbd86ed662ba2f2f89277')

build() {
    arch-meson "${pkgname}-${pkgver}/libvmaf/build" "${pkgname}-${pkgver}/libvmaf"
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build"
}

check() {
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" test
}

package() {
    DESTDIR="$pkgdir" ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" install
    install -D -m755 "${pkgname}-${pkgver}/libvmaf/build/tools/vmafossexec" -t "${pkgdir}/usr/bin"
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    cp -dr --no-preserve='ownership' "${pkgname}-${pkgver}/model" "${pkgdir}/usr/share"
}
