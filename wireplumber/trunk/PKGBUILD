# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=wireplumber
pkgname=('wireplumber' 'wireplumber-docs')
pkgver=0.4.2
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
makedepends=('cmake' 'cpptoml' 'doxygen' 'glib2' 'gobject-introspection'
'hotdoc' 'lua' 'meson' 'pipewire' 'python-breathe' 'python-sphinx_rtd_theme'
'python-sphinx' 'systemd')
checkdepends=('pipewire-alsa' 'pipewire-jack' 'pipewire-pulse')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/pipewire/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8ded43fba5de58068516558918d49c86be7d67330e8a14e735561fefadba5e27952d556987b7304dead52b4e2f72a7d9626da0170e768c4702dd164840b08e3c')
b2sums=('4747ae405a824b019415188ff1a586d32fc2e97aba6777cd1e403aadf0e44458156d1f8b6dbc706b812a7ea8c873d0f506f2b42817d1bc5312874530edaf7234')

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
    -Dsystem-lua=true \
    -Dsystemd-system-service=true \
    -Dsystemd-user-service=true \
    build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test || echo "Known to fail: https://gitlab.freedesktop.org/pipewire/wireplumber/-/issues/18"
}

package_wireplumber() {
  depends=('gcc-libs' 'glibc' 'lua' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgmodule-2.0.so' 'libgobject-2.0.so' 'libpipewire-0.3.so')
  provides=('libwireplumber-0.4.so')

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove files provided by wireplumber-docs
  rm -frv "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_wireplumber-docs() {
  pkgdesc+=" (documentation)"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # remove files provided by wireplumber
  rm -frv "${pkgdir}/usr/"{bin,include,lib,share/{gir*,wireplumber}} "${pkgdir}/etc/"
}
