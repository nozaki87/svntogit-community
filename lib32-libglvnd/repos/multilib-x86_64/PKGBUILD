# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=1.3.4
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('lib32-libx11' 'lib32-libxext' 'xorgproto' 'python' 'meson')
provides=('lib32-libgl' 'lib32-libegl' 'lib32-libgles')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/glvnd/libglvnd/-/archive/v${pkgver}/libglvnd-v${pkgver}.tar.gz")
sha512sums=('52bb91884fe7686e3fec1bfbc1b6ebee4fbeab836792995d32cbe204ee162e542e8ffa96813120ea3f54a588aee20cad7426abedbd29c4c7851d7fe3cb9c2574')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson libglvnd-v$pkgver build \
    --libdir=/usr/lib32 \
    -D headers=false \
    -D gles1=false

  ninja -C build
}

package() {
  # lib32-libglvnd needs lib32-mesa for indirect rendering
  depends=('lib32-libxext' 'libglvnd' 'lib32-mesa' 'lib32-opengl-driver')

  DESTDIR="$pkgdir" ninja -C build install

  rm -r "$pkgdir"/usr/include

  mkdir -p "$pkgdir"/usr/share/licenses
  ln -s libglvnd "$pkgdir"/usr/share/licenses/$pkgname
}
