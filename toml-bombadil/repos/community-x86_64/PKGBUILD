# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=toml-bombadil
pkgver=3.0.0
pkgrel=2
pkgdesc="Adotfile manager with templating"
arch=('x86_64')
url="https://github.com/oknozor/toml-bombadil"
license=('MIT')
depends=('gcc-libs' 'zlib')
makedepends=('rust')
options=(!lto)
source=($pkgname-$pkgver.tar.gz::https://github.com/oknozor/toml-bombadil/archive/${pkgver}.tar.gz)
sha512sums=('5f70eab633dc747ff1545ca4150d10898276e527a3f32639e10f25148c045a170e11409da782cc9a1e0a997d2da39672703a7c2d90ba53d4c7a5e4c139f69c40')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/bombadil "$pkgdir"/usr/bin/bombadil

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/bombadil generate-completions bash > "$pkgdir"/usr/share/bash-completion/completions/bombabil
  target/release/bombadil generate-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_bombabil
  target/release/bombadil generate-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/bombabil.fish

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
