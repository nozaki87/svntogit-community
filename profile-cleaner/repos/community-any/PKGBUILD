# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=profile-cleaner
pkgver=2.42
pkgrel=1
pkgdesc="Simple script to vacuum and reindex sqlite databases used by browsers"
arch=('any')
url="https://github.com/graysky2/profile-cleaner"
license=('MIT')
depends=('bash' 'bc' 'findutils' 'parallel' 'sqlite')
optdepends=('zsh-completions: for completion when using zsh')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('4f320ebc5d4dc5256c2bb4781d3c0ae36b620fbc28152d54000f6defc28e3de89f7d1a82cc908d8e7edca2083fa302b620fff78948388cc2dac98b8fcbd4d4c7')
b2sums=('723c25ddf5977001a0f8919a72c783e360359fdd0da1c1f3adc46c15e40855e3569645396126eb2e69555cc0b8b029a3b841fd66b72ee45393ac68ed5dcc3c4b')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
