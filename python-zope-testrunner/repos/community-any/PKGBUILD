# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-testrunner
pkgver=5.3.0
pkgrel=2
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface'
         'python-zope-exceptions' 'python-six')
makedepends=('python-setuptools' 'python-zope-interface'
             'python-zope-exceptions' 'python-six')
checkdepends=('python-zope-testing' 'python-zope-testrunner'
              'python-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.testrunner/archive/$pkgver.tar.gz")
sha512sums=('8d443b80663248286d7695c3d82bf7e33d26ec9e7efbafc1ab780da144f40faf2ad8b18af490842122f73346ca60cb4dd2857737fe4fc4e165f911d8a7eaa7c3')

prepare() {
  cp -a zope.testrunner-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.testrunner-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.testrunner-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py test
  )
}

package() {
  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
