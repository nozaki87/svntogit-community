# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-cassowary
_rockname=${pkgbase#lua-}
_project=$_rockname.lua
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=2.3.1
_rockrel=1 # upstream rockrel 2 is only needed for installing with Luarocks 2.x
pkgrel=2
pkgdesc='The cassowary constraint solver'
arch=(any)
url="https://github.com/sile-typesetter/$_project"
license=(MIT)
_luadeps=(penlight)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$_project-$pkgver"
source=("$_archive.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('1cdf42d8736ac39becef7d0ff635317e1b0952968c9c8a5a2755a25aed17499a')

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}" "${_luadeps[@]/#/${pkgname%-*}-}")
	luarocks --lua-version="$1" --tree="$pkgdir/usr/" \
		make --deps-mode=none --no-manifest "rockspecs/$_rockname-$pkgver-$_rockrel.rockspec"
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-cassowary() {
	_package 5.4
}

package_lua51-cassowary() {
	_package 5.1
}

package_lua52-cassowary() {
	_package 5.2
}

package_lua53-cassowary() {
	_package 5.3
}
