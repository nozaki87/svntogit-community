# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-filetype
pkgver=1.0.8
pkgrel=1
pkgdesc="Infer file type and MIME type of any file/buffer"
url="https://github.com/h2non/filetype.py"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/h2non/filetype.py/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d461a0bb4de9d1dda29c6269abfa1feb692628029329a1b31ebfc3a02b7c01c16a6f3a0f261fbbee7ce6f78433a556212951fc9bfb9dfd674b86dc1bcc9e7786')

build() {
  cd filetype.py-$pkgver
  python setup.py build
}

check() {
  cd filetype.py-$pkgver
  python -m unittest discover
}

package() {
  cd filetype.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
