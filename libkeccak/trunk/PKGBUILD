# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=libkeccak
pkgver=1.2.2
pkgrel=1
pkgdesc='Keccak-family hashing library'
arch=('x86_64')
url='https://github.com/maandree/libkeccak'
license=('custom:ISC')
depends=('glibc')
checkdepends=('valgrind')
source=("https://github.com/maandree/libkeccak/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('8b171d1ba972119db0aba601e909428e3a074c21b7212b1705e201e351e58d1c5663b357769fff1f0075e6ac151224f9895d7523d3cb69de7f79780971ac589a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' optimised.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr
}

check() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk check
}

package() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr DESTDIR="${pkgdir}" install
}
