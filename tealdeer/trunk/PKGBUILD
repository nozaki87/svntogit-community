# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
_binname=tldr
pkgver=1.4.1
pkgrel=6
pkgdesc="A fast tldr client in Rust"
arch=('x86_64')
url="https://github.com/dbrgn/tealdeer"
license=('MIT' 'Apache')
depends=('openssl')
makedepends=('cargo')
optdepends=(
  "bash-completion: for shell auto-completion"
  "fish: for shell auto-completion"
  "zsh-completions: for shell auto-completion"
)
provides=('tldr')
conflicts=('tldr')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('fe056b0445faad3e802ee895bd31669e405b5dd86622d2bfe8b5ec58d66d9bd338215a7c3b5aa66fec243c1a90f9ce0958e13109400336304652c6da9c9f4b1d')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --all-features --target-dir=target
}

check() {
  cd "$pkgname-$pkgver"
  # Skip test_markdown_rendering until next release
  # Ref: https://github.com/dbrgn/tealdeer/pull/192
  cargo test --locked --target-dir=target -- --skip test_markdown_rendering
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$_binname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/$_binname"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/$_binname"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_$_binname"
}

# vim:set ts=2 sw=2 et:
