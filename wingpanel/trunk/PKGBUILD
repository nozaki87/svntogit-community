# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.0
pkgrel=2
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter6
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
_tag=aff50fc64b687cf6c687ff4a194e0b936f67b6ff
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd wingpanel

  git describe --tags
}

build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
