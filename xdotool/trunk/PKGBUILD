# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Rttommy <rttommy@gmail.com>

pkgname=xdotool
pkgver=3.20210903.1
pkgrel=1
pkgdesc="Command-line X11 automation tool"
arch=('x86_64')
url="https://www.semicomplete.com/projects/xdotool/"
license=('BSD')
depends=('libxtst' 'libxinerama' 'libxkbcommon')
source=(https://github.com/jordansissel/xdotool/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('9110198702d7549c4eccdab95f276d35a9fa9f540015d2739b62c55618d3b7b6')

build() {
  cd $pkgname-$pkgver
  make WITHOUT_RPATH_FIX=1
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" INSTALLMAN="$pkgdir/usr/share/man" install
  install -Dm644 COPYRIGHT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
