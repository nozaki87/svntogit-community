# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.5
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(alsa-lib cmake jack2 qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('83f9bb1a66f9c23bd59b874442424f5c60babcf9086baad12d383c8a0839f46e0bacf07fb03844c877e55d622923f623b09c3ebe532cdc36f4b244e2ee7d58cd')
b2sums=('fc2e0a795780ebad123c9eb31934e37d72feb35d9479f2a3e16a02a00cb7f20af385567339d34727a56b79ac2da14a9dc1726c7882be6e9ca4112aeb0948f20d')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libjack.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
