# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-prelude
pkgname=haskell-clash-prelude
pkgver=1.4.5
pkgrel=1
pkgdesc="Clash: a functional hardware description language - Prelude library"
url="https://github.com/clash-lang/clash-prelude"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-arrows' 'haskell-bifunctors' 'haskell-constraints'
         'haskell-data-binary-ieee754' 'haskell-data-default-class' 'haskell-ghc-typelits-extra'
         'haskell-ghc-typelits-knownnat' 'haskell-ghc-typelits-natnormalise' 'haskell-half'
         'haskell-hashable' 'haskell-interpolate' 'haskell-lens' 'haskell-quickcheck'
         'haskell-recursion-schemes' 'haskell-reflection' 'haskell-singletons'
         'haskell-th-abstraction' 'haskell-th-lift' 'haskell-text-show' 'haskell-th-orphans'
         'haskell-type-errors' 'haskell-uniplate' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hedgehog'
             'haskell-hint' 'haskell-quickcheck-classes-base' 'haskell-tasty'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-tasty-th')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        hashable-1.4.patch)
sha512sums=('1412af14bad4c8e158a105c5f226040e3e452e18712f827a32a58d8f624275d3cbbfbbadc4b1d1f9026b8f77b7cf143963f5e870cc1f466361c79ef1594d08db'
            '84971d5f70f21e8962436e5c14d627a3fddb9805b505fa7ade82c22b59d94912d52d28bb509906e2e6acfcf6432c54b8d7837fc1eae5149a2ce42d51c562934a')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../hashable-1.4.patch
    uusi -u ghc-bignum $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
