# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-xlsxwriter
pkgver=3.0.1
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('27d053943ee0fe302d711687ad37335187b9352db001f3b5d6b1c527a528667f')

build() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

check() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python -m unittest discover
}

package() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
