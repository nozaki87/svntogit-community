# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-tarpaulin
pkgver=0.18.5
pkgrel=1
pkgdesc='Tool to analyse test coverage of cargo projects'
arch=(x86_64)
url=https://github.com/xd009642/tarpaulin
license=(Apache)
depends=(
  gcc-libs
  glibc
  libcurl.so
  openssl
  zlib
)
makedepends=(
  git
  rust
)
_tag=6d665bea816c3ea4458aacacfaed601a8f9b493c
source=(git+https://github.com/xd009642/tarpaulin.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd tarpaulin

  git describe --tags
}

prepare() {
  cargo fetch \
    --locked \
    --target $CARCH-unknown-linux-gnu \
    --manifest-path tarpaulin/Cargo.toml
}

build() {
  export CARGO_TARGET_DIR=target
  cargo build \
    --release \
    --frozen \
    --manifest-path tarpaulin/Cargo.toml
}

package() {
  install -Dm 755 target/release/cargo-tarpaulin -t "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
