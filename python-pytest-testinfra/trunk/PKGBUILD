# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=pytest-testinfra
pkgname=python-pytest-testinfra
pkgver=6.6.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
arch=(any)
url="https://github.com/pytest-dev/pytest-testinfra"
license=(Apache)
depends=(python-pytest)
makedepends=(python-setuptools-scm python-sphinx)
checkdepends=(ansible python-mock python-paramiko python-pytest-cov
python-pytest-xdist python-pywinrm python-tornado salt)
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts'
            'salt: for tests using saltstack')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f78f1cd5240e157d4e2949178f29900a7b85a6065b4113b18273af3f1fef6b19c859192f59426b93eba48418bb89d163ad1d951e97d4fdfcea4394635a05140a')
b2sums=('c7583685aeb770f76e4679730599909b8fff53d1909664f55e2d35f7f5a6a96741e3aa56b81da464c2d474d1d5f646892e36a4f9b6938327cb526476536b0b6c')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  # man page
  install -Dm 644 doc/build/man/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
