# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.7.1
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=(x86_64)
url="https://www.laborejo.org/fluajho/"
license=(GPL3)
groups=(pro-audio)
depends=(glibc hicolor-icon-theme python python-pyqt5 ttf-dejavu)
makedepends=(fluidsynth glib2 jack libsndfile util-linux-libs ncurses)
optdepends=('new-session-manager: Session management')
provides=(soundfont-synthesizer)
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('4b7798796ab3133cc55b9fa705cc990055de21b51c7099bc1ae5e55cdcf3167b613265f288634fa61e88c3ee5467946e799c9c7d48605c91030904d602d7ed50'
            'SKIP')
b2sums=('de632e9fab25b0ce0f69a897f8cd1f61eca7f5a67a5864476785962998d7f3f78c718ba55a8ba05a3645a52c383104f133b975a69899d805d61ce5e34cd44716'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=(libfluidsynth.so libglib-2.0.so libjack.so libncursesw.so
  libsndfile.so libuuid.so)

  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
