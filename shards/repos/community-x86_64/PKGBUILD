# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.15.0
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libevent gc)
makedepends=(crystal)
checkdepends=(git)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('89ad7f7550a6036b376008c9e376a3b9fbf4aca9f7d2eae5086282122e92711a')

build() {
  cd shards-$pkgver
  CRYSTAL_OPTS="--release" make
}

check() {
  cd shards-$pkgver

  # remove git config settings once https://github.com/crystal-lang/shards/issues/430 is fixed
  git config --global user.email "you@example.com"
  git config --global user.name "Your Name"
  git config --global column.ui always

  make test -j1 # -j1 is due to this bug https://github.com/crystal-lang/shards/issues/347
}

package() {
  cd shards-$pkgver
  PREFIX=/usr DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
