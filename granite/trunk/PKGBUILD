# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=6.1.2
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=(x86_64)
url=https://github.com/elementary/granite
license=(LGPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libgee
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
provides=(libgranite.so)
_tag=dde6e409d362b337fb7add3548fb2927793559d7
source=(git+https://github.com/elementary/granite.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd granite

  git describe --tags
}

build() {
  arch-meson granite build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
