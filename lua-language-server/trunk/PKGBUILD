# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Mohammad AlSaleh <CE.Mohammad.AlSaleh at gmail dot com>

pkgname=lua-language-server
pkgver=2.4.7
pkgrel=1
license=('MIT')
pkgdesc='Lua Language Server coded by Lua'
url=https://github.com/sumneko/lua-language-server
arch=('x86_64')
depends=('bash' 'gcc-libs')
makedepends=('git' 'ninja')
source=("git+$url.git#tag=$pkgver"
        'git+https://github.com/actboy168/bee.lua.git'
        'git+https://github.com/love2d-community/love-api.git'
        'git+https://github.com/sqmedeiros/lpeglabel.git'
        'git+https://github.com/actboy168/luamake.git'
        'git+https://github.com/electron/rcedit.git'
        'wrapper')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '0e7bac7ff59c86dc9d6124a139773586846d6224b7b6c7941e1b6672c4c387746bc3d3fb85a62225e4f1740774273648109783281686a957b650838f7f1e10cd')

prepare() {
  cd $pkgname
  git submodule init
  for i in ../{bee.lua,love-api,lpeglabel,luamake,rcedit}; do
    git config submodule.3rd/${i##../}.url $i
  done
  git submodule update
  cd 3rd/luamake
  git submodule init
  git config submodule.3rd/bee.lua.url ../../../bee.lua
  git submodule update
}

build() {
  cd $pkgname
  ninja -C 3rd/luamake -f compile/ninja/linux.ninja
  ./3rd/luamake/luamake rebuild
}

package() {
  install -D wrapper "$pkgdir"/usr/bin/$pkgname
  cd $pkgname
  install -Dt "$pkgdir"/usr/lib/$pkgname/bin/Linux bin/Linux/$pkgname
  install -m644 -t "$pkgdir"/usr/lib/$pkgname/bin/Linux bin/Linux/main.lua
  install -m644 -t "$pkgdir"/usr/lib/$pkgname {debugger,main}.lua
  cp -r locale meta script "$pkgdir"/usr/lib/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
