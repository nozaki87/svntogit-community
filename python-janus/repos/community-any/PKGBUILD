# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=python-janus
pkgver=0.6.2
pkgrel=1
pkgdesc="Thread-safe asyncio-aware queue for Python"
arch=('any')
url="https://github.com/aio-libs/janus"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-asyncio')
source=("https://github.com/aio-libs/janus/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('541b6a244b1eb6bf040c275803b6992bc49d649ffb021d762986a7dd9a7848bc9b9b319d2b8097faa5b7bcf0c510cecd571b6648ca560ed94ad6471527bda6e3')

build() {
  cd janus-$pkgver
  python setup.py build
}

check() {
  cd janus-$pkgver
  python -m pytest
}

package() {
  cd janus-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
