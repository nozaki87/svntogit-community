# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-podman
pkgver=1.1.0
pkgrel=1
pkgdesc="Molecule Podman Driver allows use of podman as backend for testing"
arch=(any)
url="https://github.com/ansible-community/molecule-podman"
license=(MIT)
depends=(molecule python-ansible-compat python-selinux)
makedepends=(python-setuptools-scm python-setuptools-scm-git-archive)
checkdepends=(podman python-pytest python-pytest-helpers-namespace)
optdepends=('podman: for using a local podman setup')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('230dc1750405fd76bff894bbb181095164de935527484e2f29a3cecfbfb68c7d4adab0f7de55b2dc4dd53ce1da006c37b1258b91c8f4944e019f726fb35d56aa')
b2sums=('120b9377a30b5abde6fd9ed3765ed45cf71354ee9845f30aa867d0775a54726160b1a275d0ed0ff12ff0aab096e230b9c36615dddd3beee50047929df69732e8')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  pytest -v -c /dev/null --ignore src/molecule_podman/test/test_func.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
