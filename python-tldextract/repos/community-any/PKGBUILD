# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=3.2.0
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-filelock')
makedepends=('python-setuptools-scm' 'python-pip')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses'
              'python-isort')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('4e0731792793eb0a6d0da8387d15e8b9de17f20108361230703fd9f66f2aaad6260073b611e27135e571b1899844b271f3dae4e77122186cc78fbb05f3b3f237')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  # tldextract/_version.py is generated by setuptools-scm, and will fail pylint here.
  python -m pytest --ignore build --ignore tldextract/_version.py
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set sw=2 et:
