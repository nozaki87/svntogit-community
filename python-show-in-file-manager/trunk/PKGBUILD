# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

_name=show-in-file-manager
pkgname=python-$_name
pkgver=1.1.3
pkgrel=1
pkgdesc="Python module to open the system file manager and select files in it"
arch=('any')
url="https://github.com/damonlynch/showinfilemanager"
license=('MIT')
depends=('python-pyxdg' 'python-packaging')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz)
sha256sums=('7cb4b27435f3573feca311e90cf696316eb26d5af7c7ef486c025ca8ccb65a92')

build() {
  cd $_name-$pkgver
  python setup.py build
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
