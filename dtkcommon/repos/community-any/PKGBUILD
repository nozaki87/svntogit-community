# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkcommon
pkgver=5.5.20
pkgrel=1
pkgdesc='DTK common files'
arch=('any')
url="https://github.com/linuxdeepin/dtkcommon"
license=('LGPL3')
depends=('dconf')
makedepends=('qt5-tools')
source=("https://github.com/linuxdeepin/dtkcommon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('92375acb25f09aca1f99cea36de368aeba8c4911e181bd77fc8100d3c6eab8771aeb042c99a77112577497be37ae316ed7963a464f2ac291ba5b8e99965e7ff6')

prepare() {
  cd dtkcommon-$pkgver
  sed -i 's|qt5/mkspecs|qt/mkspecs|;s|/etc/dbus-1|/usr/share/dbus-1|' dtkcommon.pro
}

build() {
  cd dtkcommon-$pkgver
  qmake-qt5 PREFIX=/usr VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkcommon-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
