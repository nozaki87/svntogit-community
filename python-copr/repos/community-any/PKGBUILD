# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-copr
pkgver=1.115
pkgrel=1
pkgdesc="Python client for copr service"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-marshmallow' 'python-requests' 'python-requests-toolbelt' 'python-six' 'python-munch')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://pagure.io/copr/copr/archive/python-copr-$pkgver-1/copr-python-copr-$pkgver-1.tar.gz")
sha512sums=('01530112e6f36781c56806d46800fdd83b79565b4300225ecc0355c1b93d68fd773b1fc5957543aab3a5ada6b08f0a0ff53ff5769261b80eae5329181963a2e5')

build() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py build
}

check() {
  cd copr-python-copr-$pkgver-1/python
  python -m pytest
}

package() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py install --root="$pkgdir" --optimize=1
}
