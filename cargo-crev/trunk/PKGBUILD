# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.23.0
pkgrel=2
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/crev-dev/cargo-crev"
depends=('openssl' 'libcurl.so' 'libgit2.so')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/crev-dev/cargo-crev/archive/v${pkgver}.tar.gz")
sha512sums=('ed272fd8175cfffd7a3cef76f205423e95a35a869480f73a122fc4d596373e23b1ec72714c8c78a3223073222c140faf9af357447bf83b7120a862096aaac5f6')
b2sums=('1bfdaa3de0b691e1276aa35e1f60291ebb827a6b2c31f3df1cb88d7ac4dd0c70fdabaa036c5fba0fe8997766d84e0670b31155f56bbb603d9e5a71f8bfcdf94b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release --no-default-features
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
