# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.9.2
pkgrel=2
pkgdesc='Batch image converter and resizer'
arch=('x86_64')
url='http://converseen.fasterland.net/'
license=('GPL3')
depends=('hicolor-icon-theme' 'imagemagick' 'qt5-base')
makedepends=('cmake' 'qt5-tools') 
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
         converseen-IM-formats.patch)
sha256sums=('4dfaac49e451dd1e6734e6d377508680138d7262c08abed366a69bd498636fac'
            'd9ec38860664541ba7aed7671378f835a8353a95967ecdedf30e5ab83ac99c2c')

prepare() {
  cd $pkgname-$pkgver
  convert res/converseen.png +set date:create +set date:modify -resize 256x256 -alpha on res/converseen.png
  sed -i 's|/share/pixmaps|/share/icons/hicolor/256x256/apps|' CMakeLists.txt

  patch -p1 < ../converseen-IM-formats.patch # Don't crash on missing imagemagick dependencies
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
