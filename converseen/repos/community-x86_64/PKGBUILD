# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.9.1
pkgrel=1
pkgdesc='Batch image converter and resizer'
arch=('x86_64')
url='http://converseen.fasterland.net/'
license=('GPL3')
depends=('djvulibre' 'ghostscript' 'hicolor-icon-theme' 'libheif' 'imagemagick' 'libraw' 'librsvg'
         'libwebp' 'libwmf' 'libzip' 'openexr' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('b5615a76a87e6bcb064ac0d5032f606bba0666815c178b27903806349818d30e')

prepare() {
  cd $pkgname-$pkgver
  convert res/converseen.png +set date:create +set date:modify -resize 256x256 -alpha on res/converseen.png
  sed -i 's|/share/pixmaps|/share/icons/hicolor/256x256/apps|' CMakeLists.txt
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
