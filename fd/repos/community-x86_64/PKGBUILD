# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=fd
pkgver=8.3.2
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find'
arch=('x86_64')
url=https://github.com/sharkdp/fd
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("fd-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('83778d86ef1ea3e00ba40c7dca5fd743a9eb72362a57fb964d808a6919d0bbb2b42439c03107f98fe534dd0977eb9e414e4232afb808abe9994d70f2a81d925b')

build() {
  cd fd-$pkgver
  export CFLAGS+=" -ffat-lto-objects"
  cargo build --release --locked
}

check() {
  cd fd-$pkgver
  cargo test --release --locked
}

package() {
  cd fd-$pkgver
  install -Dm755 target/release/fd "$pkgdir"/usr/bin/fd
  install -Dm644 target/release/build/fd-find-*/out/fd.bash "$pkgdir"/usr/share/bash-completion/completions/fd
  install -Dm644 target/release/build/fd-find-*/out/fd.fish "$pkgdir"/usr/share/fish/vendor_completions.d/fd.fish
  install -Dm644 contrib/completion/_fd "$pkgdir"/usr/share/zsh/site-functions/_fd
  install -Dm644 doc/fd.1 "$pkgdir"/usr/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/fd/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
