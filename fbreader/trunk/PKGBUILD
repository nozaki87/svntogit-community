# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fbreader
pkgver=0.99.5
pkgrel=1
pkgdesc="An e-book reader for Linux"
arch=('x86_64')
url="http://www.fbreader.org/"
license=('GPL')
depends=('fribidi' 'bzip2' 'curl' 'qt5-base' 'libunibreak' 'sqlite')
source=($pkgname-$pkgver.tar.gz::https://github.com/geometer/FBReader/archive/refs/tags/$pkgver.tar.gz
	fbreader-gcc6.patch
	fbreader-qt5.patch)
sha256sums=('5e4fe6cd18d2fe3f8ea36084da5f15c961479e6e639fdeeff8db293e02471e3f'
            'bc67b538c51a5b77468469df6b5e96ce918ee11a5ff4e88e398ad5661d322f9c'
            '9d00f95f2a026b7bdafa6ab8519659cd2c293f1b9b00681a62d3f4992399e716')

prepare() {
  cd FBReader-$pkgver

#  sed -i "s|VERSION|\"$pkgver\"|" fbreader/src/fbreader/FBReader.cpp
  patch -p0 -i ../fbreader-gcc6.patch # Fix build with GCC6 (Gentoo)
  patch -p1 -i ../fbreader-qt5.patch # Port to Qt5 (Gentoo)

  # Use system FLAGS
  sed -e "s/^CFLAGS = -pipe/CFLAGS +=/" -i makefiles/arch/desktop.mk
  sed -e "/^	CFLAGS +=/d" -i makefiles/config.mk
  sed -e "/^	LDFLAGS += -s$/d" -i makefiles/config.mk
  sed -e "/^LDFLAGS =$/d" -i makefiles/arch/desktop.mk
  sed -e 's|which ccache >|which ccache 2>|' -i makefiles/arch/desktop.mk
}

build() {
  export TARGET_ARCH=desktop
  export TARGET_STATUS=release
  export CXXFLAGS+=" -fPIC"
  export UI_TYPE=qt
  cd FBReader-$pkgver
  make
}

package() {
  cd FBReader-$pkgver
  export CXXFLAGS+=' -fPIC'
  make DESTDIR="$pkgdir" install
}
