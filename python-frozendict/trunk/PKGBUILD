# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-frozendict
pkgver=2.0.7
pkgrel=1
epoch=1
pkgdesc='An immutable dictionary'
url='https://pypi.python.org/pypi/frozendict/'
arch=('any')
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/f/frozendict/frozendict-$pkgver.tar.gz")
sha512sums=('2ebe1db9de956dfcf441268a45e28cc5742b22f78a7182267185c28af6d56c6e2345af55ad4292490222791fc9e07abbd72813d9176529171030b4175e03fc0b')
b2sums=('db5ea91dafe1b9a1e0996a6f8b9124fd19c8d6c70113c1189cdbd12ef83247ac73ec37b4e86bbe1dc671bf3463c69bfe71b44763c6588f5d19a8db97dc1c09eb')

prepare() {
	cd frozendict-$pkgver
	# remove unneeded tests
	pushd test
	rm -v c_only.py test_frozendict_c.py test_frozendict_c_subclass.py \
	      test_coold.py test_coold_subclass.py
	popd
}

build() {
	cd frozendict-$pkgver
	python setup.py py build
}

check(){
	cd frozendict-$pkgver
	PYTHONPATH="$PWD/build/lib" pytest
}

package() {
	cd frozendict-$pkgver
	python setup.py py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
