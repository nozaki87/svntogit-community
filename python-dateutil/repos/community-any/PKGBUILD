# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil
pkgname=('python-dateutil' 'python2-dateutil')
pkgver=2.8.2
pkgrel=1
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-six' 'python2-six' 'git')
checkdepends=('python-pytest' 'python-freezegun' 'python-hypothesis')
source=(git+https://github.com/dateutil/dateutil.git#commit=6b035517571e63b6a63a493740c5506ec1e5da44?signed)
sha256sums=('SKIP')
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23') # Mario Corchero <mariocj89@gmail.com>

prepare() {
  cd "$srcdir"

  cp -r dateutil dateutil2
}

build() {
  cd "$srcdir"/dateutil
  export PYTHONHASHSEED=0
  python setup.py build

  cd "$srcdir"/dateutil2
  python setup.py build
}

check() {
  cd dateutil
  pytest -W ignore::pytest.PytestUnknownMarkWarning # https://github.com/pytest-dev/pytest/issues/5678
}

package_python-dateutil() {
depends=('python-six')
  cd "$srcdir"/dateutil
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil() {
depends=('python2-six')
  cd "$srcdir"/dateutil2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
