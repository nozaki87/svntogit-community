# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kompose
pkgver=1.23.0
pkgrel=1
pkgdesc="Docker compose to Kubernetes transformation tool"
arch=('x86_64')
url="https://github.com/kubernetes/kompose"
license=('Apache')
makedepends=('go' 'git' 'jq') # git binary is needed for test scripts
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kubernetes/kompose/archive/v${pkgver}.tar.gz")
sha512sums=('e59bf2f6d0c5137b024a0cb719db93f61b42a04d9f8154757e0332b53d3d3a446a2497df91f82b55bca5f5bc4873b96823ea4cb1f686f51cbf14157c28ba7d02')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/kubernetes "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/kubernetes/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/kubernetes/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -v .
  chmod +x ./kompose
}

# tests are failing right now due to a bug in script/cmd/cmd_test.go:28
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./kompose "${pkgdir}/usr/bin/kompose"
}
