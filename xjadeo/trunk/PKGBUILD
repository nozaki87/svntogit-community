# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.10
pkgrel=4
pkgdesc="A simple video player that is synchronized to jack transport"
arch=(x86_64)
url="https://github.com/x42/xjadeo"
license=(GPL2)
depends=(gcc-libs glibc imlib2 libglvnd libx11 libxpm libxv)
makedepends=(alsa-lib ffmpeg4.4 gendesk glu jack liblo libltc portmidi)
backup=(etc/xjadeorc)
options=(debug)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/x42/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('a76f3ae9c444e9302d9f639186a43221348163e4c83801d3cfcdfd1c8fe59089e3c2e04f1007a3066db4004d6efa2218a17348fe9b447a45d47c1803c6f5c11e')
b2sums=('6c9d5ef7d758369b42d3e0fd0838c2588423cb37033cd0aa4003c7c45a5c1230672b84c689b26520a809342e3174fc5ea7191d3958ed316b7d0a0674ef02776e')

prepare() {
  cd "${pkgname}-${pkgver}"
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
  autoreconf -fiv
}

build() {
  # not compatible with ffmpeg >= 5: https://github.com/x42/xjadeo/issues/50
  export PKG_CONFIG_PATH="/usr/lib/ffmpeg4.4/pkgconfig:$PKG_CONFIG_PATH"
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  depends+=(libasound.so libavcodec.so libavformat.so libavutil.so
  libfreetype.so libjack.so liblo.so libltc.so libportmidi.so libswscale.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # config
  install -vDm 644 doc/xjadeorc -t "${pkgdir}/etc/"
  # desktop file
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  # icon
  install -vDm 644 "src/${pkgname}/icons/xjadeoH128.png" "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
