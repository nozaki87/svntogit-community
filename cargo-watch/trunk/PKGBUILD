# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-watch
pkgver=8.0.0
pkgrel=1
pkgdesc="Utility for Cargo to compile projects when sources change"
url='https://github.com/passcod/cargo-watch'
arch=('x86_64')
license=('CC0-1.0')
depends=('cargo')
makedepends=('ruby-ronn')
source=("https://github.com/passcod/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e9e505a744e40da54215037669533bb4cd43f9e51b2f7df6439c693ce628d176ed35ceab9bcb7a9bdc293bc029e435646a2f1f281c46a694275dffe1e03a4bdd')
b2sums=('a55bba610b8463f2dca53b19a5ecd3d4a13cf257308b0a7f74ff925dbf45f1346a9438264ee73b27f1f4fca4192c7047003c2fac9b939b04bdaa945a3d76fd77')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
  ronn --roff cargo-watch.1.ronn
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_cargo-watch"
  install -Dm 644 cargo-watch.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
