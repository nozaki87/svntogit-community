# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgdesc="A simple Markdown translator"
pkgver=0.11.0
pkgrel=1
license=('ISC')
url="https://kristaps.bsd.lv/lowdown"
arch=('x86_64')
depends=('glibc' 'libmd')
makedepends=('git')
_commit='1d91c10351eeaa51340f1c2d22fa2920adc10e32' # VERSION_0_11_0
source=("$pkgname::git+https://github.com/kristapsdz/lowdown.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed -e 's/^VERSION_//' -e 's/_/./g'
}

prepare() {
  cd "$pkgname"

  # do not install static library
  sed -i '/^\t$(INSTALL_LIB) liblowdown.a/d' Makefile
}

build () {
  cd "$pkgname"

	./configure PREFIX=/usr MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	make
}

check () {
	make -C "$pkgname" regress
}

package () {
  cd "$pkgname"

  # package
	make DESTDIR="$pkgdir" install

  # symlink unversioned to versioned shared library
  local LIBVER=$(grep "^LIBVER" Makefile | sed "s/.*= //")
  ln -sf "/usr/lib/liblowdown.so.$LIBVER" "$pkgdir/usr/lib/liblowdown.so"

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
