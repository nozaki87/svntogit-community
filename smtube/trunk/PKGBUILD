# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Andreas Schnaiter <rc.poison@gmail.com>

pkgname=smtube
pkgver=21.7.0
pkgrel=1
pkgdesc="An application that allows to browse, search and play YouTube videos"
arch=('x86_64')
url="https://www.smtube.org/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-script' 'qt5-webkit')
makedepends=('qt5-tools')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'gnome-mplayer: play videos with GNOME MPlayer'
            'dragon: play videos with Dragon Player'
            'totem: play videos with Totem'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'youtube-dl: download videos')
source=("https://downloads.sourceforge.net/project/${pkgname}/SMTube/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('03f37f7cf3dedfb82b1659e5e6b97a71b8d62ba040426f104e4b50c2a385ec49569a2d8b475e7304a2d7ec28276c9be84da5d6e5df65c4c478f0b192d6e51357')
b2sums=('22d1efa04bd8987a6a2c7b4c55de411bc4c395059e784a605c3a8d37b6645c54bda0d58f87b7acef49484f546bcd1cf78d0b0bef40d8f5fa38877bfecbae0023')

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}/" install
}
