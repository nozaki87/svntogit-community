# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.5
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(alsa-lib cmake qt6-tools)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1913215144b2612afae8ac3557efa41a9614b8ba705037eca4340ed0ea6d51b35c9a0db7481ab647ca067699d8222aa362535f3f7d6842376a67ad386340095d')
b2sums=('605496dbc88b03b27f593ed202be122923c07d00fb41489f293c0ffdac9b3d533932c2404f552c9c4688d1fb11c63dbf1843283aaf41aac727fc5700afc45933')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "$pkgname-$pkgver/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
