# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=4.4.0
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('7a28f1379ec97874256c45f6f3edceab922336015d9a438b8958c0010011cbbfc3df49ac86f769c1d8089078eb0352e5a518ff8ee600be6ad1c0f7f27e984d23')

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
