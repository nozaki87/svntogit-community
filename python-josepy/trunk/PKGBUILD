# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=1.9.0
pkgrel=1
pkgdesc="JOSE protocol implementation in Python"
url="https://github.com/certbot/josepy"
license=('PerlArtistic')
arch=('any')
depends=('python-cryptography' 'python-pyopenssl' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/j/josepy/josepy-$pkgver.tar.gz")
sha512sums=('b3a64a579d23aa87323f01ae3859fcb2cd9a699f160e43ebe58c93e11e5d5c5031cd257af1fd0400d982929eb4c06495c9d4fa09580ffcb4f6a4c09d0f191bd8')

prepare() {
  # thou shalt not enforce coverage in distro packaging
  # https://github.com/certbot/josepy/issues/34
  rm josepy-$pkgver/pytest.ini
}

build() {
  cd josepy-$pkgver
  python setup.py build
}

check() {
  cd josepy-$pkgver
  python -m pytest
}

package() {
  cd josepy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
