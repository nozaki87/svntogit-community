# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libmusicxml
pkgver=3.21
pkgrel=1
pkgdesc="Portable C++ library around the MusicXML format"
arch=(x86_64)
url="https://github.com/grame-cncm/libmusicxml"
license=(MPL2)
depends=(gcc-libs glibc)
makedepends=(cmake)
provides=(libmusicxml2.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/grame-cncm/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('c594a61e63a8ddcd11fa95cdb44cf0bb2b9251104e7407d13f013f5a7fe9cafd7d76a6d80c3d907c7428af8604b4a2c9e811b515df1072d3923bbc997c9c4921')
b2sums=('a36c7babd8929d80883d5bad47bfda1cb65c140e22f06aca7f1c0eea3ae4e2eb70058726faec1b0a7b5acbc7559e7d8a0781da7ceff9a6d2fb3e666eb9fc86a5')

build() {
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -W no-dev \
    -B build \
    -S $pkgname-$pkgver/build
  make -C build
}

package() {
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/readme.md -t "$pkgdir/usr/share/doc/$pkgname/"
  mv -v "$pkgdir/usr/"{CHANGELOG.txt,README.html} "$pkgdir/usr/share/doc/$pkgname/"
  mv -v "$pkgdir/usr/share/$pkgname/doc/"*.pdf "$pkgdir/usr/share/doc/$pkgname/"
  rmdir -v "$pkgdir/usr/share/$pkgname/doc/"
}
