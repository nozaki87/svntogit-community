# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=40.3
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'edk2-ovmf' 'gtksourceview4' 'gtk-vnc' 'libarchive' 'libgudev' 'libhandy'
         'libosinfo' 'libsecret' 'libvirt-glib' 'mtools' 'qemu' 'spice-gtk' 'tracker3'
         'webkit2gtk')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('fff08e8f45a4be6d33b0a1a963acb3739ba395deb21590d438bb1ea6fbf74058')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
