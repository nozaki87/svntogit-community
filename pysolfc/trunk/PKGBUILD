# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc
pkgver=2.12.0
pkgrel=1
pkgdesc="PySol (Fan Club edition) is an exciting collection of more than 1000 solitaire games"
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('python-attrs' 'pysolfc-cardsets' 'python-configobj' 'python-pillow' 
         'python-pysol_cards' 'python-random2' 'python-six' 'tk')
makedepends=('python-setuptools')
source=(https://downloads.sourceforge.net/$pkgname/PySolFC-$pkgver.tar.xz)
sha256sums=('244dec417234397e104f2552667be4f8a11c9818059c03bbf0ae8febd25592ef')

prepare() {
  cd PySolFC-$pkgver

  sed -i 's/pysol.py/pysol/' data/pysol.desktop
}


package() {
  cd PySolFC-$pkgver

  python setup.py install --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/pysol.py "${pkgdir}"/usr/bin/pysol
}
