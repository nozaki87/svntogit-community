# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-server
pkgver=1.11.2
pkgrel=1
pkgdesc='Provides the backend for Jupyter web applications like Jupyter notebook, JupyterLab, and Voila'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-jupyter_client jupyter-nbformat jupyter-nbconvert python-terminado python-prometheus_client python-traitlets python-tornado
         python-jinja python-send2trash python-anyio python-jupyter_packaging python-websocket-client python-requests-unixsocket)
makedepends=(python-setuptools)
optdepends=('jupyter-server-mathjax: to use local MathJax')
source=(https://pypi.io/packages/source/j/${pkgname/-/_}/${pkgname/-/_}-$pkgver.tar.gz)
sha256sums=('c1f32e0c1807ab2de37bf70af97a36b4436db0bc8af3124632b1f4441038bf95')

build() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py build 
}

package() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 COPYING.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
