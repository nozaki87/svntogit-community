# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=opensubdiv
pkgver=3.4.4
pkgrel=5
pkgdesc="An Open-Source subdivision surface library"
arch=(x86_64)
url="http://graphics.pixar.com/opensubdiv"
license=('APACHE')
depends=('ptex' 'intel-tbb' 'libxcursor' 'xorg-xrandr' 'libxinerama')
makedepends=('cmake' 'doxygen' 'glfw' 'glew' 'python' 'python-pygments' 'python-docutils' 'opencl-headers' 'cuda')
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz"
        python3.patch
        cuda11.patch)
sha512sums=('fc8f28b79347015c8991150535c1339e695d96947c72fadd4fa27b546a0813c1125cd175ee03bed5aacdb3609f74c4e526ef70103d1195ba9f7df041e73ea9fb'
            '10f1f63e07d59deb4d91ba04448bb360efff0e41202bb6737caffbce09ba244efb349fe25b24880420b80fdadb268aed84923464af0d66ee1a36e4ba8f96cfd5'
            'a9f15c1c2c66d5ecfd4b9b9e8f5a5a455488c5745def284fc5f1ac22f4429974de2b85c6da5289c0290ec9a02c2e2d022fcd760e5d37f532a0cd8c479c7f2d1a')

prepare() {
  cd "OpenSubdiv-${pkgver//./_}"

  patch -Np1 -i "${srcdir}"/python3.patch
  patch -Np1 -i "${srcdir}"/cuda11.patch
  mkdir build
}

build() {
  cd "OpenSubdiv-${pkgver//./_}"/build

  # wtf
  mkdir -p CMakeFiles/osd_static_gpu.dir/osd

  cmake .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DOSD_CUDA_NVCC_FLAGS='--gpu-architecture=compute_52' \
      -DCUDA_HOST_COMPILER=/usr/bin/g++ \
      -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "OpenSubdiv-${pkgver//./_}"/build

  DESTDIR="$pkgdir/" make install

  rm -rf "${pkgdir}"/usr/bin
}
