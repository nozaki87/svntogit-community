# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jo√£o Vitor S. Anjos <jvanjos at protonmail dot com>

pkgname=jless
pkgver=0.7.2
pkgrel=2
pkgdesc='A command-line pager for JSON data'
arch=('x86_64')
url="https://github.com/PaulJuliusMartinez/jless"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
_commit='19e0e8747ad7d1dfae395c027ffcda578adb7e5c'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/jless

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
