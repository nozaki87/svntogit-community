# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gnome-shell-extension-appindicator
pkgver=40
pkgrel=2
pkgdesc='AppIndicator/KStatusNotifierItem support for GNOME Shell'
url='https://github.com/ubuntu/gnome-shell-extension-appindicator'
arch=('any')
license=('GPL')
depends=('gnome-shell>=3.34')
makedepends=('zip')
optdepends=(
  'libappindicator-gtk2: support GTK+2 applications'
  'libappindicator-gtk3: support GTK+3 applications'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ubuntu/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('4ababf6ba4dbfd5af62e4539499c0e7a717d1873295733c36beb5396dbfaf7ed')

build() {
  cd "${pkgname}-${pkgver}"
  make zip-file
}

package() {
  cd "${pkgname}-${pkgver}"
  local dest="${pkgdir}/usr/share/gnome-shell/extensions/appindicatorsupport@rgcjonas.gmail.com"

  install -d "${dest}"
  bsdtar --no-same-owner -xvf build/appindicator-support.zip -C "${dest}"
}

# vim: ts=2 sw=2 et:
