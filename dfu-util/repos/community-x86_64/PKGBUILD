# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Ã˜yvind 'Mr.Elendig' Heggstad <mrelendig@har-ikkje.net>
# Contributor: Fabrizio Antonangeli <fabrizio.antonangeli@gmail.com>

pkgname=dfu-util
pkgver=0.10
pkgrel=3
pkgdesc='Tool intended to download and upload firmware using DFU protocol to devices connected over USB'
url='http://dfu-util.sourceforge.net/'
license=(GPL2)
arch=(x86_64)
depends=(libusb python)
optdepends=('python-intelhex: Intel HEX file format support')
source=(http://dfu-util.sourceforge.net/releases/$pkgname-$pkgver.tar.gz)
sha256sums=('a03dc58dfc79c056819c0544b2a5970537566460102b3d82cfb038c60e619b42')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm755 dfuse-pack.py "$pkgdir"/usr/bin/dfuse-pack
  install -Dm644 doc/60-dfuse.rules "$pkgdir"/usr/lib/udev/rules.d/60-dfuse.rules
}
