# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=frescobaldi
pkgver=3.1.3
pkgrel=5
pkgdesc="A LilyPond sheet music text editor"
arch=(any)
url="http://www.frescobaldi.org/"
license=(GPL)
depends=(
  python python-pyqt5 python-pyqt5-webengine python-ly poppler
  python-poppler-qt5 qt5-base qt5-svg qt5-webkit
)
makedepends=(python-setuptools)
optdepends=(
  'lilypond: music engraving'
  'python-pygame: MIDI input and playback'
  'python-pycups: printing to a CUPS server'
  'hyphen-lang: hyphenation patterns for desired languages'
)
_url=https://github.com/frescobaldi/frescobaldi
# Cherry pick Python 3.10 related patches since lasat tagged release
# https://github.com/frescobaldi/frescobaldi/issues/1398
_patches=(51886dba8a3cb3db6eba9a6cf1d65bd8eaf877aa.patch
          bd4dfe036b8dbc5d75552fe6b6b081c024e5beea.patch
          75dd43f86f45eb06e3af287a26c18c2be7d328ac.patch
          1d538efa511676ab80594273c0247e94ac3a4927.patch
          23ebdadc59d5e52c3a514814cf5858450cdcdd2c.patch
          0c3ccac837883de2110c9290e8c34e24f44dc7b1.patch)
source=(${_url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${_patches[@]/#/$_url/commit/})
sha256sums=('9c7f5036f367691454753f1db06ea02c33501c1e4f24bfb47485103911f3e9c1'
            '700b5ee43ff85bcedbe374415bcb0dd36f784b1d170323d32c14b32038a61bda'
            'd640b1aeafddab7f7ffd2195bb896b1c2b94d0fc51624bc782a265b48206e3e6'
            '9817b922812cec024d05f353ad0d5ade227244ff510606556e5c1bfc1acc417f'
            'b6816e211b10dee48832bdd381d1da19c61c4355b370968a3cacd3c6de12d241'
            '596f501b007170a1d13823dee862f8a0ef14984b4ac8211b9961f9f2153c74c6'
            '37e7006075d684aabb6b53a7c6bfa553739f6b768e76154dbde7dfaad6a24c03')

prepare() {
  cd ${pkgname}-${pkgver}
  # Provided by hyphen-*
  rm -f frescobaldi_app/hyphdicts/hyph_*.dic
  rm -f frescobaldi_app/hyphdicts/README*
  for p in ${_patches[@]}; do
    patch -p1 < ../$p
  done
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --skip-build --optimize=1
}
