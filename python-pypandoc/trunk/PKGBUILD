# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pypandoc
pkgver=1.7.2
pkgrel=1
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('pandoc')
makedepends=('python-setuptools')
checkdepends=('texlive-core' 'texlive-latexextra')
source=("https://github.com/bebraw/pypandoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e2c5de6d56781484c306214a8757b18f7aa23e64489041ae3876ef8b2c0bf8771cc48246ecc97bf8ae67762364e35d9af5212ca6549f7b81ee16d75b1a85557e')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "/install_requires = /d" pypandoc-$pkgver/setup.py
}

build() {
  cd pypandoc-$pkgver
  python setup.py build
}

check() {
  cd pypandoc-$pkgver
  python setup.py test
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
