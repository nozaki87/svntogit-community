# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='loofah'
pkgname="ruby-${_gemname}"
pkgver=2.13.0
pkgrel=1
pkgdesc='HTML/XML manipulation and sanitization based on Nokogiri'
arch=('any')
url='https://github.com/flavorjones/loofah'
license=('MIT')
options=(!emptydirs)
depends=('ruby' 'ruby-crass' 'ruby-nokogiri')
checkdepends=('ruby-minitest' 'ruby-rake' 'ruby-rr')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('da72bb8b98fd1ad91ef22165d71c035c927e0cf4cf8a2f7ca4857a9fccd1ecce902cad90c12b7d34f351f2bc438c2c421e957681a0d152e065dc54a04cd4f1f1')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # no need for markdown beautifying
  sed --in-place --regexp-extended '/hoe/Id' Rakefile "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 CHANGELOG.md README.md SECURITY.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
