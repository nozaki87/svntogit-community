# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.24
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(x86_64)
url="https://samplv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base)
makedepends=(alsa-lib cmake jack liblo libsndfile lv2 qt6-tools)
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application'
            'qt6-wayland: for native wayland support')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('572ea83e77e8cffa4679b9ffe7db0d1ce862e8adbbee4d4cfafae4bc79348e58f2f3228db2654436d07417fb0b78d9916c8f0757828379b45f396ad61364b0d0')
b2sums=('d37d9f97dcf04805b4bedde6d7bd642e93445e63a0037df64b6d5b819066ff148782d8997308b35d1236fdb9844f531af665f23051f6abce9b24672dbbcfef9b')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libsndfile.so)

  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644  "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
