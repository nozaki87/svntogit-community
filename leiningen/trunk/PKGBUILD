# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.7
pkgrel=1
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment java-runtime)
makedepends=(git)
optdepends=('leiningen-completions: completion scripts'
            'rlwrap: for readline support')
source=("git+$url#commit=7da7f3c3316a0737ec627a55ce794b30cc206a10" # tag: 2.9.7
        "$url/releases/download/$pkgver/leiningen-$pkgver-standalone.jar"{,.asc})
validpgpkeys=('6A2D483DB59437EBB97D09B1040193357D0606ED') # Phil Hagelberg <phil@hagelb.org>
b2sums=('SKIP'
        '1e0adb509eb514f6c034d072710301f3de6387d5cb3b587f9680ea56748ad6d81411b9b62c0db8b49101fd1f6eb4864f0f69469fb5d88f576feefa6aa90a865e'
        'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.jar" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
