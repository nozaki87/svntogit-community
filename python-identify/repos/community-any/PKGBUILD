# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.3.4
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/pre-commit/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/pre-commit/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ef20744faa05efd084fb7c9715bc1288845047f9092b8559e3dbc4f7b51dcd715caeb269eba36e4e25551ffab1cd9a84f171ea020604f6b1ba4d9215486ef181')
b2sums=('2ba95b7d2c8ba07100416dd4109263cd38bec327b1f79ea0b3be344dcdcbea62edd81246f36e9b3e91ddb727ff9ad77a96bba4f668dfca030c2ca2469ab25676')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
