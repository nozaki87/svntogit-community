# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

_name=pytesseract
pkgname=python-pytesseract
pkgver=0.3.9
pkgrel=1
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-packaging' 'python-pillow' 'tesseract')
makedepends=('python-build' 'python-install' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("$url/archive/v$pkgver/$_name-v$pkgver.tar.gz")
b2sums=('2b1c54df34d9717ffe342974c622f2f262a4f2f11d26512cad5a0a148c60f554ac02efba15c588bfbedc82d52eaf20c8f471d8d8d5fff8125544f10468483ad0')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m install --optimize=1 dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $_name-$pkgver
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl

  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/$_name
}

# vim:set ts=2 sw=2 et:
