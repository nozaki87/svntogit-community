# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgname=python-bcrypt
pkgver=3.2.1
pkgrel=1
pkgdesc="Modern password hashing for your software and your servers"
arch=('x86_64')
url="https://github.com/pyca/bcrypt"
license=('Apache')
depends=('python-cffi' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyca/bcrypt/archive/$pkgver.tar.gz")
sha512sums=('3e10b44efad3c41adfc29b39fa8a63b0cd31d687fba4ca43ea420fdee5454e580351389a55ceaa35d62431c2a681c296b1cba4d3861587c9acce6bb063b045fd')

build() {
  cd bcrypt-$pkgver
  python setup.py build
}

check() {
  cd bcrypt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10" pytest
}

package() {
  cd bcrypt-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
}
