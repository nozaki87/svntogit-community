# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.5
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libgig libgig.so liblscp liblscp.so
linuxsampler qt6-base)
makedepends=(cmake qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2f79e4928ccb2cecafe63fc0b35349ed2ad4b26fe4436bdea0446f1d54a61478dbd5df87445bdd77c5ffd6c5e93e478c90b1576f4c1105276abe34a99b6a30e3')
b2sums=('0051fc3bee7b3c544336b68ae36acbab265eb5dfd7750d525402d5ffea6ed69481fcccd77ba54df37c4eb2305ad25a6a56181aa4fa12d6a3afa86e012be5cb84')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
