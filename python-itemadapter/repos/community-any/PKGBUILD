# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.3.0
pkgrel=1
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-attrs' 'python-pydantic' 'python-pytest' 'scrapy')
source=("https://github.com/scrapy/itemadapter/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2bc26471d65cf66da520461a54bd9e9943ddcd910cfa359413be77ad92a73f1dfc3c6d47be2c6ce8ce22d878172e4e93f0fef0dc4a98d824bacd3589a3a49315')

build() {
  cd itemadapter-$pkgver
  python setup.py build
}

check() {
  cd itemadapter-$pkgver
  pytest
}

package() {
  cd itemadapter-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
