# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.9.6
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(cmake fluidsynth qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
options=(debug)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4f596fefda29fad9298539aaf83bdcc699846416d10b1259041a414f3d89ecc76a6cad338742bac8ffa0ed78ff462fe70147e1ef6437c20241598bd7a1d41a84')
b2sums=('daeb82f102a9a754b708b3d3ebc1963a1cf08180ec16971b4b6fb2ea3b35d7f1ca3a663bd9381d4c90052e1c781a6fef64c4cbf0d456f47ac12791da485979ea')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfluidsynth.so)

  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
