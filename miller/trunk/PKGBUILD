# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=miller
pkgver=5.10.2
pkgrel=1
pkgdesc="Name-indexed data processing tool"
arch=('x86_64')
url="https://github.com/johnkerl/miller"
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('73daf0ca8a5417890e060b04103c4542f1122c13a9a13271dc53e94c4fdf0610dcad5ab04729326fd4ab72b47673db9fdf5aacc5a4ca5330c3220046e4d8a38b')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
