# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flexmock
pkgver=0.11.2
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="https://flexmock.readthedocs.io"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-teamcity-messages' 'python-testtools' 'python-twisted')
source=("https://github.com/flexmock/flexmock/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('cba7bef955bf5241b14dfa4672579b7ad32eb9b1a40f9ea89a0e78d63f9086441695367aedbeb9851d93aa7cfdec5cacd518d6c472c19cf19e509884ff0be6dd')

export LC_CTYPE=en_US.UTF-8

build() {
  cd flexmock-$pkgver
  python setup.py build
}

check() {
  cd flexmock-$pkgver
  PYTHONPATH="$PWD"/build/lib make test
}

package() {
  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
