# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flexmock
pkgver=0.10.9
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="https://has207.github.com/flexmock"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bkabrda/flexmock/archive/$pkgver.tar.gz")
sha512sums=('ee69e23f3c944dda7f83142fdd9d5af38fc7de947cf3f925171d1ff30c59b5ab787a977fd9896febc946d3e6adbb064ae39ff843b1b8c80b943d635d808a8ce6')

export LC_CTYPE=en_US.UTF-8

build() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py pytest
}

package() {
  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
