# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flexmock
pkgver=0.10.10
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="https://has207.github.com/flexmock"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bkabrda/flexmock/archive/$pkgver.tar.gz")
sha512sums=('23d4222e71251b693ba33915122b824eacccabe08ebbc76d2e30d59a839a91c7b2718fb4722fb13756c5af3aebd167497bdec147f90ed9c1293706a532725781')

export LC_CTYPE=en_US.UTF-8

build() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py pytest
}

package() {
  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
