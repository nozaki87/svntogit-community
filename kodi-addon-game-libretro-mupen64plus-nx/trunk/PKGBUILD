# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-game-libretro-mupen64plus-nx
epoch=1
pkgver=2.2.0.16
_codename=Matrix
pkgrel=1
pkgdesc="Nintendo 64 mupen64plus-next for kodi"
arch=('x86_64')
url='https://github.com/kodi-game/game.libretro.mupen64plus'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-game')
makedepends=('cmake' 'kodi-dev' 'kodi-addon-game-libretro' 'libretro-mupen64plus-next')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kodi-game/game.libretro.mupen64plus/archive/$pkgver-$_codename.tar.gz")
sha512sums=('e58c6b9573478cf7319c24f6a8d35cc6d4d2a731e0001479ef00c11e9f9081f20eb891a9488f7fb9f54d2adb24bae9f8f82904b47291bd358940bd1c4c6f6ebb')

build() {
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
    sed -e 's/mupen64plus_libretro/mupen64plus_next_libretro/g' \
        -i CMakeLists.txt
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi-addon-game-libretro' 'libretro-mupen64plus-next')
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
	make DESTDIR="$pkgdir/" install
}

